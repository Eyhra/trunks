function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	switch(.@welcome) {
	default:
		mes "[Funcionária Kafra]";
		mes "Bem vind" + ((Sex) ? "o":"a") + " à Corporação Kafra. Os serviços da Corporação Kafra sempre estarão ao seu lado. Como posso lhe ajudar ?";
		break;
	case 1:// Niflheim mensagem específica	
		mes "[Funcionária Kafra]";
		mes "^666666B-Be-Beeeemmm V-vindooo";
		mes "a c-cooorpoor-r-raaçãaaoo";
		mes "K-k-Kafraaaa...^000000";
		break;
	case 2:// Castelo - Mensagem Kafra
		mes "[Funcionária Kafra]";
		mes "Bem vindo membro da guilda ^ff0000" + getguildname( getcharid(2) ) + "^000000.";
		mes "A Corporação Kafra, está aqui para atender você.";
		break;
	case 3:// Amatsu mensagem específica
		mes "[Funcionária Kafra]";
		mes "Então, você vem de uma terra distante para estudar nossa cultura, ou você só está observando?";
		mes "Em ambos os casos, porque não permanecer por algum tempo?";
		mes "Com a eterna fragância do aroma de flores silvestres.";
		break;
	case 4:	
		mes "[Funcionária Kafra]";
		mes "Com tantas Kafras espalhadas, você nunca estará longe de casa.";
		break;
	case 5:// O NPC tem sua própria mensagem de boas vindas. (Exibe nada)
		break;
	}
	next;
	if (.@welcome == 2)
		setarray .@K_Menu0$[0], "Usar Armazém", "Usar Armazém da Guilda", "Alugar Carrinho", "Usar Serviço de Teleporte", "Cancelar";
	else {
		switch(.@menu_num) {
			// Opção de Salvar e Usar Armazém.
			case 1:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
			// Apenas a opção usar Armazém.
			case 2:	setarray .@K_Menu0$[0],"Usar Armazém","Cancelar"; break;
			// Sem a opção Teleporte (Common)
			case 3: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Checar outra informação","Cancelar"; break;
			// Case 4 is Einbroch no tele message.
			// Sem as opções Salvar, ou Teleporte. (Common)
			case 5:	setarray .@K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Checar outra informação","Cancelar"; break;
			// Apenas Armazém e Chegar outras informações.
			case 6:	setarray .@K_Menu0$[0],"Usar Armazém","Checar outra informação","Cancelar"; break;
			// Salvar, Armazém, e Alugar Carrinho (Kafra Warehouse)
			case 7:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar"; break;
			// Salvar, Armazém, Chegar outras informações. (Turbo track)
			case 8: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Checar outra informação","Cancelar"; break;
			// No Save (Rune Knight)
			case 9: setarray .@K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Usar Serviço de Teleporte","Checar outra informação","Cancelar"; break;
			// Armazém, Salvar, e Alugar Carrinho (Dewata, reorder of case 7)
			case 10: setarray .@K_Menu0$[0],"Usar Armazém","Salvar","Alugar Carrinho","Cancelar"; break;
			// Mensagens padrão (Obsolete)
			default: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teleporte","Alugar Carrinho","Checar outra informação","Cancelar"; break;
		}
	}
	.@menu$ = implode(.@K_Menu0$,":");
	while (true) {
		.@j = select(.@menu$) - 1;
		if (.@K_Menu0$[.@j] == "Salvar")
			return;
		else if (.@K_Menu0$[.@j] == "Usar Armazém") {
			if (.@welcome == 2)// Sem Cobrança para usar Armazém do Clã
				callfunc "F_KafStor",2,0,0;
			else
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		}
		else if (.@K_Menu0$[.@j] == "Usar Serviço de Teleporte") {
			if (.@menu_num != 4)// Exibir Einbroch "Sem aviso de serviço de teleporte".
				callfunc "F_KafTele",.@welcome;
			else {
				mes "[Funcionária Kafra]";
				mes "Por causa do ^FF0000Acordo de limite de transporte^000000, a Corporação Kafra não pode fornecer serviços de teletransporte aqui na república de Schwartzwald.";
				next;
				mes "[Funcionária Kafra]";
				mes "Nós solicitamos a você utilizar o serviço de Aeroplano no lugar do teletransporte. Agradecemos sua compreensão e cooperação.";
			}
			next;
		}
		else if (.@K_Menu0$[.@j] == "Alugar Carrinho") {
			if (callfunc("F_KafCart",.@welcome,getarg(4)) == 1)
				next;
		}
		else if (.@K_Menu0$[.@j] == "Checar outra informação") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (.@K_Menu0$[.@j] == "Cancelar") {
			callfunc "F_KafEnd",.@welcome,0;
			end;
		}
		else if (.@K_Menu0$[.@j] == "Usar Armazém da Guilda") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Função do Armazém
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Desabilitado para acesso ao Armazém da Guilda (Ocupado).
	if (.@type == 1) {
		if (guildopenstorage()) {
			mes "[Funcionária Kafra]";
			mes "Sinto muito, mas outro membro do Clã está usando o armazém neste instante.";
			mes "Por favor, aguarde até que esta pessoa termine.";
			close3;
		}
		cutin "", 255;
		close;
	}
	// Desabilitado para acesso ao Armazém Normal (Nível de Habilidades Básicas Insuficiente).
	if (!callfunc("F_CanOpenStorage")) {
		mes "[Funcionária Kafra]";
		// Niflheim mensagem específica.
		if (getarg(2) == 1) {
			mes "^666666S-D-ddeessssculllpppe v-vo-voooccêe éee umm Appreenddizz...";
			mes "P-Prreeecisa teerrr nívveelll d-de hh-habibilidaddeee level 6...^000000";
			return;
		}
		mes "Lamento, mas você precisa ser um Aprendiz com nível mínimo de habilidade level 6 para usar o armazém.";
		return;
	}
	// Armazém Normal.
	if (.@type != 2) {
		// Vale-Armazém
		if (countitem(7059))
			delitem 7059,1;
		else {
			if (Zeny < .@fee) {
				mes "[Funcionária Kafra]";
				// Niflheim mensagem específica.
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "P-Preeciso de Z-Zeny...!";
					mes "P-Preciiiso de 150... Zenys...";
					mes "Ergh!!! T-tomando s-angue!";
					return;
				}
				// Mensagem Padrão.
				mes "Lamento, mas você não possui";
				mes "Zenys suficientes para usar";
				mes "o serviço de armazém.";
				mes "Para acessar o armazém o custo é " + .@fee + " zeny.";
				return;
			}
			Zeny -= .@fee;
			RESRVPTS += (.@fee/5);
		}
	}

	if (.@type != 2) {
		// Niflheim Mensagem Específica.
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Funcionária Kafra]";
			for ( .@i = 0; .@i < 5; .@i++ )
				mes "^666666Obrigado.. por... usar...^000000";
		}
		// Mensagem Normal
		else {
			mes "[Funcionária Kafra]";
			mes "Aqui, deixe-me abrir seu Armazém para você.";
			mes "Obrigado por usar o Serviço Kafra.";
		}
	}
	callfunc("F_CheckKafCode");	// Verifique sua senha do Armazém, se definido
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Função de Teletransporte
//===================================================
function	script	F_KafTele	{
	mes "[Funcionária Kafra]";
	mes "Por favor, escolha seu destino.";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Cancelar")
		return;
	// Vale-Teletransporte
	// Não consuma no castelo da guilda
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes "[Funcionária Kafra]";
			mes "Sinto muito, mas você não possui Zenys para teleportar.";
			mes "O serviço de teleporte ";
			mes "para " + @wrpD$[@num] + "custa" + @wrpP[@num] + " Zenys.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") {
		if (checkre(RENEWAL) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 131, 87;
	}
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mina Abandonada de Mjolnir") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Caverna dos Orcs") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Função do Carrinho
//===================================================
function	script	F_KafCart	{
	if (BaseClass != Job_Merchant) {
		mes "[Funcionária Kafra]";
		mes "Eu lamento, mas o serviço de aluguel de carrinho";
		if (checkre(RENEWAL) == true)
			mes "é disponível somente para Mercadores.";
		else {
			mes "é disponível somente para Mercadores, Ferreiros, Mestre Ferreiros, Alquimistas e Criadores.";
		}
		return 1;
	}
	else if (checkcart() == true) {
		mes "[Funcionária Kafra]";
		mes "Você já possui um carrinho equipado.";
		mes "Infelizmente, nós não podemos alugar mais de um carrinho por vez.";
		return 1;
	}
	else if (getskilllv("MC_PUSHCART") == 0) {
		mes "[Funcionária Kafra]";
		mes "Você só pode alugar um carrinho depois de aprender a \"Usar Carrinho\" habilidade.";
		return 1;
	}
	// Vale-Carrinho
	// Não consuma no castelo da guilda
	if (countitem(7061) > 0 && getarg(0) != 2)
		delitem 7061,1;
	else {
		.@rental_fee = getarg(1);
		mes "[Funcionária Kafra]";
		mes "O aluguel do carrinho de mão";
		mes "custará " + .@rental_fee + " Zenys.";
		mes "Você deseja alugar um carrinho de mão?";
		next;
		if (select("Alugar Carrinho.:Cancelar") == 2)
			return 0;
		if (Zeny < .@rental_fee) {
			mes "[Funcionária Kafra]";
			mes "Lamento, mas você não possui Zenys suficientes para alugar um carrinho.";
			mes "O aluguel custa " + .@rental_fee + " zenys.";
			return 1;
		}
		Zeny -= .@rental_fee;
		RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Função - Pontos de Reserva
//===================================================
function	script	F_KafInfo	{
// Descomente a próxima linha para remover a Proteção Kafra
//	.@block = 1;
	if (getarg(0) == 2)// Port Malaya, Rune Knight
		setarray .@menu$[0], "Checar reserva de pontos especiais.", "", "", "Cancelar";
	else if (.@block)
		setarray .@menu$[0], "Checar reserva de pontos especiais.", "", "Localização das Funcionárias Kafra", "Cancelar";
	else
		setarray .@menu$[0], "Checar reserva de pontos especiais.", "Serviço de Senha do Armazém", "Localização das Funcionárias Kafra", "Cancelar";
	.@menu_list$ = implode(.@menu$,":");
	while (true) {
		.@j = select(.@menu_list$) - 1;
		switch(.@j) {
		case 0:// Checar reserva de pontos especiais.
			mes "[Funcionária Kafra]";
			mes "Vamos ver...";
			mes strcharinfo(0) + "...";
			mes "Ah, você tem um total de ";
			mes RESRVPTS+ " reservas de pontos especiais.";
			next;
			mes "[Funcionária Kafra]";
			mes "Você pode trocar suas reservas de pontos especiais por recompensas no escritório da Corporação Kafra em Aldebaran.";
			mes "Por favor, utilize nossos serviços convenientes receber os benefícios do nosso programa de recompensas.";
			if (getarg(0) == 1)
				return;
			next;
			break;
		case 1:// Serviço de Senha do Armazém
			callfunc("F_SetKafCode","[Kafra Employee]","Serviços Kafra");
		case 2:// Localização das Funcionárias Kafra
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			break;
		default:
			return;
		}
	}
}

// === Função Final ==================================================
//  arg(0): usado para determinar qual mensagem será exibida.
//  arg(1): usado para determinar se a mensagem de salvar será exibida.
//  arg(2): usado para exibir o nome da área de salvamento.
//===================================================================
function	script	F_KafEnd	{
	.@type = getarg(0);
	mes "[Funcionária Kafra]";
	if (getarg(1) == true) {// Salvar
		mes "Seu ponto de retorno";
		mes "foi salvo aqui " + getarg(2) + ".";
		mes "Muito obrigado por usar os serviços da Corporação Kafra.";
	} else if (.@type == 0 || .@type == 5) {// Generic End
		mes "Nós da Corporação Kafra, sempre estamos nos esforçando para proporcionoar os melhores serviços. Esperamos satisfazer as suas necessidades de aventureiro nos seus padrões de excelência.";
	} else if (.@type == 1) {// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra n-nunca mooooorrrrre.";
		mes "Semprre... es-ta  ao seeeuuu laaadooo~^000000";
	}
	close3;
}

//===================================================
// Função de Verificar o Armazém
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Digite a senha do armazém:";
	input .@code_;
	if (.@code_ != #kafra_code) {
		dispbottom "Senha errada.";
		close3;
	}
	@kafcode_try = 0;
	return;
}

// === Set / Mudar / Limpar Senha do Armazém ===
// getarg(0): Nome do NPC
// getarg(1): Nome da Compania
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if (#kafra_code == 0) {
		mes "" + .@comp_name$ + "orgulhosamente apresenta um novo serviço:";
		mes "Proteção adicional do seu armazém com senha.";
		next;
		if (select( "Definir nova senha -> 5000z", "Cancelar" ) == 1)
			callsub( S_SET, .@npc_name$, .@comp_name$ );
		close3;
	}
	mes "Seu armazém está protegido com uma senha. O que você fará agora?";
	next;
	switch( select( "Mudar senha antiga -> 5000z", "Remover senha do Armazém -> 1000z", "Cancelar" ) ) {
	case 1:
		mes .@npc_name$;
		mes "Primeiro, digite sua ^0000FFsenha antiga^000000.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code || .@code != #kafra_code) {
			mes "Senha errada. Você não pode inserir uma nova senha.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		callsub( S_SET, .@npc_name$, .@comp_name$ );
		break;
	case 2:
		mes .@npc_name$;
		mes "Por favor, entre com a senha antes de sua retirada.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code) {
			mes "A senha não foi removida.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		mes .@npc_name$;
		if (Zeny < 1000) {
			mes "Você não possui Zeny suficiente.";
			emotion ET_MONEY;
			break;
		}
		Zeny -= 1000;
		if (.@code == #kafra_code) {
			#kafra_code = 0;
			mes "Você teve êxito ao colocar senha em seu armazém.";
			mes "Muito obrigado por usar os " + .@comp_name$ + ".";
			emotion ET_THANKS;
		} else {
			mes "Senha errada! Não devolverei seus 1000 Zenys.";
			mes "Por favor, na próxima entre com a senha correta.";
			emotion ET_SORRY;
		}
		break;
	case 3:
		break;
	}
	close3;

S_SET:
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	mes "Agora entre com sua ^FF0000nova senha^000000 para proteger seu armazém de roubos.";
	.@code = callfunc("F_EntKafCode");
	if (!.@code) {
		mes "A senha não foi trocada.";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if (Zeny < 5000) {
		mes "Você não possui Zeny suficiente.";
		emotion ET_MONEY;
		return;
	}
	Zeny -= 5000;
	#kafra_code = .@code;
	mes "Seu armazém está protegido com uma senha secreta.";
	mes "Obrigado por usar" + .@comp_name$ + ".";
	emotion ET_THANKS;
	return;
}

//===================================================
// Função Básica de Validação - Senha Função
//===================================================
function	script	F_EntKafCode	{
	mes "Digite um número entre 1000~10000000:";
	@kafcode_try += 1;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Tentou colocar senha de armazenamento.";
		return 0;
	}
	if (input(.@code_) == 1) {
		mes "Você não pode usar uma senha tão grande.";
		return 0;
	}
	if (.@code_ < 1000) {
		mes "Você não pode usar uma senha tão curta.";
		return 0;
	}
	return .@code_;
}

//===================================================
// Portais
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Caverna dos Orcs", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mina Abandonada de Mjolnir";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Caverna dos Orcs", "Mina Abandonada de Mjolnir";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancelar";
	return;
}
