//===== rAthena Script ======================================= 
//= Player Marriage
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Official Marriage script.
//= Added isloggedin() check, even though Aegis doesn't do it.
//= Currently does not support same-Sex marriages.
//= Old script located in: npc/custom/marriage.txt
//= - Variable in use: wedding_sign (max 1)
//= - Variable in use: $@wedding (max 1) 
//= - Variable in use: $@wed_groom$ $@wed_bride$
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Fixed a missing ")" [L0ne_W0lf]
//= 1.2 Corrected duration of Wedding status. [L0ne_W0lf]
//= 1.3 Changed permanent global variables to temporary ones. [Euphy]
//=     Added GM NPCs (commented).
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added Marriage Covenant item and VIP features. [Euphy]
//============================================================

prt_church,97,100,4	script	Assistente de Casamento#w	71,{
	cutin "wedding_marry01",2;
	if (countitem(6026) == 0) {

		// Se o sistema VIP estiver ativado:
		// - Jogadores não VIP devem usar uma Aliança de Casamento para se casar.
		// - Os jogadores VIP podem pagar a taxa Zeny ou usar uma Aliança de Casamento.
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Desculpe, mas você não pode se casar agora.";
			mes "O casamento está disponível apenas para jogadores inscritos.";
			mes "Mas se você tem um ^FF0000Pacto de casamento^000000, então você poderá se casar!";
			goto L_End;
		}

	} else
		set .@covenant,1;
	if (Upper == 2) {
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Olá~";
		mes "Eu sou a Assistente de Casamento, e estou aqui para lhe fornecer informações relacionadas ao casamento. Agora, você tem alguma dúvida?";
		next;
		if (select("^3CB371[»]^000000 Eu quero me casar.:^ff0000[»]^000000 ^777777Eu não preciso de sua ajuda!^000000") == 1) {
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Oh, me desculpe, mas personagens adotados não podem se casar. Por enquanto, por que você não aproveita os prazeres simples da infância?";
			goto L_End;
		}
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Ah, claro que não! Crianças pequenas não podem se casar - há muitas leis contra isso~ Você não é a coisinha mais adorável?";
		goto L_End;
	}
	mes "^0000FF[Assistente de Casamento]^000000";
	mes "O casamento é a bela união de duas almas que escolheram ficar juntas para sempre, para compartilhar sua alegria e suas vidas.";
	mes "Existe alguém especial assim em sua vida?";
	next;
	switch(select("^3CB371[»]^000000 Pergunte sobre a cerimônia de casamento:^3CB371[»]^000000 Pergunte sobre o Procedimento:^3CB371[»]^000000 Candidate-se a Casamento:^ff0000[»]^777777 Somos do Exército dos Solteiros!^000000")) {
	case 1:
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "O sábio e benevolente rei Tristão III costumava realizar cerimônias de casamento, mas não é mais capaz de fazê-lo por causa de seus deveres reais e agenda frenética.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "O bispo Vomars, o bispo do amor, é agora o oficiante da cerimônia de casamento.";
		mes "Ele é realmente um tesouro para o Reino Rune-Midgarts.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Quando você se casa com alguém, é para o resto da vida, então pense bem antes de fazer ou aceitar uma proposta de casamento.";
		mes "Tenha em mente que um homem só pode se casar com uma mulher e vice-versa.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Se você tiver a sorte de encontrar alguém com quem realmente queira passar o resto da vida, talvez queira fazer a pergunta. Espero que todos encontrem seu par perfeito~";
		break;
	case 2:
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "A primeira parte do procedimento de casamento é completar o pedido.";
		mes "tenham terminado de se candidatar, eles devem formar um grupo de dois.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Depois de formar um grupo de dois, o casal deve então falar com o bispo Vomars. O noivo fala primeiro e deve dizer o nome exato de sua noiva ao bispo. Caso contrário, a cerimônia será interrompida.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Depois, a noiva falará com o Bispo e lhe dirá o nome de seu noivo.";
		mes "Se esses nomes forem informados corretamente ao Bispo, eles poderão trocar anéis.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Uma vez que os anéis de casamento são trocados, o casal está para sempre ligado em matrimônio. Claro, antes deste ponto, há muitas chances de mudar de idéia, então...";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Se houver muitos casais que desejam se casar ao mesmo tempo, por favor, forme uma linha e fale com o Bispo Vomars em ordem, pois apenas um casal pode se casar de cada vez.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Finalmente, certifique-se de dizer ao Bispo Vomars o nome exato do seu parceiro sem perder muito tempo. Se você demorar muito, a cerimônia será interrompida automaticamente e você terá que tentar novamente.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "As noivas precisam lembrar que só têm 3 minutos para terminar de falar com o Bispo Vomars depois que seus noivos terminarem de falar com ele.";
		next;
		if (select("^3CB371[»]^000000 Obrigado, isso ajuda muito!:^3CB371[»]^000000 Como dizer o nome do meu parceiro?") == 1) {
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Bem, estou aqui para ajudar os casamentos a serem o mais tranquilos possível.";
			mes "Se houve algo que você não entendeu, sinta-se à vontade para me perguntar novamente.";
			goto L_End;
		}
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "A maneira mais fácil de escrever o nome do seu parceiro para o bispo é enviar uma mensagem privada ao seu parceiro e, em seguida, clicar com o botão esquerdo do mouse na seção do nome que fica à esquerda do prompt de bate-papo.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Pressione as teclas ''Ctrl'' e ''C'' para copiar o nome. Em seguida, você pode colar o nome no prompt de entrada pressionando as teclas ''Inserir'' e ''Shift''. Isso parece fácil, certo?";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Certo, agora vamos tentar. Pratique me dar o nome do seu parceiro usando o método que acabei de descrever.";
		next;
		input .@partner$;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Tudo bem, depois que você decidir se casar, volte para mim e envie sua inscrição.";
		mes "Vejo você mais tarde, aventureiro~";
		break;
	case 3:
		cutin "wedding_marry02",2;
		if (VIP_SCRIPT) {
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "^FF0000Espere!^000000";
			mes "^FF0000Se sua conta não for VIP, você deve ter uma aliança de casamento para se casar.^000000";
			mes "^FF0000Por favor, verifique o estado atual da sua conta e do seu amante antes de se registrar para um casamento.^000000";
			next;
		}
		if (Sex == SEX_MALE) {
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Então você gostaria de se casar?";
			mes "Como noivo, você precisa se preparar";
			if (.@covenant) {
				mes "^3377FF1 smoking^000000 e me dê o seu ^3377FF1 Pacto de Casamento^000000.";
				mes "As noivas têm que fornecer seus próprios vestidos de noiva e pagar uma taxa";
				mes "de 2,200,000 zeny ou também trazer uma aliança de casamento.";
			} else {
				mes "^3377FF1 smoking^000000 e pagar ^3377FF3,300,000 zeny^000000.";
				mes "As noivas têm que fornecer seus próprios vestidos de noiva e pagar uma taxa";
				mes "de 2,200,000 zeny.";
			}
		} else {
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Então você gostaria de se casar?";
			mes "Como noiva, você precisa se preparar";
			if (.@covenant) {
				mes "^3377FF1 vestido de noiva^000000 e me dê o seu ^3377FFPacto de casamento^000000.";
				mes "Os noivos devem trazer um smoking e pagar 3,300,000 zeny ou também trazer uma aliança de casamento.";
			} else {
				mes "^3377FF1 vestido de noiva^000000 e pagar uma taxa de 2,200,000 zenys. Os noivos devem trazer um smoking e pague 3,300,000 zeny para se casar.";
			}
		}
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Os noivos também precisam ter ^3377FF1 Anel de Diamante^000000 para serem trocados com seus parceiros.";
		mes "Você precisará de todos esses itens preparados quando enviar sua inscrição para a cerimônia de casamento.";
		next;
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Os futuros noivos devem concluir o processo de inscrição antes que o casamento possa ocorrer.";
		mes "Agora, você gostaria de se candidatar para o casamento?";
		next;
		if (select("^3CB371[»]^000000 Sim:^ff0000[»]^777777 Não^000000") == 1) {
			if (getpartnerid()) {
				cutin "wedding_marry02",2;
				mes "^0000FF[Assistente de Casamento]^000000";
				mes "Sinto muito, mas você não pode solicitar outro casamento!";
				mes "Não posso permitir que você traia seu cônjuge assim e, além disso, a poligamia não é legal aqui em Rune-Midgarts.";
				break;
			}
			else if (wedding_sign == 1) {
				mes "^0000FF[Assistente de Casamento]^000000";
				mes "Você já não completou o pedido?";
				mes "Hmm, certifique-se de que seu parceiro também terminou o processo de inscrição e depois fale com o Bispo Vomars.";
				break;
			}
			else if (BaseLevel < 300) {
				mes "^0000FF[Assistente de Casamento]^000000";
				mes "Hmm, você precisa ser forte o suficiente para proteger aquele que você ama antes de considerar o casamento.";
				mes "Depois que você ficar mais forte, venha falar comigo novamente.";
				goto L_End;
			}
			else if (countitem(2613) < 1) {
				mes "^0000FF[Assistente de Casamento]^000000";
				mes "Milímetros? Você esqueceu de trazer o Anel de Diamante para trocar com seu parceiro durante a cerimônia de casamento?";
				mes "Procure-o com cuidado e volte depois de encontrá-lo, ok?";
				goto L_End;
			}
			else if (Sex == SEX_FEMALE) {
				if (countitem(6026) == 0 && Zeny < 2200000) {
					mes "^0000FF[Assistente de Casamento]^000000";
					mes "Sinto muito, mas todas as noivas devem pagar a taxa de 2,200,000 zenys ou trazer uma Aliança de Casamento para prosseguir com a cerimônia de casamento. Talvez você possa pedir ao seu parceiro para ajudá-lo com os fundos?";
					goto L_End;
				}
				else if (countitem(2338) < 1) {
					mes "^0000FF[Assistente de Casamento]^000000";
					mes "Oh querida, você esqueceu seu vestido de noiva?";
					mes "Apresse-se e encontre-o, então traga-o para mim - você absolutamente precisa dele para o casamento!";
					goto L_End;
				}
			}
			else if (Sex == SEX_MALE) {
				if (countitem(6026) == 0 && Zeny < 3300000) {
					mes "^0000FF[Assistente de Casamento]^000000";
					mes "Sinto muito, mas você não tem os 3,300,000 zeny ou a Aliança de Casamento que todos os noivos devem pagar pela cerimônia de casamento.";
					mes "Você perdeu seu dinheiro?";
					goto L_End;
				}
				else if (countitem(7170) < 1) {
					mes "^0000FF[Assistente de Casamento]^000000";
					mes "Onde está o seu smoking?";
					mes "Você absolutamente tem que usá-lo durante a cerimônia de casamento! Encontre-o, traga-o para mim, e então podemos finalmente começar o casamento, ok?";
					goto L_End;
				}
			}
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Bem, parece que você tem tudo pronto.";
			mes "Embora eu não tenha certeza de quem é seu parceiro, deixe-me ser o primeiro a parabenizá-lo pelo seu próximo casamento~";
			next;
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Agora, vamos iniciar a aplicação. Por favor, escreva seu nome exato aqui.";
			next;
			while(1) {
				input .@name$;
				if (.@name$ != strcharinfo(0)) {
					mes "^0000FF[Assistente de Casamento]^000000";
					mes "Hmmm, você tem que escrever seu nome exatamente como é exibido. Talvez você precise copiar e colar?";
					mes "De qualquer forma, vamos tentar novamente.";
					next;
				}
				else 
					break;
			}
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Ótimo, parece que terminamos sua inscrição.";
			mes "Lembre-se que você precisará dizer ao Bispo Vomars o nome exato do seu parceiro quando falar com ele mais tarde, ok?";
			next;
			mes "^0000FF[Assistente de Casamento]^000000";
			mes "Quando seu parceiro terminar o processo de inscrição, ambos devem falar com o bispo para iniciar a cerimônia de casamento.";
			emotion ET_THROB;
			next;
			mes "^0000FF[Assistente de Casamento]^000000";
			if (Sex == SEX_FEMALE) {
				mes "Já que você é a noiva, você precisa esperar o noivo falar primeiro com o Bispo Vomars. Quando ele terminar, será sua vez de falar com o bispo Vomars.";
				if (.@covenant)
					delitem 6026,1; //Marriage_Covenant
				else
					set Zeny, Zeny-2200000;
				//delitem 2338,1; //Wedding_Dress
			}
			else {
				mes "Já que você é o noivo, você precisa falar com o Bispo primeiro. Quando terminar, será a vez de sua noiva falar com o bispo Vomars.";
				if (.@covenant)
					delitem 6026,1; //Marriage_Covenant
				else
					set Zeny, Zeny-3300000;
				//delitem 7170,1; //Tuxedo
			}
			delitem 2613,1; //Diamond_Ring
			set wedding_sign,1;
			goto L_End;
		}
		mes "^0000FF[Assistente de Casamento]^000000";
		mes "Não...?";
		mes "Bem, quando você estiver pronto para o casamento, sinta-se à vontade para voltar para mim para que você possa se inscrever, ok? Tenha um bom dia~";
		break;
	case 4:
		cutin "wedding_marry02",2;
		donpcevent "Exército de Solteiros#Prontera::OnEnable";
		donpcevent "Exército de Solteiros#Geffen::OnEnable";
		donpcevent "Exército de Solteiros#Morocc::OnEnable";
		donpcevent "Exército de Solteiros#Payon::OnEnable";
		donpcevent "Exército de Solteiros#Amatsu::OnEnable";
		donpcevent "Exército de Solteiros#Kunlun::OnEnable";
		emotion ET_HUK;
		mes "^0000FF[Exército de Solteiros]^000000";
		mes "^CC9933Você precisa refinar os itens por conta própria para fazer um ótimo equipamento!^000000";
		emotion ET_ROCK, getnpcid(0, "Exército de Solteiros#Prontera");
		next;
		mes "^0000FF[Exército de Solteiros]^000000";
		mes "^330099É um desperdício formar grupos em masmorras! Eu posso fazer isso sozinho!^000000";
		emotion ET_ROCK, getnpcid(0, "Exército de Solteiros#Geffen");
		next;
		mes "^0000FF[Exército de Solteiros]^000000";
		mes "^666666Inferno, eu treinei sozinho desde o nascimento, até a minha mudança de classe!^000000";
		emotion ET_ROCK, getnpcid(0, "Exército de Solteiros#Morocc");
		next;
		mes "^0000FF[Exército de Solteiros]^000000";
		mes "^666600EU ESCOLHO passar o Natal sozinho...";
		mes "jogando Paciência e fazendo palavras cruzadas!^000000";
		emotion ET_ROCK, getnpcid(0, "Exército de Solteiros#Payon");
		next;
		mes "^0000FF[Exército de Solteiros]^000000";
		mes "^CC9966As mulheres podem quebrar meu espírito, mas nunca tirarão... minha LIBERDADE!^000000";
		emotion ET_ROCK, getnpcid(0, "Exército de Solteiros#Amatsu");
		next;
		mes "^0000FF[Exército de Solteiros]^000000";
		mes "...Nós somos os livres! Nós somos o Exército de Solteiros Invencível!^000000";
		emotion ET_ROCK, getnpcid(0, "Exército de Solteiros#Kunlun");
		close2;
		cutin "wedding_marry01",255;
		emotion ET_SWEAT;
		donpcevent "Exército de Solteiros#Prontera::OnInit";
		donpcevent "Exército de Solteiros#Geffen::OnInit";
		donpcevent "Exército de Solteiros#Morocc::OnInit";
		donpcevent "Exército de Solteiros#Payon::OnInit";
		donpcevent "Exército de Solteiros#Amatsu::OnInit";
		donpcevent "Exército de Solteiros#Kunlun::OnInit";
		end;
	}
	close2;
	cutin "wedding_marry01",255;
	end;

L_End:
	close2;
	cutin "",255;
	end;
	
OnInit:

    setunitdata (getnpcid(0), UNPC_GROUP_ID, 7);
    setunittitle(getnpcid(0), "Dona Farailda");
	
}

prt_church,97,102,0	script	Exército de Solteiros#Prontera	105,{
	mes "^0000FF[Exército de Solteiros]^000000";
	mes "^CC9933Você precisa refinar os itens por conta própria para fazer um ótimo equipamento!^000000";
	close;

OnInit:
	hideonnpc "Exército de Solteiros#Prontera";
	end;

OnEnable:
	hideoffnpc "Exército de Solteiros#Prontera";
	emotion ET_GO;
	end;
}

prt_church,98,102,0	script	Exército de Solteiros#Geffen	705,{
	mes "^0000FF[Exército de Solteiros]^000000";
	mes "^330099É um desperdício formar grupos em masmorras! Eu posso fazer isso sozinho!^000000";
	close;

OnInit:
	hideonnpc "Exército de Solteiros#Geffen";
	end;

OnEnable:
	hideoffnpc "Exército de Solteiros#Geffen";
	emotion ET_GO;
	end;
}

prt_church,99,102,0	script	Exército de Solteiros#Morocc	707,{
	mes "^0000FF[Exército de Solteiros]^000000";
	mes "^666666Inferno, eu treinei sozinho desde o nascimento, até a minha mudança de classe!^000000";
	close;

OnInit:
	hideonnpc "Exército de Solteiros#Morocc";
	end;

OnEnable:
	hideoffnpc "Exército de Solteiros#Morocc";
	emotion ET_GO;
	end;
}

prt_church,100,102,0	script	Exército de Solteiros#Payon	708,{
	mes "^0000FF[Exército de Solteiros]^000000";
	mes "^666600EU ESCOLHO passar o Natal sozinho...";
	mes "jogando Paciência e fazendo palavras cruzadas!^000000";
	close;

OnInit:
	hideonnpc "Exército de Solteiros#Payon";
	end;

OnEnable:
	hideoffnpc "Exército de Solteiros#Payon";
	emotion ET_GO;
	end;
}

prt_church,101,102,0	script	Exército de Solteiros#Amatsu	767,{
	mes "^0000FF[Exército de Solteiros]^000000";
	mes "^CC9966As mulheres podem quebrar meu espírito, mas nunca tirarão... minha LIBERDADE!^000000";
	close;

OnInit:
	hideonnpc "Exército de Solteiros#Amatsu";
	end;

OnEnable:
	hideoffnpc "Exército de Solteiros#Amatsu";
	emotion ET_GO;
	end;
}

prt_church,102,102,0	script	Exército de Solteiros#Kunlun	780,{
	mes "^0000FF[Exército de Solteiros]^000000";
	mes "^669900...Nós somos os livres! Nós somos o Exército de Solteiros Invencível!^000000";
	close;

OnInit:
	hideonnpc "Exército de Solteiros#Kunlun";
	end;

OnEnable:
	hideoffnpc "Exército de Solteiros#Kunlun";
	emotion ET_GO;
	end;
}

prt_church,100,128,4	script	Bispo#w	60,{
	cutin "wedding_bomars01",2;
	if (Upper == 2) {
		mes "^0000FF[Vomars]^000000";
		mes "Saudações, criança.";
		mes "Você está perdido? Hmmm.";
		mes "Você sabe onde sua mamãe e seu papai estão?";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}

	if (!getpartnerid()) {
		if (!$@wedding) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == SEX_MALE) {
						set $@wedding,1;
						initnpctimer;
						mes "^0000FF[Vomars]^000000";
						mes "Jovens amantes, por favor, lembrem-se deste momento pelo resto de suas vidas. Que seu futuro seja abençoado com paz e alegria. Que o amor que você compartilha cresça a cada dia que passa.";
						next;
						mapannounce "prt_church","É a proposta de casamento do noivo, Sr. " + strcharinfo(0) + "...",bc_map;
						mes "^0000FF[Vomars]^000000";
						mes "Até o fim do mundo, que você fique ao lado de quem você ama, para apoiá-la e protegê-la. Agora, posso saber o nome da sua noiva?";
						next;
						input $@wed_bride$;
						mes "^0000FF[Vomars]^000000";
						mes "Sr. " + strcharinfo(0) + "...";
						mes "Você jura por sua vida que vai sempre amar e cuidar de sua noiva, senhorita " + $@wed_bride$ + "?";
						next;
						select("^3CB371[»]^000000 Eu Juro.");
						set $@wed_groom$,strcharinfo(0);
						mes "^0000FF[Vomars]^000000";
						mes "Agora, é hora de sua noiva fazer seus votos de casamento.";
						mes "Se ela se apresentar...";
						close2;
						mapannounce "prt_church","O noivo, Sr. " + strcharinfo(0) + ", fez seus votos para a senhorita " + $@wed_bride$ + "...",bc_map;
						cutin "",255;
						end;
					}
					mes "^0000FF[Vomars]^000000";
					mes "Sinto muito, mas o noivo deve falar comigo primeiro para começar o casamento.";
					mes "É um protocolo antiquado, mas admito que mantém as coisas funcionando sem problemas.";
					goto L_End;
				}
				mes "^0000FF[Vomars]^000000";
				mes "Antes de poder se casar, você deve primeiro formar um grupo de dois com seu parceiro.";
				mes "Então, podemos prosseguir com a cerimônia.";
				goto L_End;
			}
			mes "^0000FF[Vomars]^000000";
			mes "Você deve solicitar o casamento com a Assistende de Casamento antes de se casar.";
			mes "A Assistente de Casamento vai deixar você saber o que mais você precisa fazer para se preparar para o casamento.";
			goto L_End;
		}
		else if ($@wedding == 1) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == SEX_FEMALE) {
						if (strcharinfo(0) == $@wed_bride$) {
							mes "^0000FF[Vomars]^000000";
							mes "Jovens amantes, por favor, lembrem-se deste momento pelo resto de suas vidas. Que seu futuro seja abençoado com paz e alegria. Que o amor que você compartilha cresça a cada dia que passa.";
							next;
							mapannounce "prt_church","Vamos ouvir o que a noiva, senhorita "+$@wed_bride$+", tem a dizer...",bc_map;
							mes "^0000FF[Vomars]^000000";
							mes "Senhorita "+$@wed_bride$+"...";
							mes "Você jura permanecer fiel a "+$@wed_groom$+", estar ao seu lado, não importa quais sejam os perigos?";
							next;
							if (select("^ff0000[»]^777777 Não.^000000:^3CB371[»]^000000 Eu Juro.") == 1) {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","Próximo casal, prossiga...",bc_map;
								mes "^0000FF[Vomars]^000000";
								mes "Então "+$@wed_groom$;
								mes "não é aquele com quem você quer se casar? Hum.";
								mes "Eu realmente sinto muito por este mal-entendido...";
								set $@wedding,0;
								close2;
								stopnpctimer;
								cutin "",255;
								end;
							}
							mes "^0000FF[Vomars]^000000";
							mes "Você realmente jura fidelidade e paciência?";
							mes "Você quer se casar com "+$@wed_groom$+"?";
							next;
							if (select("^3CB371[»]^000000 Sim eu quero.:^ff0000[»]^777777 Não.^000000") == 1) {
								if (isloggedin(getcharid(3,$@wed_groom$))) {
									if (marriage($@wed_groom$)) {
										//Efeito de chamada de casamento
										wedding;
										//Dê o anel para a Noiva e mude para o sprite do casamento.
										sc_start SC_Wedding,3600000,1;
										atcommand "@produce 2634";
										//Dê o anel ao noivo e mude para o sprite do casamento.
										attachrid(getcharid(3,$@wed_groom$));
										sc_start SC_Wedding,3600000,1;
										atcommand "@produce 2635";
										detachrid;
										//Mudar a progressão do Script de volta para Bride
										attachrid(getcharid(3,$@wed_bride$));
										cutin "wedding_bomars02",2;
										mapannounce "prt_church","Eu agora declaro, "+$@wed_groom$+" e "+$@wed_bride$+", marido e mulher.",bc_map;
										mapannounce "prontera","Eu agora declaro, "+$@wed_groom$+" e "+$@wed_bride$+", marido e mulher.",bc_map;
										mes "^0000FF[Vomars]^000000";
										mes "Pelo poder investido em mim como Bispo Real do Reino de Rune-Midgarts, eu agora os declaro marido e mulher. Que seu futuro seja abençoado com muitas grandes alegrias.";
										next;
										mes "^0000FF[Vomars]^000000";
										mes "E por fim...";
										mes "Seja sempre feliz, querida "+$@wed_bride$+"...";
										set $@wed_groom$,"";
										set $@wed_bride$,"";
										set $@wedding,0;
										close2;
										stopnpctimer;
										cutin "",255;
										detachrid;
										end;
									}
								}
								cutin "wedding_bomars03",2;
								mes "^0000FF[Vomars]^000000";
								mes "Hum. Parece que seu noivo foi embora antes do término da cerimônia. Por favor, tente novamente quando ele voltar.";
								goto L_End;
							}
							else {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","Infelizmente! "+$@wed_bride$+" rejeitou o pedido de casamento de "+$@wed_groom$+"!",bc_map;
								mes "^0000FF[Vomars]^000000";
								mes "Hum. Parece que você mudou de ideia. Embora eu sinta pena do noivo, você deve fazer o que seu coração lhe diz que é certo. Agora, corra!";
							}
							set $@wed_groom$,"";
							set $@wed_bride$,"";
							set $@wedding,0;
							close2;
							stopnpctimer;
							cutin "",255;
							end;
						}
						callsub S_Busy;
					}
					callsub S_Busy;
				}
				callsub S_Busy;
			}
			if (strcharinfo(0) == $@wed_bride$) {
				mes "^0000FF[Vomars]^000000";
				mes "Hum? Parece que a Assistente de Casamento ainda não recebeu seu pedido de casamento. Por favor, fale com ela para que possamos começar a cerimônia de casamento.";
				goto L_End;
			}
			callsub S_Busy;
		}
		callsub S_Busy;
	}
	mes "^0000FF[Vomars]^000000";
	mes "Desejo-lhe felicidades eternas. Não importa quão sombrio seja o presente, fique sempre ao lado de seu ente querido e olhe para o futuro com esperança.";

L_End:
	close2;
	cutin  "",255;
	end;

S_Busy:
	if ($@wed_groom$ != "" && $@wed_bride$ != "") {
		mes "^0000FF[Vomars]^000000";
		mes "O casamento da senhorita "+ $@wed_bride$ +" e senhor "+ $@wed_groom$ +"está atualmente em andamento.";
		mes "Por favor, mantenha sua voz baixa.";
		goto L_End;
	}
	mes "^0000FF[Vomars]^000000";
	mes "Estou realizando um casamento para outro casal agora, então, por favor, espere pacientemente pela sua vez. Obrigado pela compreensão...";
	goto L_End;

OnStop:
	stopnpctimer;
	end;

OnReset:
	set $@wed_groom$,"";
	set $@wed_bride$,"";
	set $@wedding,0;
	end;

OnTimer180000:
	mapannounce "prt_church","Você respondeu muito devagar... Próximo casal, por favor, prossiga.",bc_map;
	donpcevent "Bispo#w::OnReset";
	stopnpctimer;
	end;
}

prt_church,28,178,4	script	O Rei de Rune-Midgard	108,{
	callfunc "F_GM_NPC";
	mes "^0000FF[Vomars]^000000";
	mes "O-quem é você?";
	mes "Você deve saber a senha para invocar meus poderes incríveis.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "^0000FF[Vomars]^000000";
		mes "Este não é um lugar para brincar.";
		close2;
		warp "prt_church",101,102;
		end;
	}
	mes "[ ^00B2EEInterruptor de casamento^000000 ]";
	mes "Há algum problema com a cerimônia de casamento?";
	mes "Eu posso resetar o NPC Bishop omars se você quiser.";
	next;
	switch(select("Não, obrigado:RESET")) {
	case 1:
		mes "[ ^00B2EEInterruptor de casamento^000000 ]";
		mes "Tudo bem, então.";
		mes "No entanto, se o NPC Bishop Vomars estiver travado, pode ser melhor redefini-lo.";
		close;
	case 2:
		donpcevent "Bispo#w::OnStop";
		donpcevent "Bispo#w::OnReset";
		mapannounce "prt_church","Você respondeu muito devagar... Próximo casal, por favor, prossiga.",bc_map;
		mes "[Interruptor de casamento]";
		mes "O NPC Bishop Vomars foi reativado.";
		mes "Agora deve ser possível prosseguir com casamentos.";
		close;
	}
}

prt_church,20,179,4	script	Equipe de Divórcio	71,{
	callfunc "F_GM_NPC";
	mes "^00B2EE[Final ruim]^000000";
	mes "Ei...";
	mes "Você sabe que não posso fazer nada por você.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "^00B2EE[Final ruim]^000000";
		mes "Hmm...";
		mes "Você realmente não deveria estar neste lugar...";
		close;
	}
	mes "^00B2EE[Final ruim]^000000";
	mes "Ótimo, você sabe a senha! Agora, você quer que eu remova o anel de casamento do seu inventário?";
	next;
	switch(select("Tirar 1 Anel de Casamento.:Mantê-la.")) {
	case 1:
		set .@ring, (Sex == SEX_MALE)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring)) {
			delitem .@ring,1;
			mes "^00B2EE[Final ruim]^000000";
			mes "Está feito!";
		} else {
			mes "^00B2EE[Final ruim]^000000";
			mes "Não encontrei o anel de casamento...";
			mes "Certifique-se de que não está equipado.";
		}
		close;
	case 2:
		mes "^00B2EE[Final ruim]^000000";
		mes "Tem certeza que quer ficar com aquele anel?";
		mes "Tudo bem, mas se for";
		mes "um problema, você vem a mim.";
		close;
	}
}

prt_church,22,179,4	script	Novo Anel	71,{
	callfunc "F_GM_NPC";
	mes "[Wedding Again]";
	mes "Hmm...?";
	mes "O que exatamente você está fazendo aqui?";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Wedding Again]";
		mes "Ahk!";
		mes "Um aventureiro como você não deveria estar neste lugar!";
		close;
	}
	if (getpartnerid()) {
		mes "[Wedding Again]";
		mes "Hmm...";
		mes "Eu só posso criar um Anel de Casamento se o personagem for casado.";
		close;
	}
	mes "[Wedding Again]";
	mes "Ah, vejo que você sabe a senha.";
	mes "Tudo bem, se você de alguma forma perdeu seu anel de casamento, posso fazer um novo para você.";
	next;
	switch(select("Faça um novo anel de casamento.:Cancelar.")) {
	case 1:
		set .@ring, (Sex == SEX_MALE)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring) || isequipped(.@ring)) {
			mes "[Wedding Again]";
			mes "Espera espera...";
			mes "Você está usando seu anel de casamento. É melhor eu não te fazer outro já que você não precisa de mais de um.";
		} else {
			getitem .@ring,1;
			mes "[Wedding Again]";
			mes "Aqui está ~";
			mes "É o seu novo anel de casamento!";
		}
		close;
	case 2:
		mes "[Wedding Again]";
		mes "Tudo bem. Se você perder seu anel de casamento, venha até mim se precisar de um novo, ok?";
		close;
	}
}
