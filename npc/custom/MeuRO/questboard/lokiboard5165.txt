// Quest board, baseado nas missões do Eden
//=========================================================================

s_room,43,66,6	script	Quest [51 - 65]#Tuto3	4_BOARD3,{
    mes .nome$;
    mes "Selecione a Categoria:";
    next;
	switch(select("^3CB371[»]^000000 Pegar missões:^3CB371[»]^000000 Completar missões:^ff0000[»]^000000 Cancelar")){
    case 1:
        for ( .@i = 90091; .@i < 90112; .@i++ )
            if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
        for( .@i = 90070; .@i < 90091; .@i++ ) {
            .@j = checkquest(.@i,HUNTING);
            if (.@j == 2) {
                mes .nome$;
                mes "- Você deve coletar sua - ";
                mes "- recompensa antes de começar - ";
                mes "- uma nova missão. - ";
                close;
            }
        }
        if (BaseLevel < 51) {
            mes .nome$;
            mes "- Não há tarefas - ";
            mes "- para você agora. - ";
            close;
        }
        if (BaseLevel > 65) {
            mes .nome$;
            mes "- Essas missões são fáceis - ";
            mes "- demais para o seu nível - ";
            mes "- procurar por uma missão - ";
            mes "- de nível apropriado. - ";
            close;
        }
        //next;
        .@msg$ = "";
        for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
            .@msg$ += .quest_nome$[.@i]+":";
        }
        .@s = select(.@msg$+"^3CB371[»]^000000 Pegar todas as missões:^ff0000[»]^000000 Cancelar") - 1;
        if(.@s < getarraysize(.quest_id)){
            callsub L_GetQuest,.quest_id[.@s],.quest_nome$[.@s],"- Caçar "+.quest_mqt[.@s]+" ^4d4dff"+strmobinfo(1,.quest_mob[.@s])+"^000000 - ";
        }else if(.@s == getarraysize(.quest_id)){
            mes .nome$;
            mes "- Pegando todas as missões: - ";
            for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
                .@quest_status = checkquest(.quest_id[.@i]+getarraysize(.quest_id),PLAYTIME);
                if(checkquest(.quest_id[.@i]) == -1 && .@quest_status == -1){
                    setquest .quest_id[.@i];
                    mes "- "+.quest_nome$[.@i]+" - ";
                }
            }
            close;
        }else{
            mes .nome$;
            mes "- Pare de ler o quadro de avisos. - ";
            close;
        }
    case 2:
        .@f = -1;
        for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
            if(.@f < checkquest(.quest_id[.@i],HUNTING))
                .@f = checkquest(.quest_id[.@i],HUNTING);
        }
        if(.@f == 2){
            mes .nome$;
            mes "hum...deixa eu ver~";
           for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
                .@j = checkquest(.quest_id[.@i],HUNTING);
                if (.@j == 2) {
                    mes "Você completou '"+ .quest_nome$[.@i] +"'.";
                    erasequest .quest_id[.@i];
                    setquest .quest_id[.@i]+getarraysize(.quest_id);
                    getexp .quest_base[.@i],.quest_job[.@i];
                    getitem .quest_item[.@i],.quest_iqt[.@i];
                    // Pontos de reputação
                    if(getreputpoint(4) >= 10000) end;
                        setreputpoint(4, 10);
                }
            }
            next;
            mes .nome$;
            mes "Bom trabalho~!!";
            mes "Nosso cliente está muito feliz com seu trabalho. Ele vai apreciar o que você fez.";
            mes "Por favor, ajude-nos novamente. Obrigado!";
            close;
        }else if(.@f >= 0){
            mes .nome$;
			mes "Você ainda não terminou sua missão.";
			mes "Mantenha o bom trabalho. Você está quase pronto.";
			close;
        }else{
            mes .nome$;
            mes "Você está procurando trabalho?";
            mes "Sempre temos muito trabalho a fazer.";
            next;
            mes .nome$;
            mes "Por que você não olha em volta e encontra algumas tarefas interessantes?";
            close;
        }
    case 3:
        close;
    }

L_GetQuest:
    mes .nome$;
    if (checkquest(getarg(0),HUNTING) >= 0) {
        mes "- Você só pode solicitar - ";
        mes "- uma missão de cada vez. - ";
        close;
    }
	.@quest_status = checkquest(getarg(0)+getarraysize(.quest_id),PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes "- Missão ^4d4dff"+ getarg(1) +"^000000 - ";
		mes "- Somente daqui a 12 horas. - ";
		//mes "";
		//mes "- Manager : Spike - ";
		close;
	}
	mes "- Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	//mes "";
	//mes "- Manager : Spike - ";
	next;
	if (select("^3CB371[»]^000000 Eu farei esta missão.:^3CB371[»]^000000 Procure outras missões.") == 2) {
        mes .nome$;
		mes "- vamos procurar -";
		mes "- outras missões. - ";
		close;
	}
    mes .nome$;
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;

// Configurações
OnInit:
    .nome$ = "[^FF7700Quadro de Missões^000000]";
	.@UID = getnpcid(0);
    setunitdata .@UID, UNPC_GROUP_ID, 7;
    // Id das quests no quest_db
    setarray .quest_id[0], 90070, 90071, 90072, 90073, 90074, 90075, 90076, 90077, 90078, 90079, 90080, 90081, 90082, 90083, 90084, 90085, 90086, 90087, 90088, 90089, 90090;
    // Nome das missões
    setarray .quest_nome$[0], "Eliminar Fen","Eliminar Esqueleto Soldado","Eliminar Argos","Eliminar Esqueleto Operário","Eliminar Guerreiro Orc","Eliminar Senhora Orc","Eliminar Bongun","Eliminar Lobo do Deserto","Eliminar Hode","Eliminar Sohee","Eliminar Múmia","Eliminar Arenoso","Eliminar Zenorc","Eliminar Cavalo-Marinho","Eliminar Soldadinho de Chumbo","Eliminar Presente","Eliminar Les","Eliminar Petite Terrestre","Eliminar Petite Voador","Eliminar Argiope","Eliminar Pesadelo";
    // Id dos mobs
    setarray .quest_mob[0], 1158, 1028, 1100, 1169, 1023, 1273, 1188, 1106, 1127, 1170, 1041, 1165,	1177, 1045, 1248, 1249, 1881, 1155, 1156, 1099, 1061;
    // Quantidade dos mobs
    setarray .quest_mqt[0], 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50, 50;
    // Exp de base ganha
    setarray .quest_base[0], 15000, 15500, 16000, 16250, 17000, 17500, 17750, 18000, 18000, 18000, 20000, 21500, 22000, 22500, 22500, 24900, 26500, 27500, 27500, 28550, 29150;
    // Exp de classe ganha
    setarray .quest_job[0], 7000, 10000, 10000, 10000, 11500, 11000, 12250, 15000, 15000, 15000, 16000, 16500, 16500, 16000, 15000, 15250, 19150, 19250, 19250, 19500, 20000;
    // Item ganho
    setarray .quest_item[0], 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607;
    // Quantidade dos itens
    setarray .quest_iqt[0], 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8, 8;
    end;
}