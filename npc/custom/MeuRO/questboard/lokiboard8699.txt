//= Não venda ou repasse esse arquivo sem autorização.
//=========================================================================
// Quest board, baseado nas missões do Eden
//=========================================================================

s_room,58,66,6	script	Quest [86 - 99]#Tuto5	4_BOARD3,{
    mes .nome$;
    mes "Selecione a Categoria:";
    next;
	switch(select("^3CB371[»]^000000 Pegar missões:^3CB371[»]^000000 Completar missões:^ff0000[»]^000000 Cancelar")){
    case 1:
        for ( .@i = 90206; .@i < 90238; .@i++ )
            if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
        for( .@i = 90174; .@i < 90206; .@i++ ) {
            .@j = checkquest(.@i,HUNTING);
            if (.@j == 2) {
                mes .nome$;
                mes "- Você deve coletar sua - ";
                mes "- recompensa antes de começar - ";
                mes "- uma nova missão. - ";
                close;
            }
        }
        if (BaseLevel < 86) {
            mes .nome$;
            mes "- Não há tarefas - ";
            mes "- para você agora. - ";
            close;
        }
        if (BaseLevel > 99) {
            mes .nome$;
            mes "- Essas missões são fáceis - ";
            mes "- demais para o seu nível - ";
            mes "- procurar por uma missão - ";
            mes "- de nível apropriado. - ";
            close;
        }
        //next;
        .@msg$ = "";
        for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
            .@msg$ += .quest_nome$[.@i]+":";
        }
        .@s = select(.@msg$+"^3CB371[»]^000000 Pegar todas as missões:^ff0000[»]^000000 Cancelar") - 1;
        if(.@s < getarraysize(.quest_id)){
            callsub L_GetQuest,.quest_id[.@s],.quest_nome$[.@s],"- Caçar "+.quest_mqt[.@s]+" ^4d4dff"+strmobinfo(1,.quest_mob[.@s])+"^000000 - ";
        }else if(.@s == getarraysize(.quest_id)){
            mes .nome$;
            mes "- Pegando todas as missões: - ";
            for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
                .@quest_status = checkquest(.quest_id[.@i]+getarraysize(.quest_id),PLAYTIME);
                if(checkquest(.quest_id[.@i]) == -1 && .@quest_status == -1){
                    setquest .quest_id[.@i];
                    mes "- "+.quest_nome$[.@i]+" - ";
                }
            }
            close;
        }else{
            mes .nome$;
            mes "- Pare de ler o quadro de avisos. - ";
            close;
        }
    case 2:
        .@f = -1;
        for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
            if(.@f < checkquest(.quest_id[.@i],HUNTING))
                .@f = checkquest(.quest_id[.@i],HUNTING);
        }
        if(.@f == 2){
            mes .nome$;
            mes "hum...deixa eu ver~";
           for(.@i = 0; .@i < getarraysize(.quest_id); .@i++){
                .@j = checkquest(.quest_id[.@i],HUNTING);
                if (.@j == 2) {
                    mes "Você completou '"+ .quest_nome$[.@i] +"'.";
                    erasequest .quest_id[.@i];
                    setquest .quest_id[.@i]+getarraysize(.quest_id);
                    getexp .quest_base[.@i],.quest_job[.@i];
                    getitem .quest_item[.@i],.quest_iqt[.@i];
                    // Pontos de reputação
                    if(getreputpoint(4) >= 10000) end;
                        setreputpoint(4, 10);
                }
            }
            next;
            mes .nome$;
            mes "Bom trabalho~!!";
            mes "Nosso cliente está muito feliz com seu trabalho. Ele vai apreciar o que você fez.";
            mes "Por favor, ajude-nos novamente. Obrigado!";
            close;
        }else if(.@f >= 0){
            mes .nome$;
			mes "Você ainda não terminou sua missão.";
			mes "Mantenha o bom trabalho. Você está quase pronto.";
			close;
        }else{
            mes .nome$;
            mes "Você está procurando trabalho?";
            mes "Sempre temos muito trabalho a fazer.";
            next;
            mes .nome$;
            mes "Por que você não olha em volta e encontra algumas tarefas interessantes?";
            close;
        }
    case 3:
        close;
    }

L_GetQuest:
    mes .nome$;
    if (checkquest(getarg(0),HUNTING) >= 0) {
        mes "- Você só pode solicitar - ";
        mes "- uma missão de cada vez. - ";
        close;
    }
	.@quest_status = checkquest(getarg(0)+getarraysize(.quest_id),PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes "- Missão ^4d4dff"+ getarg(1) +"^000000 - ";
		mes "- Somente daqui a 12 horas. - ";
		//mes "";
		//mes "- Manager : Spike - ";
		close;
	}
	mes "- Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	//mes "";
	//mes "- Manager : Spike - ";
	next;
	if (select("^3CB371[»]^000000 Eu farei esta missão.:^3CB371[»]^000000 Procure outras missões.") == 2) {
        mes .nome$;
		mes "- vamos procurar -";
		mes "- outras missões. - ";
		close;
	}
    mes .nome$;
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;

// Configurações
OnInit:
    .nome$ = "[^FF7700Quadro de Missões^000000]";
	.@UID = getnpcid(0);
    setunitdata .@UID, UNPC_GROUP_ID, 7;
    // Id das quests no quest_db
    setarray .quest_id[0], 90174, 90175, 90176, 90177, 90178, 90179, 90180, 90181, 90182, 90183, 90184, 90185, 90186, 90187, 90188, 90189, 90190, 90191, 90192, 90193, 90194, 90195, 90196, 90197, 90198, 90199, 90200, 90201, 90202, 90203, 90204, 90205;
    // Nome das missões
    setarray .quest_nome$[0], "Eliminar Isilla","Eliminar Strouf","Eliminar Alarme","Eliminar Kikimora","Eliminar Sting","Eliminar Pasana","Eliminar Vanberk","Eliminar Tengu","Eliminar Fornalha Velha","Eliminar Tritão","Eliminar Dullahan","Eliminar Agressor","Eliminar Shinobi","Eliminar Incubus","Eliminar Succubus","Eliminar Andarilho","Eliminar Sedora","Eliminar Sropho","Eliminar Dofle","Eliminar Yeti","Eliminar Rybio","Eliminar Nepenthes","Eliminar Loli Ruri","Eliminar Dramoh Rei","Eliminar Múmia Anciã","Eliminar Ferus Vermelho","Eliminar Mavka","Eliminar Zumbi Dilacerado","Eliminar Serial Killer","Eliminar Massacre","Eliminar Acidus Azul","Eliminar Titan de Gelo";
    // Id dos mobs
    setarray .quest_mob[0], 1772, 1065, 1193, 1883, 1207, 1154, 1771, 1405, 1617, 1264, 1504, 1315,	1401, 1374, 1370, 1208, 2204, 2201, 2203, 1775, 1201, 1999, 1505, 2198, 1297, 1714, 1884, 1865, 1507, 1864, 1716, 1777;
    // Quantidade dos mobs
    setarray .quest_mqt[0], 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80, 80;
    // Exp de base ganha
    setarray .quest_base[0], 750000, 750000, 85000, 900000, 900000, 900000, 900000, 950000, 950000, 115000, 115000, 115000, 115000, 125000, 125000, 150000, 150000, 150000, 150000, 150000, 150000, 150000, 150000, 175000, 175000, 175000, 175000, 185000, 185000, 200000, 215000, 225000;
    // Exp de classe ganha
    setarray .quest_job[0], 70000, 70000, 70000, 70000, 70000, 70000, 70000, 75000, 75000, 85000, 85000, 85000, 85000, 85000, 85000, 95000, 95000, 95000, 95000, 95000, 95000, 95000, 95000, 100000, 100000, 100000, 100000, 100000, 100000, 115000, 117500, 125000;
    // Item ganho
    setarray .quest_item[0], 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607, 607;
    // Quantidade dos itens
    setarray .quest_iqt[0], 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21, 21;
    end;
}