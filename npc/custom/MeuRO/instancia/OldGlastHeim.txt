glast_01,204,273,5	script	Hugin	4_M_SAGE_C,{
	if(BaseLevel<120) {
		mes "[Hugin]";
		mes "Por que você não volta depois de se tornar mais forte?";
		mes "Talvez, nível 120.";
		close;
	}
	if(isbegin_quest(12316)==0) {
		mes "[Hugin]";
		mes "Há muito tempo, este castelo não se parecia com isso.";
		next;
		mes "[Hugin]";
		mes "Ah, me desculpe. Estou resmungando diante de um estranho.";
		next;
		mes "[Hugin]";
		mes "Meu nome é Hugin. Estou estudando o espaço entre o tempo e o espaço.";
		next;
		select("Tem algo assim aqui?");
		mes "[Hugin]";
		mes "Você já se perguntou sobre a história do Glast Heim?";
		next;
		mes "[Hugin]";
		mes "Havia um rei chamado Shumiche que era conhecido como um tirano na história do Reino de Rune-Midgard.";
		next;
		mes "[Hugin]";
		mes "O tempo irá revelar as respostas verdadeiras se olharmos nos lugares certos.";
		next;
		if(select("Eu não me importo com a história","Isso é interessante. Você encontrou alguma coisa?")==1) {
			mes "[Hugin]";
			mes "Realmente? Hmm, por favor, volte mais tarde quando estiver interessado.";
			close;
		}
		mes "[Hugin]";
		mes "Na verdade, é possível viajar no tempo dimensional, mas tenho muito medo de ir para lá.";
		next;
		mes "[Hugin]";
		mes "Pode ser possível para você!";
		next;
		mes "[Hugin]";
		mes "Você quer viajar no tempo?";
		next;
		if(select("Não, obrigado","Sim, claro que sim!")==1) {
			mes "[Hugin]";
			mes "Sério? Mas, pulando gigawatts, esta é uma ótima oportunidade.";
			close;
		}
		mes "[Hugin]";
		mes "Eu sabia que você entenderia o que eu disse. Bem, por favor, me diga o que você quer fazer.";
		setquest 12316;// Encontrando Hugin
		completequest 12316;
	}
	else {
		mes "[Hugin]";
		mes "Umm? Você me viu em outro lugar? Eu não penso assim. Que tal desta vez?";
		if(isbegin_quest(12322)==1)
			erasequest 12322;
	}
	next;
	switch(checkquest(12317,PLAYTIME)) {
	case -1:
		.@party_id=getcharid(1);
		.@p_name$=getpartyname(.@party_id);
		.@md_name$="Velha Glast Heim";

		if(!instance_check_party(.@party_id)) {
			mes "[Hugin]";
			mes "Por que você não faz uma festa com mais de uma pessoa e fala comigo de novo?";
			close;
		}
		if (is_party_leader() == true)
			.@menu$="Gerar intervalo de tempo";
		else {
			mes "[Hugin]";
			mes "Já nos conhecemos antes? De jeito nenhum. É a primeira vez que vejo você. O que você quer?";
		}
		if(isbegin_quest(12318)==0)
			setquest 12318;// Caça à alma corrompida
		switch(select(.@menu$, "Entrar Velha Glast Heim","Cancelar")) {
		case 1:
			switch(instance_create(.@md_name$)) {
			case -3:
				dispbottom "Memorial Dungeon, 'Velha Glast Heim' já está em andamento.",0xFFFFFF;
				close;
			case -4:
			case -2:
			case -1:
				mes "Party Name: "+getpartyname(getcharid(1));
				mes "Líder do Grupo: "+strcharinfo(0);
				mes "^0000ff"+.@md_name$+"^000000 - a geração de intervalo de tempo falhou.";
				close;
			}
			mes "[Hugin]";
			mes "Depois que a lacuna de tempo se abrir, por favor me avise novamente.";
			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "[Hugin]";
				mes "Ocorreu um erro desconhecido.";
				close;
			case IE_NOINSTANCE:
				mes "[Hugin]";
				mes "O intervalo de tempo ainda não está aberto.";
				close;
			case IE_NOMEMBER:
				mes "[Hugin]";
				mes "Seu corpo não está apto a entrar no intervalo de tempo. Você não poderá entrar se não estiver em uma festa.";
				close;
			case IE_OK:
				mapannounce "glast_01",""+getpartyname(getcharid(1))+" membro do grupo "+strcharinfo(0)+" insere o "+.@md_name$+".",bc_map,0x00FF99;
				setquest 12317;// Rastreamento da viagem no tempo
				// warp "1@gl_k",150,20;
				end;
			}
		case 3:
			close;
		}
	case 0:
	case 1:
		mes "[Hugin]";
		mes "Oh, meu...";
		mes "Você ainda tem efeitos posteriores de viagem no tempo. Você não pode viajar novamente com essa condição.";
		next;
		mes "[Hugin]";
		mes "Manter-se saudável é importante, então, por favor, faça uma pausa e volte mais tarde.";
		close;
	case 2:
		mes "^0000ffTodos os vestígios de acesso ao Velha Glast Heim foram removidos. Agora você pode falar com Hugin novamente.^000000";
		if(isbegin_quest(12318)==0)
			setquest 12318;// Caça à alma corrompida
		erasequest 12317;
		close;
	}
}

// Warps
1@gl_k,96,80,0	warp	#2Control	2,2,1@gl_k,80,80
1@gl_k,90,80,0	warp	#2Control2	2,2,1@gl_k,105,80
1@gl_k,202,79,0	warp	#3Control	2,2,1@gl_k,215,79
1@gl_k,206,79,0	warp	#3Control2	2,2,1@gl_k,195,79
1@gl_k,227,216,0	warp	#4Control	2,2,1@gl_k,215,216
1@gl_k,222,216,0	warp	#4Control2	2,2,1@gl_k,233,216
1@gl_k,150,284,0	warp	#2F Entrance	2,2,2@gl_k,150,46
2@gl_k,149,32,0	warp	#1 Control	2,2,1@gl_k,150,270
2@gl_k,145,123,0	warp	#22 Control	2,2,2@gl_k,126,123
2@gl_k,136,122,0	warp	#22 Control2	2,2,2@gl_k,150,116
2@gl_k,154,101,0	warp	#23 Control	2,2,2@gl_k,174,101
2@gl_k,165,101,0	warp	#23 Control2	2,2,2@gl_k,150,111
2@gl_k,150,163,0	warp	#24 Control	2,2,2@gl_k,150,179
2@gl_k,150,167,0	warp	#24 Control2	2,2,2@gl_k,150,149
1@gl_k,69,168,0	warp	#Secret Room Exit	2,2,1@gl_k,48,168

// Andar 1
//============================================================
1@gl_k,149,41,5	script	Varmundt#0	4_M_BARMUND,{
	if (is_party_leader() == false) {
		mes "[Varmundt]";
		mes "Onde está seu líder? Eu preciso da ajuda dele.";
		cutin "gl_barmund2",2;
		close3;
	}
	.@account_id=getcharid(3);
	.@player_name$=strcharinfo(0);
	mes "[Varmundt]";
	mes "Você é o único ^0000ffHerico^000000 enviado para me ajudar?";
	npctalk "Varmundt: Você é o único que Herico enviou para me ajudar?";
	cutin "gl_barmund1",2;
	next;
	select("Oh. Bem, eu...");
	mes "["+.@player_name$+"]";
	mes "Ah sim, eu sou. Herico me disse para conhecê-lo.";
	unittalk .@account_id, .@player_name$+" : Ah sim, eu sou. Herico me disse para conhecê-lo.";
	next;
	mes "[Varmundt]";
	mes "Nós não temos muito tempo. Devemos reportar a Sr. Heinrich sobre a invasão de Himelmez.";
	npctalk "Varmundt: Nós não temos muito tempo. Devemos informar a Sr. Heinrich sobre a invasão de Himelmez.";
	cutin "gl_barmund2",2;
	next;
	select("Himelmez...");
	mes "["+.@player_name$+"]";
	mes "Quem é Himelmez?";
	unittalk .@account_id, .@player_name$+" : Quem é Himelmez?";
	next;
	mes "[Varmundt]";
	mes "Herico não te contou?";
	npctalk "Varmundt: Herico não te contou?";
	cutin "gl_barmund3",2;
	next;
	mes "[Varmundt]";
	mes "Ela é a Governante da Morte, Valquíria do Homem Morto. Himelmez está procurando tirar o coração de Ymir de nós.";
	npctalk "Varmundt: Ela é a Governante da Morte, Valquíria do Homem Morto. Himelmez está procurando tirar o coração de Ymir de nós.";
	cutin "gl_barmund2",2;
	next;
	mes "[Varmundt]";
	mes "Ela pode até destruir todo este castelo se ela quiser.";
	npctalk "Varmundt: Ela pode até destruir todo este castelo se ela quisesse.";
	next;
	mes "[Varmundt]";
	mes "Depressa! Reporte ao senhor. Heinrich sobre Himelmez. Eu devo verificar a barreira de defesa aqui!";
	npctalk "Varmundt: Depressa! Reporte ao senhor. Heinrich sobre Himelmez. Eu devo verificar a barreira de defesa aqui!";
	close2;
	cutin "",255;
	disablenpc instance_npcname("Varmundt#0");
	enablenpc instance_npcname("Heinrich#1");
	enablenpc instance_npcname("Varmundt#1");
	end;
}

1@gl_k,145,104,6	script	Cavaleiro Khalitzburg#1	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "Meu turno de trabalho terminará em breve, mas a próxima equipe não virá.";
	close;
}

1@gl_k,154,104,3	script	Cavaleiro Khalitzburg#2	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "Algo que você quer me dizer?";
	close;
}

1@gl_k,145,99,6	script	Cavaleiro Branco#3	4_WHITEKNIGHT,{
	mes "[Cavaleiro Branco]";
	mes "Você está com Varmundt? O comandante está esperando por você.";
	close;
}

1@gl_k,154,99,3	duplicate(Cavaleiro Khalitzburg#1)	Cavaleiro Branco#4	4_WHITEKNIGHT

1@gl_k,145,94,6	script	Cavaleiro Khalitzburg#5	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "Eu tive um sonho estranho na noite passada. Minha mãe estava nela... Me pergunto se ela está bem...";
	close;
}

1@gl_k,154,94,3	duplicate(Cavaleiro Branco#3)	Cavaleiro Khalitzburg#6	4_F_KHALITZBURG

1@gl_k,145,89,6	script	Cavaleiro Branco#7	4_WHITEKNIGHT,{
	mes "[Cavaleiro Branco]";
	mes "Por favor, não faça bagunça aqui.";
	close;
}
1@gl_k,154,89,3	duplicate(Cavaleiro Branco#7)	Cavaleiro Branco#8	4_WHITEKNIGHT

1@gl_k,145,84,6	script	Cavaleiro Khalitzburg#9	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "Pedidos, por favor.";
	close;
}
1@gl_k,154,84,3	duplicate(Cavaleiro Khalitzburg#9)	Cavaleiro Khalitzburg#10	4_F_KHALITZBURG

1@gl_k,145,79,6	script	Cavaleiro Branco#11	4_WHITEKNIGHT,{
	mes "[Cavaleiro Branco]";
	mes "Como você conhece Varmundt? Ouvi dizer que ele não é realmente um homem amigável...";
	close;
}
1@gl_k,154,79,3	duplicate(Cavaleiro Branco#11)	Cavaleiro Branco#12	4_WHITEKNIGHT

1@gl_k,145,74,6	script	Cavaleiro Khalitzburg#13	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "Este castelo não parece estranho de repente? Algo não está certo.";
	close;
}
1@gl_k,154,74,3	duplicate(Cavaleiro Khalitzburg#13)	Cavaleiro Khalitzburg#14	4_F_KHALITZBURG

1@gl_k,145,69,6	script	Cavaleiro Branco#15	4_WHITEKNIGHT,{
	mes "[Cavaleiro Branco]";
	mes "Eu não gosto de conversar durante o trabalho.";
	close;
}
1@gl_k,154,69,3	duplicate(Cavaleiro Branco#15)	Cavaleiro Branco#16	4_WHITEKNIGHT

1@gl_k,145,64,6	script	Cavaleiro Khalitzburg#17	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "...";
	close;
}
1@gl_k,154,64,3	duplicate(Cavaleiro Khalitzburg#17)	Cavaleiro Khalitzburg#18	4_F_KHALITZBURG

1@gl_k,145,59,6	script	Cavaleiro Branco#19	4_WHITEKNIGHT,{
	mes "[Cavaleiro Branco]";
	mes "O que posso fazer por você?";
	close;
}
1@gl_k,154,59,3	duplicate(Cavaleiro Branco#19)	Cavaleiro Branco#20	4_WHITEKNIGHT

1@gl_k,145,54,6	script	Cavaleiro Khalitzburg#21	4_F_KHALITZBURG,{
	mes "[Cavaleiro Khalitzburg]";
	mes "Tem algo que você precisa?";
	close;
}
1@gl_k,154,54,3	duplicate(Cavaleiro Khalitzburg#21)	Cavaleiro Khalitzburg#22	4_F_KHALITZBURG

1@gl_k,149,100,6	script	Heinrich#1	4_M_HEINRICH,{
	if (is_party_leader() == false) {
		mes "[Heinrich]";
		mes "Onde está seu líder? Eu preciso falar com ele.";
		cutin "gl_heinrich2",2;
		close3;
	}
	.@account_id=getcharid(3);
	.@player_name$=strcharinfo(0);
	cutin "gl_heinrich2",2;
	select("Sr. Heinrich. Varmundt...");
	mes "["+.@player_name$+"]";
	mes "Sr. Heinrich. Você sabe o que está acontecendo no castelo agora?";
	unittalk .@account_id, .@player_name$+" : Sr. Heinrich. Você sabe o que está acontecendo no castelo agora?";
	next;
	mes "[Heinrich]";
	mes "Você não é o aventureiro que veio junto com Varmundt?";
	npctalk "Heinrich: Você não é o aventureiro que veio junto com Varmundt?";
	next;
	mes "[Heinrich]";
	mes "O que é isso? Algo errado com o castelo?";
	npctalk "Heinrich: O que é isso? Algo errado com o castelo?";
	next;
	select("Invasão de Himelmez...");
	mes "["+.@player_name$+"]";
	mes "Valquíria do Homem Morto, Himelmez está vindo para pegar o pedaço do Coração de Ymir escondido dentro deste castelo!";
	unittalk .@account_id, .@player_name$+" : Valquíria do Homem Morto, Himelmez está vindo para pegar a peça Coração de Ymir escondida dentro deste castelo!";
	next;
	mes "[Heinrich]";
	mes "Haha. Engraçado. Você realmente acha que isso é possível?";
	npctalk "Heinrich: Haha. Engraçado. Você realmente acha que isso é possível?";
	cutin "gl_heinrich1",2;
	next;
	mes "[Varmundt]";
	mes "Eu não estou brincando, Sr. Heinrich. Se eu estiver correto, então ela estará aqui muito em breve.";
	npctalk "Varmundt: Eu não estou brincando, Sr. Heinrich. Se eu estiver correto, então ela estará aqui muito em breve.",Instance_npcname("Varmundt#1");
	cutin "gl_barmund2",2;
	next;
	mes "[Varmundt]";
	mes "Precisamos esconder o pedaço de coração em um lugar seguro antes que o ataque de Himelmez comece!";
	npctalk "Varmundt: Devemos esconder o pedaço de coração em um lugar seguro antes do ataque de Himelmez começar!",Instance_npcname("Varmundt#1");
	next;
	select("Mesmo que você não acredite em mim...");
	mes "["+.@player_name$+"]";
	mes "Eu não posso fazer você acreditar em mim, mas não há tempo para discutir!";
	unittalk .@account_id, .@player_name$+" : Eu não posso fazer você acreditar em mim, mas não há tempo para discutir!";
	cutin "",255;
	next;
	mes "[Heinrich]";
	mes "Obrigado pela ajuda. Mas nem mesmo temos nosso rei conosco agora.";
	npctalk "Heinrich: Obrigado pela ajuda. Mas nós nem sequer temos o nosso rei conosco agora.";
	cutin "gl_heinrich1",2;
	next;
	mes "[Heinrich]";
	mes "Não podemos arriscar mover o coração só porque algum estranho diz isso.";
	npctalk "Heinrich: Não podemos arriscar mover o coração só porque algum estranho diz isso.";
	close2;
	disablenpc instance_npcname("Heinrich#1");
	enablenpc instance_npcname("Heinrich#2");
	enablenpc instance_npcname("Himelmez#1");
	donpcevent instance_npcname("#talkinstance1")+"::OnEnable";
	cutin "",255;
	end;
}

1@gl_k,152,97,3	script	Varmundt#1	4_M_BARMUND,{ end; }
1@gl_k,149,97,5	duplicate(Varmundt#1)	Heinrich#2	4_M_HEINRICH
1@gl_k,149,100,5	duplicate(Varmundt#1)	Heinrich#3	4_M_HEINRICH
1@gl_k,149,89,1	duplicate(Varmundt#1)	Himelmez#1	4_F_HIMEL

// Control Timer
//============================================================
1@gl_k,1,1,0	script	#talkinstance1	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	mapannounce 'map_name$[0], "????'s: Muahahahaha~!",bc_map,0xFFFF00,FW_NORMAL,18;
	initnpctimer;
	'npc_himelmez1$=instance_npcname("Himelmez#1");
	'npc_varmundt1$=instance_npcname("Varmundt#1");
	'npc_heinrich2$=instance_npcname("Heinrich#2");
	'npc_heinrich3$=instance_npcname("Heinrich#3");
	end;
OnTimer1500:
	npctalk "Heinrich: Quem é você?",'npc_heinrich2$;
	end;
OnTimer4500:
	npctalk "Himelmez: Bem, bem~ Estou interrompendo você? Você não estava me esperando?",'npc_himelmez1$;
	end;
OnTimer10000:
	npctalk "Varmundt: Himelmez!! Já!",'npc_varmundt1$;
	end;
OnTimer17500:
	npctalk "Himelmez: Meu nome é Lisa Kahn Himelmez. Mestre de Dullahan, valquíria do Homem Morto, é como me chamam.",'npc_himelmez1$;
	end;
OnTimer22000:
	npctalk "Heinrich: Você não é um pouco feminino demais para ser o Governante da morte? Não temos medo de você...",'npc_heinrich2$;
	end;
OnTimer28500:
	npctalk "Himelmez: Vamos ver se você pode relaxar assim depois de descobrir onde seu rei está.",'npc_himelmez1$;
	end;
OnTimer36000:
	npctalk "Heinrich: O quê?",'npc_heinrich2$;
	end;
OnTimer41000:
	npctalk "Himelmez: Hmm, agora eu tenho sua atenção, não é?",'npc_himelmez1$;
	end;
OnTimer47000:
	npctalk "Himelmez: Você não quer a posição do rei, apenas espere ele voltar. Eu gosto da sua pureza.",'npc_himelmez1$;
	end;
OnTimer54000:
	npctalk "Himelmez: É uma pena conhecê-lo em uma situação como esta.",'npc_himelmez1$;
	end;
OnTimer59000:
	npctalk "Heinrich: Meu rei está visitando a família real de Rune-Midgarts. Ele ainda não voltou.",'npc_heinrich2$;
	end;
OnTimer66500:
	npctalk "Heinrich: E agora você está tentando me enganar, o que aconteceu com ele ?!",'npc_heinrich2$;
	end;
OnTimer71500:
	npctalk "Himelmez: Bem~ Eu adoraria sentar e explicar para você, mas estou meio ocupado hoje~",'npc_himelmez1$;
	end;
OnTimer78000:
	npctalk "Himelmez: Eu tenho negócios para cuidar. Meus lacaios vão te tratar bem o suficiente para mim~",'npc_himelmez1$;
	end;
OnTimer84500:
	npctalk "Heinrich: Ela provavelmente já sabe onde está o coração do Ymir.",'npc_heinrich2$;
	disablenpc 'npc_himelmez1$;
	end;
OnTimer90500:
	npctalk "Heinrich: Todos os Khalitzburg e White Knights devem me seguir agora...",'npc_heinrich2$;
	end;
OnTimer92000:
	for(.@i=1; .@i<=20; .@i+=4) {
		hideonnpc instance_npcname("Cavaleiro Khalitzburg#"+.@i);
		hideonnpc instance_npcname("Cavaleiro Khalitzburg#"+(.@i+1));
		hideonnpc instance_npcname("Cavaleiro Branco#"+(.@i+2));
		hideonnpc instance_npcname("Cavaleiro Branco#"+(.@i+3));
	}
	hideonnpc instance_npcname("Cavaleiro Khalitzburg#21");
	hideonnpc instance_npcname("Cavaleiro Khalitzburg#22");

	.@label$=instance_npcname("#talkinstance1")+"::OnMyMobDead";
	monster 'map_name$[0],145,59,"Cavaleiro do Abismo",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
	monster 'map_name$[0],154,59,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],145,69,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],154,69,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],145,79,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],154,79,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],145,89,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],154,89,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],145,99,"Cavaleiro do Abismo",2470,1,.@label$;
	monster 'map_name$[0],154,99,"Cavaleiro do Abismo",2470,1,.@label$;

	monster 'map_name$[0],145,54, "Khalitzburg",2471,1,.@label$;// G_MG_KHALITZBURG
	unittalk $@mobid[0], "Água... Alguém me dê água...";
	monster 'map_name$[0],154,54, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Kkkrrrruughgh...";
	monster 'map_name$[0],145,64, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Sr. Heinrich. Salve-me...";
	monster 'map_name$[0],154,64, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Sinto falta da minha irmã...";
	monster 'map_name$[0],145,74, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Não me deixe em paz, me ajude.";
	monster 'map_name$[0],154,74, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Aaarrrrrhhhh";
	monster 'map_name$[0],145,84, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Estou com muita sede";
	monster 'map_name$[0],154,84, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Oh... Não... eu não posso morrer ainda...";
	monster 'map_name$[0],145,94, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Eu me sinto mal ao meu estômago urrgg";
	monster 'map_name$[0],154,94, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Alguém pode...";
	monster 'map_name$[0],145,104, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Arrgg... Meu corpo";
	monster 'map_name$[0],154,104, "Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0], "Minha garganta está queimando!";
	end;
OnTimer94000:
	npctalk "Heinrich: Meus homens... Isso não pode estar acontecendo!",'npc_heinrich2$;
	end;
OnTimer96500:
	npctalk "Varmundt: Sr. Heinrich, eles são todos monstros. Você precisa dar-lhes descanso!",'npc_varmundt1$;
	end;
OnTimer100000:
	npctalk "Heinrich: Eu sinto muito...",'npc_heinrich2$;
	end;
OnTimer103000:
	npctalk "Heinrich: Me desculpe, meus cavaleiros! Me perdoe!",'npc_heinrich2$;
	end;
OnTimer106000:
	mapannounce 'map_name$[0], "Sr. Heinrich: Morte a todos!",bc_map,0xFFFF33, FW_NORMAL,15;
	end;
OnTimer109000:
	mapannounce 'map_name$[0], "Sr. Heinrich: Volte para a escuridão!",bc_map,0xFFFF33, FW_NORMAL,15;
	end;
OnTimer109500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#1");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#2");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#1");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#2");
	end;
OnTimer110000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#3");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#4");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#5");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#6");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#3");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#4");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#5");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#6");
	end;
OnTimer110500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#7");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#8");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#9");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#10");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#7");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#8");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#9");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#10");
	end;
OnTimer111000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#11");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#12");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#13");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#14");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#11");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#12");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#13");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#14");
	end;
OnTimer111500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#15");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#16");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#17");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#18");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#15");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#16");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#17");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#18");
	end;
OnTimer112000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#19");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Branco#20");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#21");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Cavaleiro Khalitzburg#22");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#19");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Branco#20");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#21");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Cavaleiro Khalitzburg#22");
	disablenpc 'npc_heinrich2$;
	enablenpc 'npc_heinrich3$;
	killmonster 'map_name$[0], instance_npcname("#talkinstance1")+"::OnMyMobDead";
	end;
OnTimer112500:
	npctalk "Heinrich: Eu matei meus próprios homens...",'npc_heinrich3$;
	end;
OnTimer115500:
	npctalk "Varmundt: Sr. Heinrich! Nós não temos muito tempo!",'npc_varmundt1$;
	end;
OnTimer118500:
	npctalk "Varmundt: Se você se apressar agora, há uma chance!",'npc_varmundt1$;
	end;
OnTimer121500:
	npctalk "Heinrich: Varmundt está certo. Agora não é a hora do luto.",'npc_heinrich3$;
	end;
OnTimer124500:
	npctalk "Heinrich: Eu tenho um pedido para você seguidores.",'npc_heinrich3$;
	end;
OnTimer127500:
	npctalk "Heinrich: Himelmez pode transformar seres vivos em monstros.",'npc_heinrich3$;
	end;
OnTimer130500:
	npctalk "Heinrich: Mas, ainda pode haver sobreviventes aqui.",'npc_heinrich3$;
	end;
OnTimer134500:
	npctalk "Heinrich: Destrua os monstros e encontre sobreviventes.",'npc_heinrich3$;
	end;
OnTimer138500:
	npctalk "Heinrich: Varmundt e eu vou perseguir Himelmez.",'npc_heinrich3$;
	end;
OnTimer143500:
	npctalk "Heinrich: Muito bem, Varmundt. Vamos encontrar Himelmez.",'npc_heinrich3$;
	end;
OnTimer147500:
	disablenpc 'npc_heinrich3$;
	end;
OnTimer148500:
	stopnpctimer;
	donpcevent instance_npcname("#ghmemorialmob01")+"::OnStart";

	disablenpc 'npc_varmundt1$;
	disablenpc instance_npcname("#talkinstance1");
	'npc_himelmez1$='npc_varmundt1$='npc_heinrich2$='npc_heinrich3$="";
	end;
OnMyMobDead:
	end;
}

// Setor 1 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob01	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob01");
	enablenpc instance_npcname("#2Control");
	enablenpc instance_npcname("#2Control2");
	mapannounce 'map_name$[0], "Um portal foi aberto para o oeste.",bc_map,0xFFFF00;

	// nota: x17 MG_GHOUL / x17 MG_ZOMBIE / x17 MG_WRAITH
	.@label$=instance_npcname("#ghmemorialmob01")+"::OnMyMobDead";
	areamonster 'map_name$[0],76,99,87,10,"Grande Chamberlain em dor",2466,14,.@label$;
	areamonster 'map_name$[0],67,39,12,6,"Monge Corrompido",2465,17,.@label$;
	areamonster 'map_name$[0],67,39,12,6,"Grande Chamberlain em dor",2466,3,.@label$;
	areamonster 'map_name$[0],32,75,51,58,"Comissário Corrompido",2464,5,.@label$;
	areamonster 'map_name$[0],45,84,6,137,"Comissário Corrompido",2464,12,.@label$;
	end;
OnMyMobDead:
	.@label$=instance_npcname("#ghmemorialmob01")+"::OnMyMobDead";
	.@mob_dead_num=51 - mobcount('map_name$[0],.@label$);
	if(.@mob_dead_num>35) {
		mapannounce 'map_name$[0], "A maldição de Himelmez está enfraquecendo. Encontre sobreviventes!",bc_map,0xFFFFFF;
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("Ministrante Domun#1");
		disablenpc instance_npcname("#ghmemorialmob01");
	}
	end;
}

1@gl_k,17,51,3	script	Ministrante Domun#1	4_M_KID1,{
	if (is_party_leader() == false) {
		mes "[Ministrante Domun]";
		mes "Salve-me, salve-me, por favor...";
		close;
	}
	.@account_id=getcharid(3);
	.@player_name$=strcharinfo(0);
	mes "[Ministrante Domun]";
	mes "Salve-me! Salve-me!!!";
	npctalk "Salve-me! Salve-me!!!";
	next;
	select("Espere! Você é o único sobrevivente?");
	mes "["+.@player_name$+"]";
	mes "Espere! Você é o único sobrevivente?";
	unittalk .@account_id, .@player_name$+" : Espere! Você é o único sobrevivente?";
	next;
	mes "[Ministrante Domun]";
	mes "Chamberlains... monges... Eles se transformaram em monstros. Eu não pude fazer nada.";
	npctalk "Ministrante Domun: Chamberlains... monges... Todos eles se transformaram em monstros. Eu não pude fazer nada.";
	next;
	mes "[Ministrante Domun]";
	mes "Tudo o que eu pude fazer foi... Apenas esconder aqui... Nada, nada que eu pudesse fazer...";
	npctalk "Ministrante Domun: Tudo o que eu pude fazer foi... apenas esconder aqui... Nada, nada que eu pudesse fazer...";
	next;
	select("Puxe juntos!");
	mes "["+.@player_name$+"]";
	mes "Acorde garoto! Vá para o leste e encontre a passagem do meio para o lado de fora! É seguro lá!";
	unittalk .@account_id, .@player_name$+" : Acorde garoto! Vá para o leste e encontre a passagem do meio para o lado de fora! É seguro lá!";
	next;
	mes "[Ministrante Domun]";
	mes "Para a passagem leste? Sozinha? Como?";
	npctalk "Ministrante Domun: Para a passagem leste? Sozinho? Como?";
	next;
	select("Eu te darei uma arma.");
	mes "["+.@player_name$+"]";
	mes "Aqui está uma arma. Apenas feche seus olhos e gire para esses monstros.";
	unittalk .@account_id, .@player_name$+" : Aqui está uma arma. Apenas feche seus olhos e gire para esses monstros.";
	next;
	mes "[Ministrante Domun]";
	mes "Ok, eu vou tentar.";
	npctalk "Ministrante Domun: Ok, eu... vou tentar.";
	disablenpc instance_npcname("Ministrante Domun#1");
	donpcevent instance_npcname("#ghmemorialmob02")+"::OnStart";
	close;
}

// Setor 2 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob02	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob02");
	enablenpc instance_npcname("#3Control");
	enablenpc instance_npcname("#3Control2");
	for(.@i=1; .@i<=26; .@i++)
		enablenpc instance_npcname("Um Homem Morto#"+.@i);
	mapannounce 'map_name$[0], "Um portal abriu para o leste.",bc_map,0xFFFF00;

	// note: x34 MG_ZOMBIE / x35 MG_GHOUL
	.@label$=instance_npcname("#ghmemorialmob02")+"::OnMyMobDead";
	areamonster 'map_name$[0],236,12,288,52, "Outraged Refiner",2466,18,.@label$;// MG_GHOUL
	areamonster 'map_name$[0],236,12,288,52, "Decomposed Blacksmith",2464,17,.@label$;// MG_ZOMBIE
	areamonster 'map_name$[0],242,71,286,145, "Outraged Refiner",2466,17,.@label$;
	areamonster 'map_name$[0],242,71,286,145, "Decomposed Blacksmith",2464,17,.@label$;
	end;
OnMyMobDead:
	.@label$=instance_npcname("#ghmemorialmob02")+"::OnMyMobDead";
	.@mob_dead_num=69 - mobcount('map_name$[0],.@label$);
	if(.@mob_dead_num>56) {
		mapannounce 'map_name$[0], "A maldição de Himelmez está enfraquecendo. Encontre sobreviventes!",bc_map,0xFFFFFF;
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("Holgren, o Destruidor#1");
		disablenpc instance_npcname("#ghmemorialmob02");
	}
	end;
}

1@gl_k,291,145,3	script	Holgren, o Destruidor#1	4_F_JOB_BLACKSMITH,{
	if (is_party_leader() == false) {
		mes "[Holgren, o Destruidor]";
		mes "Você é humano?";
		close;
	}
	.@account_id=getcharid(3);
	.@player_name$=strcharinfo(0);
	mes "[Holgren, o Destruidor]";
	mes "Morra!! Você deve morrer!!!";
	npctalk "Holgren o Destruidor: Morra!! Você deve morrer!!!";
	specialeffect EF_CRASHEARTH;
	next;
	select("Relaxe! Eu não sou um monstro!");
	mes "["+.@player_name$+"]";
	mes "Relaxe! Eu sou Humano. Você está sozinho? Tem mais alguém aqui?";
	unittalk .@account_id, .@player_name$+" : Relaxe! Eu sou Humano. Você está sozinho? Tem mais alguém aqui?";
	next;
	mes "[Holgren, o Destruidor]";
	mes "É só eu.";
	npctalk "Holgren, o Destruidor: É só eu";
	next;
	select("É perigoso aqui...");
	mes "["+.@player_name$+"]";
	mes "É perigoso aqui. Você sabe o caminho para a passagem do meio? Você pode se mover?";
	unittalk .@account_id, .@player_name$+" : É perigoso aqui. Você sabe o caminho para a passagem do meio? Você pode se mover?";
	next;
	mes "[Holgren, o Destruidor]";
	mes "Sim, eu posso me mover. Eu preciso sair daqui.";
	npctalk "Holgren, o Destruidor: Sim, eu posso me mover. Eu preciso sair daqui.";
	next;
	select("Esteja seguro...");
	mes "["+.@player_name$+"]";
	mes "Desculpe, eu não posso ir com você.";
	unittalk .@account_id, .@player_name$+" : Desculpe, não posso ir com você.";
	next;
	mes "[Holgren, o Destruidor]";
	mes "Tudo bem. Você já fez o suficiente. Eu posso me ajudar. Boa sorte para você também.";
	npctalk "Holgren, o Destruidor: Tudo bem. Você já fez o suficiente. Eu posso me ajudar. Boa sorte para você também.";
	disablenpc instance_npcname("Holgren, o Destruidor#1");
	donpcevent instance_npcname("#ghmemorialmob03")+"::OnEnable";
	close;
}

// Mobs Tramp
//============================================================
1@gl_k,221,82,3	script	Um Homem Morto#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	.@i=rand(1,10);
	if(.@i==1) .@mobs=3;
	else if(.@i==2) .@mobs=4;
	else if(.@i==3) .@mobs=5;
	else if(.@i<7) .@mobs=6;
	else .@mobs=7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
1@gl_k,233,123,4	duplicate(Um Homem Morto#1)	Um Homem Morto#2	4_M_DIEMAN,5,5
1@gl_k,258,150,2	duplicate(Um Homem Morto#1)	Um Homem Morto#3	4_M_DIEMAN,5,5
1@gl_k,255,157,5	duplicate(Um Homem Morto#1)	Um Homem Morto#4	4_M_DIEMAN,5,5
1@gl_k,280,167,4	duplicate(Um Homem Morto#1)	Um Homem Morto#5	4_M_DIEMAN,5,5
1@gl_k,293,161,2	duplicate(Um Homem Morto#1)	Um Homem Morto#6	4_M_DIEMAN,5,5
1@gl_k,249,101,3	duplicate(Um Homem Morto#1)	Um Homem Morto#7	4_M_DIEMAN,5,5
1@gl_k,241,86,6	duplicate(Um Homem Morto#1)	Um Homem Morto#8	4_M_DIEMAN,5,5
1@gl_k,246,62,0	duplicate(Um Homem Morto#1)	Um Homem Morto#9	4_M_DIEMAN,5,5
1@gl_k,240,43,7	duplicate(Um Homem Morto#1)	Um Homem Morto#10	4_M_DIEMAN,5,5
1@gl_k,271,19,1	duplicate(Um Homem Morto#1)	Um Homem Morto#11	4_M_DIEMAN,5,5
1@gl_k,282,48,5	duplicate(Um Homem Morto#1)	Um Homem Morto#12	4_M_DIEMAN,5,5
1@gl_k,285,81,7	duplicate(Um Homem Morto#1)	Um Homem Morto#13	4_M_DIEMAN,5,5
1@gl_k,276,106,0	duplicate(Um Homem Morto#1)	Um Homem Morto#14	4_M_DIEMAN,5,5
1@gl_k,261,164,0	duplicate(Um Homem Morto#1)	Um Homem Morto#15	4_M_DIEMAN,5,5
1@gl_k,269,173,0	duplicate(Um Homem Morto#1)	Um Homem Morto#16	4_M_DIEMAN,5,5
1@gl_k,252,120,0	duplicate(Um Homem Morto#1)	Um Homem Morto#17	4_M_DIEMAN,5,5
1@gl_k,213,63,0	duplicate(Um Homem Morto#1)	Um Homem Morto#18	4_M_DIEMAN,5,5
1@gl_k,222,39,0	duplicate(Um Homem Morto#1)	Um Homem Morto#19	4_M_DIEMAN,5,5
1@gl_k,214,27,0	duplicate(Um Homem Morto#1)	Um Homem Morto#20	4_M_DIEMAN,5,5
1@gl_k,223,17,1	duplicate(Um Homem Morto#1)	Um Homem Morto#21	4_M_DIEMAN,5,5
1@gl_k,230,50,2	duplicate(Um Homem Morto#1)	Um Homem Morto#22	4_M_DIEMAN,5,5
1@gl_k,235,16,3	duplicate(Um Homem Morto#1)	Um Homem Morto#23	4_M_DIEMAN,5,5
1@gl_k,226,96,4	duplicate(Um Homem Morto#1)	Um Homem Morto#24	4_M_DIEMAN,5,5
1@gl_k,222,119,5	duplicate(Um Homem Morto#1)	Um Homem Morto#25	4_M_DIEMAN,5,5
1@gl_k,251,20,6	duplicate(Um Homem Morto#1)	Um Homem Morto#26	4_M_DIEMAN,5,5

// Setor 3 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob03	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob03");
	enablenpc instance_npcname("#4Control");
	enablenpc instance_npcname("#4Control2");
	mapannounce 'map_name$[0], "Um portal apareceu no noroeste.",bc_map,0xFFFF00;

	// note: x50 MG_RAYDRIC / x50 MG_RAYDRIC_ARCHER
	.@label$=instance_npcname("#ghmemorialmob03")+"::OnMyMobDead";
	areamonster 'map_name$[0],16,186,43,280, "Outraged Archer",2469,25,.@label$;
	areamonster 'map_name$[0],44,191,89,225, "Hungry Palace Guard",2468,25,.@label$;
	areamonster 'map_name$[0],115,215,188,273, "Corrupted Palace Guard",2468,20,.@label$;
	areamonster 'map_name$[0],108,232,193,281, "Corrupted Palace Guard",2468,5,.@label$;
	areamonster 'map_name$[0],158,236,230,250, "Arqueiro da Morte",2469,12,.@label$;
	areamonster 'map_name$[0],65,231,155,256, "Arqueiro da Morte",2469,13,.@label$;
	end;
OnMyMobDead:
	.@label$=instance_npcname("#ghmemorialmob03")+"::OnMyMobDead";
	.@mob_dead_num=100 - mobcount('map_name$[0],.@label$);
	if(.@mob_dead_num>85) {
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("#Mimelon");
		enablenpc instance_npcname("Himelmez#2");
		enablenpc instance_npcname("Varmundt#2");
		enablenpc instance_npcname("Heinrich#4");
		disablenpc instance_npcname("#ghmemorialmob03");
		mapannounce 'map_name$[0], "Himelmez: Nada mal. Pensei que você estaria morto agora pelas minhas criaturas~",bc_map,0xFFFFFF;
		initnpctimer;
	}
	end;
OnTimer5000:
	mapannounce 'map_name$[0], "Himelmez: Eu esperarei por você no norte do castelo. Eu quero ver o quão sortudo você é.",bc_map,0xFFFFFF;
	stopnpctimer;
	end;
}

// 1º MVP
//============================================================
1@gl_k,144,258,6	script	Heinrich#4	4_M_HEINRICH,{
	mes "[Heinrich]";
	mes "Himelmez... Eu nunca esquecerei o que você fez aos meus homens.";
	cutin "gl_heinrich1",2;
	close3;
}

// note: nunca escondido
1@gl_k,156,258,3	script	Varmundt#2	4_M_BARMUND,{
	cutin "gl_barmund1",2;
	mes "[Varmundt]";
	if(checkquest(12318,HUNTING) !=2) {
		mes "Eu acho que é isso. É impossível parar a vontade do viajante no tempo ?!";
		mes "Um portal parece ter aparecido ao norte.";
		close3;
	}
	erasequest 12318;
	if(isbegin_quest(12319)==0)
		setquest 12319;// Amdarais Caça
	if(isbegin_quest(12320)==0) {
		setquest 12320;// Time Traveler
		completequest 12320;
		getexp 250000,250000;
	}
	mes "Este item tem uma aura extraordinária com ele. Pode ser muito útil para alguém especial.";
	getitem 6607,1;// Temporal_Crystal
	getitem 6608,1;// Coagulated_Spell
	close3;
}

1@gl_k,150,257,3	script	Himelmez#2	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "Não me olhe nervosa assim. Acabará em breve...";
	close3;
}

1@gl_k,150,257,0	script	#Mimelon	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Mimelon");
	initnpctimer;
	'npc_himelmez2$=instance_npcname("Himelmez#2");
	'npc_heinrich4$=instance_npcname("Heinrich#4");
	end;
OnTimer3000:
	npctalk "Himelmez: Acho que você é sortudo ou poderoso para chegar até aqui.",'npc_himelmez2$;
	end;
OnTimer6000:
	npctalk "Himelmez: Mas isso não importa.",'npc_himelmez2$;
	end;
OnTimer9000:
	npctalk "Himelmez: Vocês todos vão morrer aqui.",'npc_himelmez2$;
	end;
OnTimer12000:
	npctalk "Heinrich: Himelmez! Nós não vamos deixar você se afastar de nós!",'npc_heinrich4$;
	end;
OnTimer15000:
	npctalk "Himelmez: Ha ha ha, preocupe-se consigo mesmo. Você acha que estou sozinho aqui?",'npc_himelmez2$;
	end;
OnTimer18000:
	npctalk "Varmundt: Sr. Heinrich! Sinto algo estranho e forte vindo!",Instance_npcname("Varmundt#2");
	end;
OnTimer21000:
	npctalk "Heinrich: O que... Estes são!",'npc_heinrich4$;
	end;
OnTimer24000:
	npctalk "Himelmez: Eu seguirei meu caminho enquanto meu novo brinquedo te diverte.",'npc_himelmez2$;
	end;
OnTimer27000:
	npctalk "Himelmez: Fare bem~, espero vê-lo novamente algum dia, Heinrich.",'npc_himelmez2$;
	end;
OnTimer28000:
	disablenpc 'npc_himelmez2$;
	end;
OnTimer31000:
	npctalk "Heinrich: Sir Varmundt! Ajude os aventureiros. Eu irei atrás de Himelmez!",'npc_heinrich4$;
	end;
OnTimer32000:
	disablenpc 'npc_heinrich4$;
	end;
OnTimer35000:
	mapannounce 'map_name$[0], "Um eco vem de dentro do santuário do cavaleiro.",bc_map,0xFFFFFF;
	end;
OnTimer38000:
	donpcevent instance_npcname("#ghmemorialmob04")+"::OnStart";
	stopnpctimer;
	'npc_himelmez2$='npc_heinrich4$="";
	end;
}

1@gl_k,1,1,0	script	#ghmemorialmob04	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob04");
	monster 'map_name$[0],150,259,"Alma Corrupta",2475,1, instance_npcname("#ghmemorialmob04")+"::OnMyMobDead";// MG_CORRUPTION_ROOT
	unittalk $@mobid[0], "Grrrrrrhh~~~";
	end;
OnMyMobDead:
	if(mobcount('map_name$[0], instance_npcname("#ghmemorialmob04")+"::OnMyMobDead")<1) {
		mapannounce 'map_name$[0], "Abrindo a entrada do segundo andar na direção das 12 horas.",bc_map,0xFFFF00;
		npctalk "Varmundt: Eu coletei alguns itens removidos de seu corpo. Você pode pegar de mim.",Instance_npcname("Varmundt#2");

		enablenpc instance_npcname("#2F Entrance");
		enablenpc instance_npcname("#1 Control");
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		enablenpc instance_npcname("#Servanton");
		enablenpc instance_npcname("Heinrich#21");
		enablenpc instance_npcname("Varmundt#21");
		donpcevent instance_npcname("#ghmemorialmob05") + "::OnStart";
		disablenpc instance_npcname("#ghmemorialmob04");
	}
	end;
}

// andar 2
//============================================================
2@gl_k,148,67,1	script	Heinrich#21	4_M_HEINRICH,{
	mes "[Heinrich]";
	mes "O espaço fechado de Himelmez cobre todos os lugares...";
	cutin "gl_heinrich1",2;
	close3;
}

2@gl_k,151,71,7	script	Varmundt#21	4_M_BARMUND,{
	mes "[Varmundt]";
	mes "Quantas vezes eu fiz este trabalho? No meu sonho, eu fiz isso de novo e de novo...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,150,66,0	script	#Servanton_effect	HIDDEN_WARP_NPC,{
	end;
OnEffect:
	specialeffect EF_LORD;
	end;
}

// note: esta parte pode ser ignorada
2@gl_k,150,66,0	script	#Servanton	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Servanton");
	initnpctimer;
	'npc_heinrich$=instance_npcname("Heinrich#21");
	'npc_varmundt$=instance_npcname("Varmundt#21");
	end;
OnTimer3000:
	npctalk "Heinrich: Esta estrutura não estava no castelo antes!",'npc_heinrich$;
	end;
OnTimer6000:
	npctalk "Varmundt: Este é o espaço fechado de Himelmez. Nós bloqueamos cada área e corredor.",'npc_varmundt$;
	end;
OnTimer9000:
	npctalk "Heinrich: Nós tentamos quebrá-lo, mas é muito forte.",'npc_heinrich$;
	end;
OnTimer12000:
	npctalk "Varmundt: Dê um passo atrás. Deixe-me tentar dissipar a magia.",'npc_varmundt$;
	end;
OnTimer15000:
	specialeffect EF_LORD,AREA, instance_npcname("#Servanton_effect");
	disablenpc instance_npcname("#Servanton_effect");
	end;
OnTimer18000:
	npctalk "Varmundt: Eu acho que parte do espaço fechado está quebrada.",'npc_varmundt$;
	end;
OnTimer21000:
	npctalk "Varmundt: Eu nunca vi esse espaço fechado.",'npc_varmundt$;
	end;
OnTimer24000:
	npctalk "Varmundt: Himelmez esconde correntes de pedra no corpo das pessoas não mortas.",'npc_varmundt$;
	end;
OnTimer27000:
	npctalk "Varmundt: Talvez este espaço fechado seja mantido com estas correntes de pedra.",'npc_varmundt$;
	end;
OnTimer30000:
	npctalk "Varmundt: Se você quiser quebrá-lo, você tem que matar um daqueles que têm uma corrente de pedra.",'npc_varmundt$;
	end;
OnTimer33000:
	npctalk "Varmundt: Mas nós não podemos reconhecer quem tem uma cadeia de pedras, então temos que purificar tudo.",'npc_varmundt$;
	end;
OnTimer36000:
	npctalk "Heinrich: É um comportamento indesculpável.",'npc_heinrich$;
	end;
OnTimer39000:
	npctalk "Heinrich: Eu já exterminou todos os meus soldados e talvez mais...",'npc_heinrich$;
	end;
OnTimer42000:
	npctalk "Varmundt: Commander...",'npc_varmundt$;
	end;
OnTimer45000:
	npctalk "Varmundt: Comandante! Você precisa ser forte.",'npc_varmundt$;
	end;
OnTimer48000:
	npctalk "Varmundt: Nosso inimigo não é humano.",'npc_varmundt$;
	end;
OnTimer51000:
	npctalk "Varmundt: Mesmo que você negue, eles nunca voltarão a ser humanos.",'npc_varmundt$;
	end;
OnTimer54000:
	npctalk "Heinrich: ...",'npc_heinrich$;
	end;
OnTimer57000:
	npctalk "Varmundt: Bem, vamos lá então.",'npc_varmundt$;
	end;
OnTimer60000:
	npctalk "Varmundt: E vocês, apenas façam uma pausa e sigam-nos.",'npc_varmundt$;
	end;
OnTimer63000:
	npctalk "Varmundt: Pode ser difícil lutar, então fique forte.",'npc_varmundt$;
	end;
OnTimer66000:
	npctalk "Varmundt: Ok, é hora de começar Heinrich.",'npc_varmundt$;
	end;
OnTimer69000:
	mapannounce 'map_name$[1], "Um portal para o oeste abriu no corredor central.",bc_map,0xFFFF00;
	disablenpc 'npc_varmundt$;
	disablenpc 'npc_heinrich$;
	stopnpctimer;
	'npc_varmundt$ = 'npc_heinrich$ = "";
	enablenpc instance_npcname("#22 Control");
	enablenpc instance_npcname("#22 Control2");
	end;
}

// Pontos
// Nota: o temporizador é a única condição para eles reaparecerem
2@gl_k,118,141,0	script	#ogh_2-1	HIDDEN_WARP_NPC,20,20,{
	end;
OnTouch_:
	disablenpc instance_npcname(strnpcinfo(0));
	.@hidden_name$=strnpcinfo(2);
	.@event_type=atoi(charat(.@hidden_name$,4));
	if(.@event_type==2)
		.@label$ = instance_npcname("#ghmemorialmob05") + "::OnMyMobDead";
	else
		.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	getmapxy .@map$,.@x,.@y, BL_NPC;
	monster .@map$,.@x,.@y, "Corrupted Palace Guard", 2468,1, .@label$;
	monster .@map$,.@x,.@y, "Arqueiro da Morte", 2469,1, .@label$;
	monster .@map$,.@x,.@y, "Cavaleiro do Abismo Corrompido", 2470,1, .@label$;
	monster .@map$,.@x,.@y, "Suffered Khalitzburg", 2471,1, .@label$;
	monster .@map$,.@x,.@y, "Cavaleiro Sangrento", 2472,1, .@label$;
	initnpctimer;
	end;
OnTimer30000:
	enablenpc instance_npcname(strnpcinfo(0));
	stopnpctimer;
	end;
OnStop:
	disablenpc instance_npcname(strnpcinfo(0));
	stopnpctimer;
	end;
}
2@gl_k,128,81,0	duplicate(#ogh_2-1)	#ogh_2-2	HIDDEN_WARP_NPC,20,20
2@gl_k,131,54,0	duplicate(#ogh_2-1)	#ogh_2-3	HIDDEN_WARP_NPC,20,20
2@gl_k,89,48,0	duplicate(#ogh_2-1)	#ogh_2-4	HIDDEN_WARP_NPC,20,20
2@gl_k,64,117,0	duplicate(#ogh_2-1)	#ogh_2-5	HIDDEN_WARP_NPC,20,20
2@gl_k,62,82,0	duplicate(#ogh_2-1)	#ogh_2-6	HIDDEN_WARP_NPC,20,20
2@gl_k,38,138,0	duplicate(#ogh_2-1)	#ogh_2-7	HIDDEN_WARP_NPC,20,20

2@gl_k,171,120,0	duplicate(#ogh_2-1)	#ogh_3-1	HIDDEN_WARP_NPC,20,20
2@gl_k,232,133,0	duplicate(#ogh_2-1)	#ogh_3-2	HIDDEN_WARP_NPC,20,20
2@gl_k,256,149,0	duplicate(#ogh_2-1)	#ogh_3-3	HIDDEN_WARP_NPC,20,20
2@gl_k,212,106,0	duplicate(#ogh_2-1)	#ogh_3-4	HIDDEN_WARP_NPC,20,20
2@gl_k,243,73,0	duplicate(#ogh_2-1)	#ogh_3-5	HIDDEN_WARP_NPC,20,20
2@gl_k,229,26,0	duplicate(#ogh_2-1)	#ogh_3-6	HIDDEN_WARP_NPC,20,20
2@gl_k,181,34,0	duplicate(#ogh_2-1)	#ogh_3-7	HIDDEN_WARP_NPC,20,20

// Comandante 1
2@gl_k,1,1,0	script	#ghmemorialmob05	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_2-" + .@i);
	end;

OnMyMobDead:
	if (rand(50) == 0) {// pode re-spawn
		mapannounce 'map_name$[1], "Evil Forces estão aparecendo nessa área.",bc_map,0xFFFF44, FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 41,146, 58,44, 122,148, 131,64;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "1º Comandante da Destruição",2473,1, instance_npcname("#ghmemorialmob05 ")+"::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "Um portal abriu para o leste no corredor central.",bc_map,0xFFFF00;
	enablenpc instance_npcname("#23 Control");
	enablenpc instance_npcname("#23 Control2");
	donpcevent instance_npcname("#ghmemorialmob06")+"::OnStart";

	disablenpc instance_npcname("#ghmemorialmob05");
	for(.@i=1; .@i<=7; .@i++)
		donpcevent instance_npcname("#ogh_2-"+.@i)+"::OnStop";
	end;
}

// Comandante 2
2@gl_k,1,1,0	script	#ghmemorialmob06	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob06");
	for(.@i=1; .@i<=7; .@i++)
		enablenpc instance_npcname("#ogh_3-"+.@i);

	// Fixar spawn
	.@label$=instance_npcname("#ghmemorialmob06")+"::OnMyMobDead";
	monster 'map_name$[1],252, 75, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],253, 76, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],247, 77, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],248, 80, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],236,100, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],240,100, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],242,100, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],244,100, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],199,111, "Corrupted Palace Guard",2468,1,.@label$;
	monster 'map_name$[1],181,107, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],177,110, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],236,27, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],242,27, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],252,26, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],172,130, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],171,127, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],173,127, "Arqueiro da Morte",2469,1,.@label$;
	monster 'map_name$[1],177,69,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],186,66,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],189,67,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],190,68,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],257,157,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],246,159,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],237,158,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],206,159,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],172,120,"Cavaleiro do Abismo Corrompido",2470,1,.@label$;
	monster 'map_name$[1],226,30, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],226,38, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],228,29, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],226,63, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],268,137, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],263,138, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],259,138, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],259,138, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],187,121, "Suffered Khalitzburg",2471,1,.@label$;
	monster 'map_name$[1],265, 52,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],265, 55,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],264, 79,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],225, 92,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],237, 92,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],249, 92,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],256, 93,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],264,100,"Cavaleiro do Abismo",2472,1,.@label$;
	monster 'map_name$[1],181,120,"Cavaleiro do Abismo",2472,1,.@label$;
	end;

OnMyMobDead:
	if(rand(50)==0) {// pode re-spawn
		mapannounce 'map_name$[1], "Uma presença maligna foi teleportada para esta área.",bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob06")+"::OnBossDead";

		.@r=rand(4)*2;
		setarray .@coord[0], 166,119, 211,45, 227,139, 245,74;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "2º Comandante da Destruição",2474,1, instance_npcname("#ghmemorialmob06")+"::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "Um novo portal apareceu no final do corredor central.",bc_map,0xFFFF00;
	enablenpc instance_npcname("#24 Control");
	enablenpc instance_npcname("#24 Control2");
	enablenpc instance_npcname("#Geron");
	for(.@i=1; .@i<=35; .@i++)
		enablenpc instance_npcname("Corpo Decomposto"+.@i);

	disablenpc instance_npcname("#ghmemorialmob06");
	for(.@i=1; .@i<=7; .@i++)
		donpcevent instance_npcname("#ogh_3-"+.@i)+"::OnStop";

	// mobs escondidos
	setarray .@coord[0],
		 37,265, 57,265, 77,265, 97,265, 117,265, 188,264, 208,264,
		 50,172, 70,172, 90,172, 110,172, 170,172, 210,172, 230,172,
		 88,214, 108,214, 128,214, 180,219, 200,219, 220,219, 240,219;
	.@size=getarraysize(.@coord);
	for(.@i=0; .@i<.@size; .@i+=2)
		monster 'map_name$[1], .@coord[.@i], .@coord[.@i+1], "Chama de destruição",2337,1;// HIDDEN_MOB
	end;
}

// Entrada do Quarto Amdarais
2@gl_k,150,180,0	script	#Geron	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Geron");
	mapannounce 'map_name$[1], "???: Não venha aqui! É uma armadilha... Kkkkah!!!",bc_map,0xFF7777,FW_NORMAL,15;
	enablenpc instance_npcname("Himelmez#22");
	enablenpc instance_npcname("Varmundt#23");
	enablenpc instance_npcname("Heinrich#23");
	enablenpc instance_npcname("Gerhalt#23");
	end;
}

2@gl_k,143,260,4	script	Corpo Decomposto#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname(strnpcinfo(0));
	.@i=rand(1,10);
	if(.@i==1) .@mobs=3;
	else if(.@i==2) .@mobs=4;
	else if(.@i==3) .@mobs=5;
	else if(.@i<7) .@mobs=6;
	else .@mobs=7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
	initnpctimer;
	end;
OnTimer45000:
	enablenpc instance_npcname(strnpcinfo(0));
	stopnpctimer;
	end;
}
2@gl_k,145,236,4	duplicate(Corpo Decomposto#1)	Corpo Decomposto#2	4_M_DIEMAN,5,5
2@gl_k,141,222,2	duplicate(Corpo Decomposto#1)	Corpo Decomposto#3	4_M_DIEMAN,5,5
2@gl_k,147,203,5	duplicate(Corpo Decomposto#1)	Corpo Decomposto#4	4_M_DIEMAN,5,5
2@gl_k,167,225,4	duplicate(Corpo Decomposto#1)	Corpo Decomposto#5	4_M_DIEMAN,5,5
2@gl_k,172,233,2	duplicate(Corpo Decomposto#1)	Corpo Decomposto#6	4_M_DIEMAN,5,5
2@gl_k,176,244,3	duplicate(Corpo Decomposto#1)	Corpo Decomposto#7	4_M_DIEMAN,5,5
2@gl_k,184,248,6	duplicate(Corpo Decomposto#1)	Corpo Decomposto#8	4_M_DIEMAN,5,5
2@gl_k,193,228,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#9	4_M_DIEMAN,5,5
2@gl_k,206,250,7	duplicate(Corpo Decomposto#1)	Corpo Decomposto#10	4_M_DIEMAN,5,5
2@gl_k,130,249,1	duplicate(Corpo Decomposto#1)	Corpo Decomposto#11	4_M_DIEMAN,5,5
2@gl_k,122,236,5	duplicate(Corpo Decomposto#1)	Corpo Decomposto#12	4_M_DIEMAN,5,5
2@gl_k,130,228,7	duplicate(Corpo Decomposto#1)	Corpo Decomposto#13	4_M_DIEMAN,5,5
2@gl_k,106,226,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#14	4_M_DIEMAN,5,5
2@gl_k,104,245,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#15	4_M_DIEMAN,5,5
2@gl_k,131,187,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#16	4_M_DIEMAN,5,5
2@gl_k,121,197,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#17	4_M_DIEMAN,5,5
2@gl_k,107,194,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#18	4_M_DIEMAN,5,5
2@gl_k,92,187,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#19	4_M_DIEMAN,5,5
2@gl_k,153,214,3	duplicate(Corpo Decomposto#1)	Corpo Decomposto#20	4_M_DIEMAN,5,5
2@gl_k,155,195,4	duplicate(Corpo Decomposto#1)	Corpo Decomposto#21	4_M_DIEMAN,5,5
2@gl_k,154,188,2	duplicate(Corpo Decomposto#1)	Corpo Decomposto#22	4_M_DIEMAN,5,5
2@gl_k,143,195,5	duplicate(Corpo Decomposto#1)	Corpo Decomposto#23	4_M_DIEMAN,5,5
2@gl_k,132,214,4	duplicate(Corpo Decomposto#1)	Corpo Decomposto#24	4_M_DIEMAN,5,5
2@gl_k,125,208,2	duplicate(Corpo Decomposto#1)	Corpo Decomposto#25	4_M_DIEMAN,5,5
2@gl_k,114,210,3	duplicate(Corpo Decomposto#1)	Corpo Decomposto#26	4_M_DIEMAN,5,5
2@gl_k,137,182,6	duplicate(Corpo Decomposto#1)	Corpo Decomposto#27	4_M_DIEMAN,5,5
2@gl_k,138,246,0	duplicate(Corpo Decomposto#1)	Corpo Decomposto#28	4_M_DIEMAN,5,5
2@gl_k,132,260,7	duplicate(Corpo Decomposto#1)	Corpo Decomposto#29	4_M_DIEMAN,5,5
2@gl_k,128,251,1	duplicate(Corpo Decomposto#1)	Corpo Decomposto#30	4_M_DIEMAN,5,5
2@gl_k,179,260,3	duplicate(Corpo Decomposto#1)	Corpo Decomposto#31	4_M_DIEMAN,5,5
2@gl_k,170,261,4	duplicate(Corpo Decomposto#1)	Corpo Decomposto#32	4_M_DIEMAN,5,5
2@gl_k,177,219,2	duplicate(Corpo Decomposto#1)	Corpo Decomposto#33	4_M_DIEMAN,5,5
2@gl_k,190,214,5	duplicate(Corpo Decomposto#1)	Corpo Decomposto#34	4_M_DIEMAN,5,5
2@gl_k,201,214,4	duplicate(Corpo Decomposto#1)	Corpo Decomposto#35	4_M_DIEMAN,5,5

// Amdarais Spawn
2@gl_k,153,250,8	script	Heinrich#23	4_M_HEINRICH,{
	cutin "gl_heinrich1",2;
	mes "[Heinrich]";
	mes "O que você está colocando em Himelmez!";
	close3;
}

2@gl_k,162,250,1	script	Varmundt#23	4_M_BARMUND,{
	mes "[Varmundt]";
	mes "Não pode tirar esse freio...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,158,255,1	script	Gerhalt#23	4_LEVITATEMAN,{
	mes "[Gerhalt]";
	mes "Uuuuu... Khhhah! Apenas fuja com o comandante.";
	close;
}

2@gl_k,158,252,1	script	Himelmez#22	4_F_HIMEL,3,3,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "Isso é incrível~ você chegou até aqui. Posso dizer obrigado?";
	close3;

OnTouch:
	end;
OnTouch_:
	disablenpc instance_npcname("Himelmez#22");
	enablenpc instance_npcname("Himelmez#23");
	initnpctimer;
	'npc_himelmez$=instance_npcname("Himelmez#23");
	'npc_gerhalt$=instance_npcname("Gerhalt#23");
	'npc_heinrich$=instance_npcname("Heinrich#23");
	'npc_varmundt$=instance_npcname("Varmundt#23");
	end;
OnTimer3000:
	npctalk "Himelmez: Incrível~ Eu pensei que você não estava nem perto de chegar aqui...",'npc_himelmez$;
	end;
OnTimer6000:
	npctalk "Gerhalt: Kkkkah! Fuja! Eu não aguento mais!",'npc_gerhalt$;
	end;
OnTimer9000:
	npctalk "Heinrich: Gerhalt!",'npc_heinrich$;
	end;
OnTimer15000:
	npctalk "Heinrich: O que você está fazendo com o meu soldado Himelmez!",'npc_heinrich$;
	end;
OnTimer18000:
	npctalk "Himelmez: Hoo hoo, eu já encontrei um pedaço do coração de Ymir, Heinrich.",'npc_himelmez$;
	end;
OnTimer21000:
	npctalk "Himelmez: Se ele não me incomodasse, teria sido mais rápido.",'npc_himelmez$;
	end;
OnTimer24000:
	npctalk "Gerhalt: Commandant... Venha, você precisa fugir daqui... Ugh.",'npc_gerhalt$;
	end;
OnTimer27000:
	npctalk "Himelmez: Realmente? O que faz você pensar assim?",'npc_himelmez$;
	end;
OnTimer30000:
	npctalk "Heinrich: Himelmez! Você já fez o que queria, então não há mais necessidade de um sacrifício!",'npc_heinrich$;
	end;
OnTimer33000:
	npctalk "Heinrich: Deixe-o ir! Eu não preciso ver mais ninguém sofrer!",'npc_heinrich$;
	end;
OnTimer36000:
	npctalk "Himelmez: Deixe ele ir? Eu acho que... Não...",'npc_himelmez$;
	end;
OnTimer39000:
	npctalk "Himelmez: Além disso, este é seu último soldado sobrevivente, Heinrich.",'npc_himelmez$;
	end;
OnTimer42000:
	npctalk "Himelmez: Ele tornou a vida difícil para mim.",'npc_himelmez$;
	end;
OnTimer45000:
	npctalk "Himelmez: Ele está perfeitamente apto para minha nova criação Amdarais.",'npc_himelmez$;
	end;
OnTimer48000:
	npctalk "Heinrich: Eu nunca vou te perdoar",'npc_heinrich$;
	end;
OnTimer51000:
	npctalk "Himelmez: Uh uh~ Estou com tanto medo.",'npc_himelmez$;
	end;
OnTimer54000:
	npctalk "Himelmez: Enfim, teremos a chance de ver porque precisamos conversar mais.",'npc_himelmez$;
	end;
OnTimer57000:
	npctalk "Himelmez: meninos tão longos.",'npc_himelmez$;
	end;
OnTimer60000:
	specialeffect EF_BARRIER,AREA, 'npc_gerhalt$;
	end;
OnTimer63000:
	disablenpc 'npc_himelmez$;
	end;
OnTimer65000:
	npctalk "Gerhalt: Você pode fazer meu corpo mas você não pode levar minha alma, Himelmez!",'npc_gerhalt$;
	end;
OnTimer66000:
	specialeffect EF_CHAINCOMBO,AREA, 'npc_gerhalt$;
	end;
OnTimer67000:
	npctalk "Heinrich: Eu nunca vou deixar você escapar Himelmez!!",'npc_heinrich$;
	specialeffect EF_MAPPILLAR,AREA, 'npc_gerhalt$;
	end;
OnTimer70000:
	specialeffect EF_MAPPILLAR2,AREA, 'npc_gerhalt$;
	specialeffect EF_MAPPILLAR,AREA, 'npc_gerhalt$;
	disablenpc 'npc_heinrich$;
	npctalk "Varmundt: Não temos escolha. Temos que lutar contra os Amdarais!",'npc_varmundt$;
	end;
OnTimer73000:
	mapannounce 'map_name$[1], "O corpo de Gerhalt está mudando.",bc_map,0xFFFFFF;
	end;
OnTimer76000:
	specialeffect EF_LORD,AREA, 'npc_gerhalt$;
	disablenpc 'npc_varmundt$;
	mapannounce 'map_name$[1], "Varmundt: Deixe-me ajudar nessa batalha com minha ilusão. Basta seguir as ordens da ilusão.",bc_map,0xFFFF00;
	end;
OnTimer80000:
	disablenpc 'npc_gerhalt$;
	donpcevent instance_npcname("#ghmemorialmob07")+"::OnStart";
	stopnpctimer;
	'npc_himelmez$='npc_gerhalt$='npc_heinrich$='npc_varmundt$="";
	end;
}

2@gl_k,158,252,4	script	Himelmez#23	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "Isso é incrível~ você chegou até aqui. Posso dizer obrigado?";
	close3;
}

2@gl_k,1,1,0	script	#ghmemorialmob07	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob07");
	monster 'map_name$[1],158,255,"Amdarais",2476,1,instance_npcname("#ghmemorialmob07")+"::OnMyMobDead";// MG_AMDARAIS
	'boss_id=$@mobid[0];
	unittalk 'boss_id, "Fuja... fuja de mim...";
	initnpctimer;
	end;
OnTimer5000:
	unittalk 'boss_id, "Eu não quero... eu não quero matar ninguém. Uhuuuuuh";
	end;
OnTimer10000:
	unittalk 'boss_id, "Por favor, me mate! Por favor!";
	end;
OnTimer16000:
	unittalk 'boss_id, "Eeeeee... eee... morra... morra...";
	end;
OnTimer22000:
	unittalk 'boss_id, "Demolition... Death!...";
	end;
OnTimer55000:
	donpcevent instance_npcname("#ghmemorialmob08")+"::OnStart";// Buffs de Varmundt e monstros adicionais
	stopnpctimer;
	end;

OnMyMobDead:
	if(mobcount('map_name$[1], instance_npcname("#ghmemorialmob07")+"::OnMyMobDead")<1) {
		stopnpctimer;
		enablenpc instance_npcname("Hugin#21");
		enablenpc instance_npcname("#Secret Room Exit");

		stopnpctimer;
		disablenpc instance_npcname("#ghmemorialmob07");
		// note: monstros do#ghmemorialmob08 ainda vivos nos mortos de Amdarais
	}
	end;
}

// Buffs de Varmundt
2@gl_k,1,1,0	script	#ghmemorialmob08	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob08");
	initnpctimer;
	end;
OnTimer5000:
	if(unitexists('boss_id)==0) {
		disablenpc instance_npcname("#ghmemorialmob08");
		stopnpctimer;
		end;
	}
	getunitdata 'boss_id, .@data;
	.@percent_hp=(.@data[UMOB_HP]*100) / .@data[UMOB_MAXHP];
	mapannounce 'map_name$[1], "Amdarais HP "+.@percent_hp+"% alcance!",bc_map,0x70DBDB;

	// tipo de evento a cada 10%
	switch(.@percent_hp / 10) {
	case 10:
		break;
	case 9:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff2")+"::OnEvent";
		break;
	case 8:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff3")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Suffered Khalitzburg",2471,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	case 7:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff3")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Cavaleiro do Abismo",2470,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	case 6:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff3")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Cavaleiro do Abismo",2472,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	case 5:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff2")+"::OnEvent";
		break;
	case 4:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff3")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Arqueiro da Morte",2469,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	case 3:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff1")+"::OnEvent";
		donpcevent instance_npcname("Fantasma de Varmundt#Buff3")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Cavaleiro do Abismo",2472,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	case 2:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff1")+"::OnEvent";
		donpcevent instance_npcname("Fantasma de Varmundt#Buff3")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Cavaleiro do Abismo",2470,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	case 1:
	case 0:
		donpcevent instance_npcname("Fantasma de Varmundt#Buff1")+"::OnEvent";
		donpcevent instance_npcname("Fantasma de Varmundt#Buff4")+"::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Arqueiro da Morte",2469,6, instance_npcname("#ghmemorialmob08")+"::OnMobDead";
		break;
	}
	end;
OnTimer30000:
	killmonster 'map_name$[1], instance_npcname("#ghmemorialmob08")+"::OnMobDead";
	initnpctimer;
	end;
OnMobDead:
	end;
}

2@gl_k,150,247,5	script	Fantasma de Varmundt#Buff1	4_M_BARMUND,2,2,{
	end;
OnTouch:
	specialeffect2 EF_ENHANCE;
	// buff
	// .@num=atoi(replacestr(strnpcinfo(2), "Buff",""));
	// if(.@num==1)
	// else if(.@num==2)
	// else if(.@num==3)
	// else
	end;
OnEvent:
	initnpctimer;
	enablenpc instance_npcname(strnpcinfo(0));
	.@num=atoi(replacestr(strnpcinfo(2), "Buff",""));
	if(.@num==1)
		npctalk "O Fantasma de Varmundt: Amdarais pode usar um forte escudo mágico! Fique perto de mim e eu vou te proteger!";
	else if(.@num==2)
		npctalk "O Fantasma de Varmundt: Agora é uma chance de atacar! Aproxime-se de mim e bata!";
	else if(.@num==3)
		npctalk "O Fantasma de Varmundt: Se você não quer ser atacado pelo zumbi de Amdarais, venha a mim e consiga mais poder!";
	else
		npctalk "O Fantasma de Varmundt: Se você não quer ser atacado pelo poder de Amdarais, venha a mim e consiga mais poder!";
	end;
OnTimer10000:
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
2@gl_k,165,247,3	duplicate(Fantasma de Varmundt#Buff1)	Fantasma de Varmundt#Buff2	4_M_BARMUND,2,2
2@gl_k,150,232,8	duplicate(Fantasma de Varmundt#Buff1)	Fantasma de Varmundt#Buff3	4_M_BARMUND,2,2
2@gl_k,165,232,2	duplicate(Fantasma de Varmundt#Buff1)	Fantasma de Varmundt#Buff4	4_M_BARMUND,2,2


2@gl_k,158,241,1	script	Hugin#21	4_M_SAGE_C,{
	if(checkquest(12319,HUNTING)==2) {// nota: caça completa 'Pré-requisitos de Soul Corrupted'
		mes "[Hugin]";
		mes "Hm, você está muito bem. Primeiro de tudo, deixe-me dar alguns tesouros de Amdarais.";
		erasequest 12319;
		setquest 12322;// Distorção do espaço
		if(isbegin_quest(12321)==0) {
			setquest 12321;// Time Conquistador
			completequest 12321;
			getitem 6607,5;// Temporal_Crystal
			getitem 6608,5;// Coagulated_Spell
			getexp 350000,350000;
		}
		else {
			getitem 6607,1;// Temporal_Crystal
			getitem 6608,1;// Coagulated_Spell
		}
		next;
		mes "[Hugin]";
		mes "O tempo de Varmundt é interrompido por mim.";
		mes "E seu tempo será distorcido em breve.";
		specialeffect2 EF_BLIND;
		soundeffect "_blind.wav",0;
		next;
		mes "[Hugin]";
		mes "Talvez esse pobre viajante do tempo tente impedir que a tragédia de Glast Heim aconteça.";
		next;
		mes "[Hugin]";
		mes "No entanto, nunca vamos esquecer seu comportamento agora e para sempre...";
		next;
		mes "[Hugin]";
		mes "Agora deixe-me remover sua memória. Se você me ver novamente, será alguém novo.";
		specialeffect2 EF_FREEZE;
		close2;
		warp 'map_name$[1],158,244;
		end;
	}
	mes "[Hugin]";
	mes "Opa. Eu quase distorci o intervalo de tempo. Venha até aqui. Precisamos sair!";
	next;
	select("Você estava apenas comigo...");
	mes "[Hugin]";
	mes "O que você disse antes?";
	mes "De qualquer forma, isso não é importante. O intervalo de tempo será fechado, então precisamos sair daqui.";
	next;
	if(select("Deixe-me ver mais:Por favor, deixe-me sair")==1) {
		mes "[Hugin]";
		mes "Sério? Esse lugar será quebrado em breve. Por favor, olhe rapidamente.";
		close;
	}
	close2;
	warp "glast_01",204,270;
	end;
}

// Treasure Room
//============================================================
1@gl_k,165,136,3	script	Rachado Estranho#2	CLEAR_NPC,{
	if(isbegin_quest(12322)==0) {// Distorção do espaço
		mes "O crack parece suspeito, mas nada mais para checar.";
		close;
	}
	specialeffect EF_SPELLBREAKER;

	.@random=rand(1,4);
	switch(atoi(strnpcinfo(2))) {
	case 2:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 719,1,"this",165,138;
		makeitem 6608,1,"this",165,138;
		break;
	case 3:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 720,1,"this",159,138;
		if(rand(1,4)==4)
			makeitem2 15066,1,"this",159,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",159,138;
		makeitem 7229,1,"this",159,138;
		break;
	case 4:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 721,1,"this",153,138;
		if(rand(1,4)==4)
			makeitem2 13086,1,"this",153,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",153,138;
		makeitem 7230,1,"this",153,138;
		break;
	case 5:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 722,1,"this",147,138;
		if(rand(1,4)==4)
			makeitem2 2949,1,"this",147,138,0,0,0,0,0,0,0;
		makeitem 6612,1,"this",147,138;
		makeitem 6613,1,"this",147,138;
		makeitem 6608,1,"this",147,138;
		break;
	case 6:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 725,1,"this",141,138;
		makeitem 7228,1,"this",141,138;
		if(rand(1,4)==4)
			makeitem2 13440,1,"this",141,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",141,138;
		break;
	case 7:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 726,1,"this",135,138;
		if(rand(1,4)==4)
			makeitem2 2022,1,"this",135,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",135,138;
		break;
	case 8:
		for(.@i=1; .@i<=.@random;++.@i)
			makeitem 727,1,"this",129,138;
		if(rand(1,4)==4)
			makeitem2 21007,1,"this",129,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",129,138;
		break;
	}
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
1@gl_k,159,136,3	duplicate(Rachado Estranho#2)	Rachado Estranho#3	CLEAR_NPC
1@gl_k,153,136,3	duplicate(Rachado Estranho#2)	Rachado Estranho#4	CLEAR_NPC
1@gl_k,147,136,3	duplicate(Rachado Estranho#2)	Rachado Estranho#5	CLEAR_NPC
1@gl_k,141,136,3	duplicate(Rachado Estranho#2)	Rachado Estranho#6	CLEAR_NPC
1@gl_k,135,136,3	duplicate(Rachado Estranho#2)	Rachado Estranho#7	CLEAR_NPC
1@gl_k,129,136,3	duplicate(Rachado Estranho#2)	Rachado Estranho#8	CLEAR_NPC

1@gl_k,269,267,3	script	Rachado Estranho#1	CLEAR_NPC,{
	if(isbegin_quest(12322)==0) {// Distorção do espaço
		mes "O crack parece suspeito, mas nada mais para checar.";
		close;
	}
	warp 'map_name$[0],149,198;
	end;

OnInstanceInit:
	'map_name$[0] = instance_mapname("1@gl_k");
	'map_name$[1] = instance_mapname("2@gl_k");

	// entrada
	disablenpc instance_npcname("Himelmez#1");
	disablenpc instance_npcname("Varmundt#1");
	disablenpc instance_npcname("Heinrich#1");
	disablenpc instance_npcname("Heinrich#2");
	disablenpc instance_npcname("Heinrich#3");

	// Resgatar 1
	disablenpc instance_npcname("#ghmemorialmob01");
	disablenpc instance_npcname("Ministrante Domun#1");

	// Resgatar 2
	disablenpc instance_npcname("#ghmemorialmob02");
	disablenpc instance_npcname("Holgren, o Destruidor#1");
	for(.@i=1; .@i<=26; .@i++)
		disablenpc instance_npcname("Um Homem Morto#"+.@i);

	// Setor 3
	disablenpc instance_npcname("#ghmemorialmob03");

	// Raiz da Corrupção
	disablenpc instance_npcname("Himelmez#2");
	disablenpc instance_npcname("Varmundt#2");
	disablenpc instance_npcname("Heinrich#4");
	disablenpc instance_npcname("#Mimelon");
	disablenpc instance_npcname("#ghmemorialmob04");

	// Entrance 2nd map
	disablenpc instance_npcname("Varmundt#21");
	disablenpc instance_npcname("Heinrich#21");
	disablenpc instance_npcname("#Servanton");
	hideonnpc instance_npcname("#Servanton_effect");

	// Comandantes
	disablenpc instance_npcname("#ghmemorialmob05");
	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ ) {
		disablenpc instance_npcname("#ogh_2-" + .@i);
		disablenpc instance_npcname("#ogh_3-" + .@i);
	}

	// Entrada do quarto Amdarais
	disablenpc instance_npcname("#Geron");
	for(.@i=1; .@i<=35; .@i++)
		disablenpc instance_npcname("Corpo Decomposto#" + .@i);

	// Amdarais Spawn
	disablenpc instance_npcname("Gerhalt#23");
	disablenpc instance_npcname("Himelmez#22");
	disablenpc instance_npcname("Himelmez#23");
	disablenpc instance_npcname("Varmundt#23");
	disablenpc instance_npcname("Heinrich#23");
	disablenpc instance_npcname("#ghmemorialmob07");

	// Buffs de Varmundt
	disablenpc instance_npcname("#ghmemorialmob08");
	disablenpc instance_npcname("Fantasma de Varmundt#Buff1");
	disablenpc instance_npcname("Fantasma de Varmundt#Buff2");
	disablenpc instance_npcname("Fantasma de Varmundt#Buff3");
	disablenpc instance_npcname("Fantasma de Varmundt#Buff4");
	disablenpc instance_npcname("Hugin#21");

	// Warps
	disablenpc instance_npcname("#2Control");
	disablenpc instance_npcname("#2Control2");
	disablenpc instance_npcname("#3Control");
	disablenpc instance_npcname("#3Control2");
	disablenpc instance_npcname("#4Control");
	disablenpc instance_npcname("#4Control2");
	disablenpc instance_npcname("#2F Entrance");
	disablenpc instance_npcname("#1 Control");
	disablenpc instance_npcname("#22 Control");
	disablenpc instance_npcname("#22 Control2");
	disablenpc instance_npcname("#23 Control");
	disablenpc instance_npcname("#23 Control2");
	disablenpc instance_npcname("#24 Control");
	disablenpc instance_npcname("#24 Control2");
	disablenpc instance_npcname("#Secret Room Exit");
	end;
}
