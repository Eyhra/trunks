prt_fild05,141,236,3	script	Guarda da Aldeia::alapvvg	4_ANGELING,{
OnStart:
	set .np$,"^00B2EE[ Guarda da Aldeia ]^000000";
	set .@party_id, getcharid(1);
	set .@porv_leader, ( getcharid(0) == getpartyleader(.@party_id,2) );

	//RATHENA
	set .@has_instance, PV_INSTANCE_ID > 0;
	//HERCULES
	//set .@has_instance, has_instance(.map$[0], instance_id());	

	set .@instance_delay, ( instance_delay_porvil - gettimetick(2) );

	mes .np$;
	mes "O que você está fazendo aqui?";
	mes "Esta é a entrada da Vila dos Porings. Quem é Você?";
	mes "Bom, de qualquer forma. Não posso deixar você entrar. O Rei ficou louco...";
	next;		
	switch( select( 
		(( .@party_id && .@porv_leader && !.@has_instance )?"^3CB371[»]^000000 Criar " + .instance_name$:""),
		(( .@party_id && .@has_instance )?"^DAA520[»]^000000 Entrar " + .instance_name$:""),
		(( .@party_id && .@porv_leader && .@has_instance )?"^ff0000[»]^000000 Destruir " + .instance_name$:""),
	"^ff0000[»]^000000 Sair"
		)){
	case 1:
		if( .@instance_delay > 0 )
		{
			mes "Você tem que esperar : ^FF0000"+.@instance_delay+" Segundos.^000000";
			//close;
		}
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			set .@origin, getcharid(3);
			set .@gettimetick, gettimetick(2);
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set .@member_count, .@member_count + 1;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) set .@fail, .@fail|1;
					if( Zeny < .instance_zeny ) set .@fail, .@fail|2;
					if( instance_delay_porvil > .@gettimetick ) set .@fail, .@fail|4;
					if( .@fail ){
						set .@name$, strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		
		//clean any previously remaining instance
		if(PV_INSTANCE_ID > 0)
		{
			getpartymember getcharid(1),2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) && PV_INSTANCE_ID > 0){
					set PV_INSTANCE_ID, -1;
				}
			instance_destroy instance_id();
		}
		
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Falhou ]^000000";
			mes "^0055FF"+.@name$+"^000000, Você não pode participar agora.";
			mes " ";
			mes "^FF0000[ Razão: ]^000000";
			if( .@fail & 1 ) mes "^777777Você deve ser Nível: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777Você precisa "+ .instance_zeny +" Zeny^000000";
			if( .@fail & 4 ) mes "^777777Você ainda tem cooldown.^000000";
			break;
		}
		
		getpartymember getcharid(1),0;
		if (instance_check_party(.@party_id, .min_party_member, .base_level[0], .base_level[1]) == 0) {
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Faça ou participe de um grupo com pelo menos um membro "+.min_party_member+" e tente novamente, com nível entre " + .base_level[0] + " e " + .base_level[1] + ".";
		}
		else{			
			//RATHENA			
			set .@instance, instance_create( .instance_name$ );
			//HERCULES
			//set .@instancee, instance_create( .instance_name$, .@party_id, IOT_PARTY ); 
			if( .@instance < 0 ) {
				npctalk .instance_name$ + " Falha na reserva.";
				mes "^0000FF"+ .instance_name$ +" ^000000 - Falha na reserva!";
				close;
			}
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "^0000FF"+.instance_name$+" ^000000 - Reservada";
			set .@gettimetick, gettimetick(2);
			getpartymember .@party_id,2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set instance_delay_porvil, ( .@gettimetick + ( .instance_delay ));
					set Zeny, Zeny - .instance_zeny;
					set PV_INSTANCE_ID, .@instance;
					dispbottom "[ "+.instance_name$+" ] : -"+ .instance_zeny +" Zeny e cooldown da Instância Aplicado.";
				}
			attachrid( .@origin );
			npctalk .instance_name$ + " foi gerada para o grupo - " + getpartyname( .@party_id );

			//HERCULES
			//for( set .@i, 0; .@i < getarraysize(.map$); set .@i,.@i+1 )
			//{
			//	if( instance_attachmap( .map$[.@i],.@instance ) == "" ){
			//		setmapflag .map$[.@i],mf_zone,"Caverna Memorial";
			//		npctalk .instance_name$ + " reserva falhou devido a falha de anexação do mapa.";
			//		mes "- ^FF0000Falha na reserva!^000000";
			//		instance_destroy( instance_id() );
			//		close;
			//	}
			//}
			//instance_set_timeout .instance_delay,300,.@instance;
			//instance_init(.@instance);

 		}
	case 2:
		if( PV_INSTANCE_ID <= 0 ){
			mes .instance_name$ + " não existe para o grupo - "+getpartyname( .@party_id );
		}
		else{
			//RATHENA
			switch(instance_enter(.instance_name$)) {
				default:
					mes "Um erro desconhecido ocorreu.";
					close;
				case 2:
					mes "A masmorra memorial " + .instance_name$ + " não existe.";
					mes "O líder do grupo ainda não gerou a masmorra.";
					close;
				case 1:
					mes "Você pode entrar na masmorra depois de fazer a festa.";
					close;
				case 0:
					mapannounce .map$[0], strcharinfo(0)+" do grupo, " +getpartyname( .@party_id )+", está entrando na instância, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
					end;
			}

			//HERCULES
			//if( has_instance( .map$[0] ) == "" ){
			//	mes .instance_name$ + " não existe para o grupo - "+getpartyname( .@party_id );
			//}
			//else{
			//	warp .map$[0],102,24;
			//	instance_announce instance_id(), getpartyname(getcharid(1))+"  entrou na "+.instance_name$+"!",bc_map,"0x00ff99";
			//}
		}
		break;

	case 3:
		mes .instance_name$ + " Destruído.";			
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && PV_INSTANCE_ID > 0){
				set PV_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
		break;
	default: 
		break;
}
close;

OnPCLoginEvent:
OnPCLogoutEvent:
	set PV_INSTANCE_ID, -1;
	end;

OnInit:
	setarray .base_level[0], 30, 60;
	setarray .map$[0],"1@begi";
	set .map_size, getarraysize( .map$ );
	set .min_party_member, 1;
	set .instance_zeny, 0;
	set .instance_delay, 7200;
	set $porv_timer_delay, 3600; //1 hour to clean the instance
	set .instance_name$, "Vila dos Porings";
	end;
}

1@begi,101,107,4	script	poring_village	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
end;

OnInstanceInit:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	
	//apply mapflags
	
	set 'instance_porv_level, 0;
	
	set .timerCount, 0;
	initnpctimer;

	summon_normal (.@map$, .@npc_name$, 1);
	end;

function	summon_normal	{
	set .@map$, getarg(0);
	set .np$, getarg(1);
	set .@increaseFloor, getarg(2);
	
	if(.@increaseFloor == 1)
	{
		'instance_porv_level++;
	}
	
	switch( 'instance_porv_level ){
		case 1:
			set .@clabel$, .np$ + "::OnNormalDied";
			//poring, drops, poporing
			areamonster .@map$,149,39,185,35,"--ja--",1002,10,.@clabel$;
			areamonster .@map$,149,39,185,35,"--ja--",1113,10,.@clabel$;
			areamonster .@map$,187,57,87,68,"--ja--",1002,15,.@clabel$;
			areamonster .@map$,187,57,87,68,"--ja--",1031,10,.@clabel$;
			areamonster .@map$,75,50,19,93,"--ja--",1002,15,.@clabel$;
			areamonster .@map$,75,50,19,93,"--ja--",1113,15,.@clabel$;
			areamonster .@map$,75,50,19,93,"--ja--",1031,10,.@clabel$;
			break;
		case 2:
			set .@clabel$, .np$ + "::OnNormalDied";
			//drops, marin, poporing
			areamonster .@map$,148,106,219,109,"--ja--",1113,10,.@clabel$;
			areamonster .@map$,148,106,219,109,"--ja--",1242,10,.@clabel$;
			areamonster .@map$,219,109,194,146,"--ja--",1242,15,.@clabel$;
			areamonster .@map$,219,109,194,146,"--ja--",1031,10,.@clabel$;
			areamonster .@map$,172,155,62,141,"--ja--",1113,15,.@clabel$;
			areamonster .@map$,172,155,62,141,"--ja--",1242,15,.@clabel$;
			areamonster .@map$,172,155,62,141,"--ja--",1031,10,.@clabel$;
			break;
		case 3:
			set .@clabel$, .np$ + "::OnNormalDied";
			//poring, drops, marin, poporing
			areamonster .@map$,46,185,113,203,"--ja--",1002,5,.@clabel$;
			areamonster .@map$,46,185,113,203,"--ja--",1113,7,.@clabel$;
			areamonster .@map$,46,185,113,203,"--ja--",1242,8,.@clabel$;
			areamonster .@map$,46,185,113,203,"--ja--",1031,10,.@clabel$;
			break;
	}
	return;
}

function	summon_guardian	{
	set .@map$, getarg(0);
	set .@label$, getarg(1);
			
	switch( 'instance_porv_level ){
		case 1:
			//Big Poring
			setarray .@monster,1002;
			setarray .@coordinate,100, 117, 135, 94;
			break;
		case 2:
			//Big Mastering
			setarray .@monster,1090;
			setarray .@coordinate,30, 171, 60, 141;
			break;
		case 3:
			//King Poring
			setarray .@monster,2538;
			setarray .@coordinate,157,207,198,183;
			break;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],1,.@label$,2;
	}
	return;
}

OnNormalDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
	if( .@mob_dead_num == 0 ){
		sleep2 500;

		if('instance_porv_level == 3)
		{
			instance_announce instance_id(), "[ Rei Poring ] : O que é isso? Quem é Você?", bc_map, 0xFF0000;
			sleep2 1500;
			instance_announce instance_id(), "[ Rei Poring ] : O que você está fazendo aqui?", bc_map, 0xFF0000;
			sleep2 1500;
			instance_announce instance_id(), "[ Rei Poring ] : Você não tem o direito de estar aqui! Eu vou te esmagar até a morte!", bc_map, 0xFF0000;
			sleep2 1500;
		}
		summon_guardian( .@map$, .@npc_name$ + "::OnGuardianDied" );
	}
	end;
	
OnGuardianDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnGuardianDied" );
	
	if( !.@mob_dead_num ){	
		sleep2 500;

		if('instance_porv_level == 3)
		{
			instance_announce instance_id(), "[ Rei Poring ] : Obrigado aventureiro... não sei o que......?", bc_map, 0xFF0000;
			sleep2 1500;
			instance_announce instance_id(), "[ Rei Poring ] : De qualquer forma, obrigado pela sua ajuda?!", bc_map, 0xFF0000;
			sleep2 1500;
			instance_announce instance_id(), "[ Rei Poring ] : Vocês serão os amigos de Porings até o fim!", bc_map, 0xFF0000;
			enablenpc instance_npcname( "alapovivig", instance_id() );
			sleep2 25000;		
			getpartymember getcharid(1),2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) && PV_INSTANCE_ID > 0){
					set PV_INSTANCE_ID, -1;
				}
			instance_destroy instance_id();
			end;
		}
		
		sleep 500;
		summon_normal( .@map$,.@npc_name$, 1 );
	}
	end;
	
OnTimer10000:
	stopnpctimer;
	//each 10s, increase timer
	set .timerCount, .timerCount + 10;
	if(.timerCount == $porv_timer_delay)
	{
		instance_announce instance_id(), "[ Vila dos Porings ] : O rei perdeu a cabeça! Eu tenho que tirar você daí!", bc_map, 0x00FF00;
		sleep 5000;
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && PV_INSTANCE_ID > 0){
				set PV_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
		stopnpctimer;
	}
	initnpctimer;
	end;
}

1@begi,146,35,5	script	Barricada::alapovibar	4_ROPEPILE,2,2,{
	end;

OnTouch:
	if('porvTalking == 0)
	{
		set 'porvTalking, 1;
		instance_announce instance_id(), "[ Guarda Poring ] : Prepare-se. vou largar essa barricada que montamos.", bc_map, 0x00FF00;
		sleep2 1500;
		instance_announce instance_id(), "[ Guarda Poring ] : Uma vez dentro, você só terá que ir direto para encontrar o Rei.", bc_map, 0x00FF00;
		sleep2 1500;
		specialeffect 90;
		disablenpc instance_npcname( "alapovibar", instance_id() );
		disablenpc instance_npcname( "alapovibar2", instance_id() );
		disablenpc instance_npcname( "alapovibar3", instance_id() );
		disablenpc instance_npcname( "alapovibar4", instance_id() );
		disablenpc instance_npcname( "alapovibar5", instance_id() );
		disablenpc instance_npcname( "alapovibar6", instance_id() );
		setcell instance_mapname( "1@begi", instance_id() ),145,35,145,41,cell_walkable,1;
	}
	end;

OnInstanceInit:
OnEnable:
	setcell instance_mapname( "1@begi", instance_id() ),145,35,145,41,cell_walkable,0;
	set 'porvTalking, 0;
	enablenpc strnpcinfo(3);
	end;
}

1@begi,146,35,5	script	Guarda da Aldeia::alapovivig	4_ANGELING,{
	set .np$,"^00B2EE[ Guarda da Aldeia ]^000000";
	
	mes .np$;
	mes "Obrigado pela ajuda!";
	mes "Vou te mandar de volta.";
	warp "prt_fild05", 144, 238;
	close;

OnInstanceInit:
OnEnable:
	disablenpc strnpcinfo(3);
	end;
}

1@begi,146,36,5	duplicate(alapovibar)	Barricada::alapovibar2	4_ROPEPILE
1@begi,146,37,5	duplicate(alapovibar)	Barricada::alapovibar3	4_ROPEPILE
1@begi,146,38,5	duplicate(alapovibar)	Barricada::alapovibar4	4_ROPEPILE
1@begi,146,39,5	duplicate(alapovibar)	Barricada::alapovibar5	4_ROPEPILE
1@begi,146,40,5	duplicate(alapovibar)	Barricada::alapovibar6	4_ROPEPILE

//===== Alayne Scripts =======================================
//= Poring Village Item quests
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//= The official instance Poring Village stuff transcription
//===== Todo Remaining: ======================================
// 
//===== Additional Comments: =================================
//
//
//============================================================

prt_fild05,154,239,3	script	Coisas de Poring::alaaspr	4_F_02,{
	set .np$,"^00B2EE[ Guarda da Aldeia ]^000000";

	if(.enchantingId != -1)
	{
		mes .np$;
		mes "Espere um pouco, por favor, estou preso com outra pessoa.";
		close;
	}
	disable_items;
	mes .np$;
	if (countitem(.reqId) >= .reqCountEnchant) {
		mes "Tenho estudado formas de aprimorar o Coisas de Poring para maximizar sua capacidade.";
		next;
		mes .np$;
		mes "Encantamento é uma habilidade incrível que infunde poderes de status misteriosos no soquete oculto da armadura.";
		next;
		mes .np$;
		mes "Eu sou capaz de colocar algum tipo de bônus em um pedaço de material representando Porings que você pode usar.";
		next;
		set .@typeChoice, select("^3CB371[»]^000000 Comprar Chapéu","^3CB371[»]^000000 Encantar Chapéu");
		switch(.@typeChoice) {
		case 1:
			goto S_BuyHat;
			break;
		case 2:
			.@menu$ = "";
			for(.@i = 0; .@i<getarraysize(.itemList); ++.@i)
			{
				if(getequipid(EQI_HEAD_TOP) == .itemList[.@i])
				{
					set .@realItems[getarraysize(.@realItems)], .itemList[.@i];
					.@menu$ += getitemname(.itemList[.@i])+":";
				}
			}
			break;
		}
		if(getarraysize(.@realItems) == 0)
		{
			next;
			mes .np$;
			mes "Você tem que usá-lo para me permitir encantá-lo.";
			close;
		}
	
		callsub S_EnchantArmor, .@realItems[select(.@menu$)-1], .@typeChoice;
		end;
	}
	mes "Você sabe que pode encantar o pedaço de coisas de Poring?";
	next;
	mes .np$;
	mes "Se por acaso você quiser encantar um, me traga " + .reqCountEnchant + " " + getitemname(.reqId) + ", e veremos o que podemos fazer.";
	close;
	
S_BuyHat:
	next;
	mes .np$;
	mes "Então você quer comprar um item?";
	mes "Vai te custar " + .reqCountBuy + " " + getitemname(.reqId) + " por um item.";
	for(.@i = 0; .@i<getarraysize(.itemList); ++.@i)
	{
		.@menu$ += getitemname(.itemList[.@i])+":";
	}
	set .@selectItem, select(.@menu$) - 1;
	next;
	mes .np$;
	mes "Então você escolheu um " + getitemname(.itemList[.@selectItem]) + ", hu?";
	if(select("^3CB371[»]^000000 Sim","^ff0000[»]^000000 Não") == 1)
	{
		if(countitem(.reqId) >= .reqCountBuy)
		{
			next;
			mes .np$;
			mes "Tudo bem. Aqui está então.";
			delitem .reqId, .reqCountBuy;
			getitem .itemList[.@selectItem], 1;
			next;
			mes .np$;
			mes "Vejo você em breve.";
		}
		else
		{
			next;
			mes .np$;
			mes "Como eu te disse, eu preciso " + .reqCountBuy + " " + getitemname(.reqId) + ", eu esperarei aqui.";
		}
	}
	else
	{
		next;
		mes .np$;
		mes "Mesmo?";
		mes "Vamos fazer de novo então.";
		goto S_BuyEarring;
	}
	close;
	
S_EnchantArmor:
	.@itemid = getarg(0);
	.@typeChoice = getarg(1);
	next;
	mes .np$;
	mes "Primeiro e mais importante.";
	mes "^ff5555Nível de Refinamento Existente da Armadura";
	mes "e as cartas sumirão.^000000";
	mes "Você ainda quer experimentar um encantamento?";
	next;
	if(select("^ff0000[»]^000000 Hmm... Deixe-me pensar sobre isso", "^3CB371[»]^000000 Vá em frente") == 1) {
		mes .np$;
		mes "Bem, não posso culpá-lo. Segurança primeiro, eh?";
		mes "Agora tenha bom dia.";
		close;
	}

	if(countitem(.@itemId) > 1)
	{
		next;
		mes .np$;
		mes "Desculpe, mas você precisa ter apenas um item em sua janela de equipamento.";
		mes "Caso contrário, o encantamento pode misturar as coisas...";
		close;
	}

	mes .np$;
	mes "Muito aventureiro né? Bem, vamos?";
	mes "Agora você deve escolher o tipo de encantamento que deseja.";
	set .@enchantChoice, select("^3CB371[»]^000000 HP/SP e Atributos");

	if (countitem(.reqId) < .reqCountEnchant) {
		mes .np$;
		mes "Desculpe, mas você não tem zeny suficiente.";
		close;
	}
	
	//find good slot
	set .@slot, -1;
	if(getequipid(EQI_HEAD_TOP) == .@itemid)
	{
		set .@cSlot, getequipcardid(EQI_HEAD_TOP, 2);
		if(.@cSlot != 0)
		{
			set .@slot3Id, .@cSlot;
			set .@cSlot, getequipcardid(EQI_HEAD_TOP, 3);
			if(.@cSlot == 0)
			{
				set .@slot, 3;
			}
		}
		else
		{
			set .@slot, 2;
		}
	}

	if(.@slot == -1)
	{
		next;
		mes .np$;
		mes "Seu item já está encantado duas vezes... Não posso trabalhar com isso.";
		close;
	}
	
	//enchant
	set .enchantingId, getcharid(3);
	progressbar "ffff00",7;
	delitem .reqId, .reqCountEnchant;
	delitem .@itemid,1;
	
	switch(.@enchantChoice)
	{
		case 1:
			if(.@slot == 2)
			{
				switch(rand(1, 7))
				{
					case 1:
						set .@addpart, 4700;
						break;
					case 2:
						set .@addpart, 4710;
						break;
					case 3:
						set .@addpart, 4720;
						break;
					case 4:
						set .@addpart, 4730;
						break;
					case 5:
						set .@addpart, 4740;
						break;
					case 6:
						set .@addpart, 4750;
						break;
					case 7:
						specialeffect 155;
						next;
						mes .np$;
						mes "Bem, isso é muito ruim.";
						mes "O equipamento solicitado falhou em encantar, desculpe...";
						set .enchantingId, -1;
						close;
				}
			}
			else
			{
				switch(rand(1, 10))
				{
					case 1:
						set .@addpart, 4795;
						break;
					case 2:
						set .@addpart, 4796;
						break;
					case 3:
						set .@addpart, 4928;
						break;
					case 4:
						set .@addpart, 4870;
						break;
					case 5:
						set .@addpart, 4800;
						break;
					case 6:
						set .@addpart, 4861;
						break;
					case 7:
						set .@addpart, 4862;
						break;
					default:
						specialeffect 155;
						next;
						mes .np$;
						mes "Bem, isso é muito ruim.";
						mes "O equipamento solicitado falhou em encantar, desculpe...";
						//break item
						set .enchantingId, -1;
						close;
				}
			}
			break;
	}
	
	if(.@slot == 2)
	{
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, .@addpart, 0;
	}
	else if(.@slot == 3)
	{
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, .@slot3Id, .@addpart;
	}
	specialeffect 154;
	set .enchantingId, -1;
	close;
	
OnInit:
	set .enchantingId, -1;
	set .reqId, 7539;
	set .reqCountEnchant, 4;
	set .reqCountBuy, 1;
	setarray .itemList[0],5035,5255,18748,18785;
	end;
}