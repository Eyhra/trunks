//===== rAthena Script =======================================
//= Eden Group Quests - Mission [26 - 40]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 26 - 40.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.4 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,38,38,3	script	Placa de Missão[26-40]	857,{
	set .np$,"^00B2EE[ Placa de Missão ]^000000";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Você não é um membro qualificado. Você não pode usar o quadro de avisos.";
		close;
	}
	mes .np$;
	mes "- Muitas missões são -";
	mes "- no quadro de avisos. -";
	next;
	if ((BaseLevel < 26) || (BaseLevel > 40)) {
		mes "Seu nível não é qualificado. Você não pode usar o quadro de avisos.";
		close;
	}
	switch(select("^3CB371[»]^000000 Missão de caça:^3CB371[»]^000000 Missão de entrega:^3CB371[»]^000000 Missão de coleta:^3CB371[»]^000000 Missão de escolta")) {
	case 1:
		mes .np$;
		mes "- Detalhes da missão de caça -";
		mes "- estão no tabuleiro. -";
		next;
		switch(select("^3CB371[»]^000000 Caçando Esporos Venenoso:^3CB371[»]^000000 Caçando Fumaça:^3CB371[»]^000000 Caçando Salgueiro Ancião (Acima Lv.30):^3CB371[»]^000000 Caçando Coco (Acima Lv.35):^3CB371[»]^000000 Caçando o Sapo Thara (Acima Lv.38)")) {
			case 1: callsub L_Quest,8266,0,"Caçando Esporos Venenoso","Sponiac","Geffen","Caçar 10 Esporos de Veneno";
			case 2: callsub L_Quest,8267,0,"Caçando Fumaça","Sponiac","Geffen","Caçar 10 Smokees";
			case 3: callsub L_Quest,8268,30,"Caçando Salgueiro Ancião","Sponiac","Geffen","Caçar 10 Salgueiros Anciões";
			case 4: callsub L_Quest,8269,35,"Caçando Coco","Sponiac","Geffen","Caçar 10 Cocos";
			case 5: callsub L_Quest,8270,38,"Caçando o Sapo Thara","Sponiac","Geffen","Caçar 10 Sapos Thara";
		}
	case 2:
		mes .np$;
		mes "- Delivery Mission details -";
		mes "- are on the board. -";
		next;
		switch(select("^3CB371[»]^000000 Entregue um inseticida:^3CB371[»]^000000 Entregar mel:^3CB371[»]^000000 Entregar Cobertor (Acima Lv.35)")) {
			case 1: callsub L_Quest,8271,0,"Entregue um inseticida","Dieshin Man","Morocc Ruínas","Pegue um 'Insecticide' do Dieshin Man e entregue-o ao sudeste de Payon";
			case 2: callsub L_Quest,8272,0,"Entregar mel","Dieshin Man","Morocc Ruínas","Pegue 'Honey' de Dieshin Man e entregue-o a oeste da Abadia de St. Capitolina";
			case 3: callsub L_Quest,8273,35,"Entregar Cobertor","Dieshin Man","Morocc Ruínas","Pegue cobertores de Dieshin Man e entregue no campo ao sul de Lutie";
		}
	case 3:
		mes .np$;
		mes "- Reunindo detalhes da missão -";
		mes "- estão no tabuleiro. -";
		next;
		switch(select("^3CB371[»]^000000 Junte Ossos:^3CB371[»]^000000 Colete Peles de Animais:^3CB371[»]^000000 Colete Peelings (Acima do Lv.30):^3CB371[»]^000000 Junte Caudas (Acima do Nv.32):^3CB371[»]^000000 Recolher Cookies (Acima do Lv.35):^3CB371[»]^000000 Junte Bigodes (Acima de Lv.35)")) {
			case 1: callsub L_Quest,8274,0,"Junte Ossos","Dashia","Payon","Junte '10 Ossos de esqueleto' e dá-los a Dashia";
			case 2: callsub L_Quest,8275,0,"Colete Peles de Animais","Dashia","Payon","Junte '10 Pele de urso' e '5 Peles de animais' e dá-los a Dashia";
			case 3: callsub L_Quest,8276,30,"Colete Peelings","Dashia","Payon","Junte '20 Peelings de Minhoca' e '20 Garlets' e dá-los a Dashia";
			case 4: callsub L_Quest,8277,32,"Junte Caudas","Dashia","Payon","Junte '10 Yoyo Tails', '10 bolotas' e '10 folhas de guaxinim' e dá-los a Dashia";
			case 5: callsub L_Quest,8278,35,"Reúna cookies","Dashia","Payon","Junte '5 Biscoitos bem assados' e '1 pedaço de bolo' e dá-los a Dashia";
			case 6: callsub L_Quest,8279,35,"Junte bigodes","Dashia","Payon","Junte '5 Bigodes de Toupeira' e '3 Garras de Toupeira' e dá-los a Dashia";
		}
	case 4:
		mes .np$;
		mes "Atualmente não há Missões de Escolta registradas no quadro.";
		close;
	}
	end;
L_Quest:
	//QuestID,ReqLvl,Quest,Client,Loc,Desc
	.@string$ = getarg(2);
	if (BaseLevel < getarg(1,26)) {
		mes "Seu nível não atende aos requisitos. Você não pode fazer a missão '"+ .@string$ +"'.";
		close;
	}
	mes "["+ .@string$ +" !!]";
	mes "Cliente : "+getarg(3);
	mes "Localização do cliente : "+getarg(4);
	mes "Missão solicitada : "+getarg(5);
	mes " ";
	mes "Você vai aceitar esta missão?";
	next;
	if(select("^ff0000[»]^000000 Não, eu não vou aceitar:^3CB371[»]^000000 Sim, eu vou aceitar") == 1) {
		mes "Você recusou a missão "+ .@string$ +".";
		close;
	}
	if (checkquest(getarg(0)) > -1) mes "Você já está fazendo esta missão.";
	else {
		setquest getarg(0);
		mes "Você aceitou a missão "+ .@string$ +".";
	}
	mes "Você pode obter uma recompensa do cliente depois de terminar a missão.";
	next;
	mes "Para mais detalhes, pressione 'Alt + U', para obter mais informações na janela de missões.";
	close;
}

geffen,103,42,5	script	Sponiac	99,{
	set .np$,"^00B2EE[ Sponiac ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes .np$;
		mes "Por que você está carregando tantos itens?";
		mes "Você está em um curso de treinamento ou algo assim?";
		close;
	}
	mes .np$;
	mes "Lu~lu~lu~lu~";
	mes "Lu~ Huh?";
	next;
	mes .np$;
	mes "Uau, você deve ser um iniciante, certo?";
	mes "Que cara inocente! Ha~ hahaha.";
	emotion ET_KIK;
	next;
	mes .np$;
	mes "Por que você não faz uma caçada lá fora em vez de andar por aí e desperdiçar seu tempo?";
	next;
	if(select("^ff0000[»]^000000 Estou prestes a sair agora:^3CB371[»]^000000 Eu tenho uma missão de caça") == 1) {
		mes .np$;
		mes "Ah bem. Então, vá entender.";
		mes "Tchau~ Tchau.";
		close;
	}
	mes .np$;
	mes "Oh, você é do Grupo Éden?";
	mes "Que missão você tem?";
	next;
	switch(select("^3CB371[»]^000000 Caçando Esporos Venenoso:^3CB371[»]^000000 Caçando Fumaça:^3CB371[»]^000000 Caçando Salgueiro Ancião:^3CB371[»]^000000 Caçando Coco:^3CB371[»]^000000 Caçando o Sapo Thara")) {
		case 1: callsub L_Quest,8266,"Caçando Esporos Venenoso",1000,0,100,"Você pode encontrar Poison Spores no Geffen Dungeon 1F ou a nordeste de Prontera. Existem outros lugares, mas vou deixar para você descobrir.";
		case 2: callsub L_Quest,8267,"Caçando Fumaça",1000,0,100,"Você pode encontrar Smokies a nordeste e noroeste de Geffen. Ah, também o campo a leste de Ayothaya é um bom local para encontrá-los.";
		case 3: callsub L_Quest,8268,"Caçando Salgueiro Ancião",1000,100,110,"Você pode encontrar Elder Willows a nordeste de Geffen, a leste de payon e a sudoeste de Prontera.";
		case 4: callsub L_Quest,8269,"Caçando Coco",1000,200,150,"Você pode encontrar Cocos ao redor do leste de Payon, a sudoeste de Prontera e no campo a leste de Ayothaya.";
		case 5: callsub L_Quest,8270,"Caçando o Sapo Thara",1500,300,200,"Você pode encontrar Thara Frogs na Caverna a leste de Comodo ou na Ilha da Tartaruga.";
	}
	end;
L_Quest:
	//QuestID,Quest,BExp,JExp,PotionAmt,Desc
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == 1) {
		if (checkquest(.@quest_id,HUNTING) == 2) {
			mes .np$;
			mes getarg(1)+"!";
			mes "Uau, eu não esperava que você faria essa missão. Eu estava prestes a desistir. Mas você fez um bom trabalho. Eu agradeço!";
			next;
			mes .np$;
			mes "Bem, eu solicitei a missão e você fez um excelente trabalho.";
			mes "Eu te darei uma recompensa como prometi.";
			next;
			mes .np$;
			mes "Aqui está. Muito obrigado. Deus o abençoe!";
			getitem 569,getarg(4); //Novice_Potion
			getexp getarg(2),getarg(3);
			erasequest .@quest_id;
			close;
		}
		mes .np$;
		mes "Ah, você está certo.";
		next;
		mes .np$;
		mes "Mas, você não fez a missão ainda. Por que você veio até mim agora? Você não completou a missão!";
		mes "Eu lhe darei uma recompensa se você completar a missão. OK? Estamos bem?";
		next;
		mes .np$;
		mes getarg(5);
		mes "Você pode verificar o mapa do mundo pressionando a tecla 'ctrl + ~'.";
		next;
		mes .np$;
		mes "De qualquer forma, estarei aqui esperando por você. Por favor, volte quando terminar a missão!";
		close;
	}
	mes .np$;
	mes "Huh? Tem certeza? Algo está errado...";
	next;
	mes .np$;
	mes "Isso é estranho.";
	mes "Por favor, verifique sua janela de missão pressionando 'alt + u'.";
	close;
}

moc_ruins,173,55,3	script	Dieshin Man	89,{
	set .np$,"^00B2EE[ Dieshin Man ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes .np$;
		mes "Por que você está carregando tantos itens?";
		mes "Você está em um curso de treinamento ou algo assim?";
		close;
	}
	mes .np$;
	mes "Hu hu~~ somos entregadores~";
	mes "Somos mais rápidos que qualquer voo~!";
	next;
	mes .np$;
	mes "Bem-vindo~ bem-vindo!";
	mes "Somos a empresa de entrega mais rápida, Dieshin Corporation.";
	next;
	mes .np$;
	mes "Como posso ajudá-lo?";
	next;
	if(select("^ff0000[»]^000000 Eu não preciso de ajuda:^3CB371[»]^000000 Eu tenho uma missão de entrega") == 1) {
		mes .np$;
		mes "Ah bem. Então, vá entender. Tchau~!";
		close;
	}
	mes .np$;
	mes "Oh, você é do Grupo Éden?";
	mes "Que missão você tem?";
	next;
	switch(select("^3CB371[»]^000000 Entregue um inseticida:^3CB371[»]^000000 Entregar mel:^3CB371[»]^000000 Entregar cobertores")) {
		case 1: callsub L_Quest,8271;
		case 2: callsub L_Quest,8272;
		case 3: callsub L_Quest,8273;
	}
	end;
L_Quest:
	if (checkquest(getarg(0)) != 1) {
		mes .np$;
		mes "Huh? Tem certeza? Algo está errado...";
		next;
		mes .np$;
		mes "Isso é estranho.";
		mes "Por favor, verifique sua janela de missão pressionando 'alt + u'.";
		close;
	}
	if (countitem(6218) > 0) {
		mes .np$;
		mes "Espere, mas você já tem uma caixa de entrega.";
		next;
		mes .np$;
		mes "Não podemos permitir que você entregue duas caixas ao mesmo tempo. Então, por favor, termine sua entrega primeiro.";
		next;
		mes .np$;
		mes "Se você esqueceu onde entregar, verifique a Janela de Missões pressionando 'alt + u'.";
		close;
	}
	switch(getarg(0)) {
	case 8271:
		mes .np$;
		mes "Ah há! eu entendi!";
		mes "O inseticida? Deve ser o zoólogo no sudeste de Payon que ordenou o inseticida.";
		break;
	case 8272:
		mes .np$;
		mes "Ah há! eu entendi!";
		mes "Querida? Deve ser o Entomologista a oeste da Abadia de Santa Capitolina, que fica a nordeste de Prontera que encomendou o Mel.";
		break;
	case 8273:
		mes .np$;
		mes "Oh, certo!";
		mes "Os cobertores! Deve ser o biólogo do sul do campo Lutie que encomendou os cobertores.";
		next;
		mes .np$;
		mes "Você pode ir a Lutie perguntando ao Sr. Claus na parte nordeste de Al De Baran.";
		break;
	}
	next;
	mes .np$;
	mes "O motivo de cooperação da Dieshin é dar a entrega mais rápida e creditada. Então, por favor, entregá-lo agora sem qualquer atraso.";
	next;
	mes .np$;
	mes "Se você esqueceu onde fazer a entrega, verifique a Janela de Missões pressionando 'alt + u'.";
	getitem 6218,1; //Disin_Delivery_Box
	close;
}

pay_fild07,83,89,7	script	Zoólogo	89,{
	set .np$,"^00B2EE[ Zoólogo ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes .np$;
		mes "Por que você está carregando tantos itens?";
		mes "Você está em um curso de treinamento ou algo assim?";
		close;
	}
	mes .np$;
	mes "Você já ouviu falar de um monstro chamado Pé Grande?";
	mes "Parece um urso com pelo vermelho e tem uma cicatriz em forma de 'X' no ombro.";
	next;
	mes .np$;
	mes "Segundo pesquisas recentes, moscas e monstros Pé Grande têm uma relação simbiótica.";
	next;
	mes .np$;
	mes "Atualmente estou estudando sobre isso e tentar provar a teoria. Então pretendo matar as moscas de Pé Grande e observar a reação dele.";
	next;
	if(select("^ff0000[»]^000000 ...eu não estou interessado:^3CB371[»]^000000 Aqui está o inseticida") == 1) {
		mes .np$;
		mes "Oh, eu preciso de um inseticida para continuar o estudo.";
		close;
	}
	if (checkquest(8271) != 1) {
		mes .np$;
		mes "Huh?";
		mes "Você não é o entregador de Dieshin?";
		next;
		mes .np$;
		mes "Estou esperando uma entrega especial.";
		close;
	}
	if (countitem(6218) > 0) {
		mes .np$;
		mes "Oh Ho! Sim! Aquele!";
		next;
		mes .np$;
		mes "Esse é o inseticida que eu pedi! Você foi enviado por Dieshin certo?";
		next;
		mes .np$;
		mes "Eu estava esperando por isso!";
		mes "Me dê isto!";
		next;
		mes .np$;
		mes "Chegou muito mais rápido do que eu pensava. quero testar logo!";
		next;
		mes .np$;
		mes "Bem feito. Bom trabalho! Muito obrigado! Aqui está um pequeno presente para você.";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8271;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes .np$;
	mes "Eu aprecio sua bondade.";
	mes "Mas, o que você trouxe?";
	next;
	mes .np$;
	mes "Hmm, eu pensei que Dieshin era rápido e preciso, mas você me trouxe uma caixa vazia. Que decepcionante.";
	next;
	mes .np$;
	mes "Por favor, traga meu pedido. Dieshin lhe dará meu produto.";
	close;
}

prt_fild03,30,254,3	script	Entomologista	83,{
	set .np$,"^00B2EE[ Entomologista ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes .np$;
		mes "Por que você está carregando tantos itens?";
		mes "Você está em um curso de treinamento ou algo assim?";
		close;
	}
	mes .np$;
	mes "Você já ouviu falar sobre o monstro Horn?";
	mes "Tem a forma de um inseto com pele preta e lustrosa e tem um chifre gigante na cabeça.";
	next;
	mes .np$;
	mes "Pesquisas recentes saíram e mostraram que esse monstro pode ser domado e treinado por Honey.";
	next;
	mes .np$;
	mes "Atualmente estou estudando sobre isso e tentando provar a teoria. Então eu pretendo colocar uma armadilha Honey nos lugares que ele costuma passar e observar sua reação.";
	next;
	if(select("^ff0000[»]^000000 ...eu não estou interessado:^3CB371[»]^000000 Aqui está o mel") == 1) {
		mes .np$;
		mes "Oh, eu preciso de Honey para continuar o estudo.";
		close;
	}
	if (checkquest(8272) != 1) {
		mes .np$;
		mes "Huh?";
		mes "Você não é o entregador de Dieshin?";
		next;
		mes .np$;
		mes "Estou esperando uma entrega especial.";
		close;
	}
	if (countitem(6218) > 0) {
		mes .np$;
		mes "Oh Ho! Sim! Aquele!";
		next;
		mes .np$;
		mes "Esse é o Mel que eu pedi! Você foi enviado por Dieshin certo?";
		next;
		mes .np$;
		mes "Eu estava esperando por isso!";
		mes "Me dê isto!";
		next;
		mes .np$;
		mes "Chegou muito mais rápido do que eu pensava. quero testar logo!";
		next;
		mes .np$;
		mes "Bem feito. Bom trabalho! Muito obrigado! Aqui está um pequeno presente para você.";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8272;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes .np$;
	mes "Eu aprecio sua bondade.";
	mes "Mas, o que você trouxe?";
	next;
	mes .np$;
	mes "Hmm, eu pensei que Dieshin era rápido e preciso, mas você me trouxe uma caixa vazia. Que decepcionante.";
	next;
	mes .np$;
	mes "Por favor, traga meu pedido. Dieshin lhe dará meu produto.";
	close;
}

xmas_fild01,92,57,3	script	Biólogo	97,{
	set .np$,"^00B2EE[ Biólogo ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes .np$;
		mes "Por que você está carregando tantos itens?";
		mes "Você está em um curso de treinamento ou algo assim?";
		close;
	}
	mes .np$;
	mes "Você já ouviu falar sobre o monstro Marin?";
	mes "Parece o poring usual, mas tem uma característica única.";
	next;
	mes .np$;
	mes "Porings roubam itens se os virem no chão. No entanto, Marin não tem interesse em itens descartados. Então eu estava me perguntando, como é que Marin não presta atenção em coletar itens mesmo sendo o mesmo tipo de monstro que Porings.";
	next;
	mes .np$;
	mes "Mas, este lugar é muito frio.";
	next;
	if(select("^ff0000[»]^000000 ...eu não estou interessado:^3CB371[»]^000000 Aqui estão os cobertores") == 1) {
		mes .np$;
		mes "Hã, tanto faz. Mas tenha cuidado! é muito escorregadio aqui.";
		close;
	}
	if (checkquest(8273) != 1) {
		mes .np$;
		mes "Huh?";
		mes "Você não é o entregador de Dieshin?";
		next;
		mes .np$;
		mes "Estou esperando uma entrega especial.";
		close;
	}
	if (countitem(6218) > 0) {
		mes .np$;
		mes "Oh Ho! Sim! Aquele!";
		next;
		mes .np$;
		mes "Esses são os cobertores que encomendei! Você foi enviado por Dieshin certo?";
		next;
		mes .np$;
		mes "Eu estava esperando por isso!";
		mes "Me dê isto!";
		next;
		mes .np$;
		mes "Chegou muito mais rápido do que eu pensava. Eu quero testá-los imediatamente!";
		next;
		mes .np$;
		mes "Bem feito. Bom trabalho! Muito obrigado! Aqui está um pequeno presente para você.";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8273;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes .np$;
	mes "Eu aprecio sua bondade.";
	mes "Mas, o que você trouxe?";
	next;
	mes .np$;
	mes "Hmm, eu pensei que Dieshin era rápido e preciso, mas você me trouxe uma caixa vazia. Que decepcionante.";
	next;
	mes .np$;
	mes "Por favor, traga meu pedido. Dieshin lhe dará meu produto.";
	close;
}

payon,179,66,3	script	Dashia	931,{
	set .np$,"^00B2EE[ Dashia ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes .np$;
		mes "Por que você está carregando tantos itens?";
		mes "Você está em um curso de treinamento ou algo assim?";
		close;
	}
	mes .np$;
	mes "Aqui está um pouco de cabelo ~ um relógio quebrado~!";
	next;
	mes .np$;
	mes "Relógio quebrado ..? Hu hu..";
	next;
	mes .np$;
	mes "Ei~! Parceiro! Você parece um aventureiro muito rico.";
	next;
	if(select("^ff0000[»]^000000 Eu sou um pobre aventureiro:^3CB371[»]^000000 Eu tenho uma missão de coleta") == 1) {
		mes .np$;
		mes "Sério? Hum, certo. Você não parece sofisticado.";
		next;
		mes .np$;
		mes "Você é um pobre aventureiro~! Boa sorte!";
		close;
	}
	mes .np$;
	mes "Oh, você é do Grupo Éden?";
	mes "Que missão você tem?";
	next;
	switch(select("^3CB371[»]^000000 Junte Ossos:^3CB371[»]^000000 Colete Pele de Animal:^3CB371[»]^000000 Recolher Peelings:^3CB371[»]^000000 Junte Caudas:^3CB371[»]^000000 Reúna cookies:^3CB371[»]^000000 Junte bigodes")) {
	case 1:
		callsub L_Quest,8274;
		if (countitem(932) > 9) {
			callsub L_Reward;
			delitem 932,10; //Skel_Bone
			getitem 569,100; //Novice_Potion
			getexp 1000,100;
			erasequest 8274;
			close;
		}
		mes .np$;
		mes "Eu posso ver que você tem uma missão de coleta.";
		mes "Mas, onde estão os Ossos de esqueleto?";
		next;
		mes .np$;
		mes "Você ainda não fez a missão. Por que você voltou aqui?";
		next;
		mes .np$;
		mes "Você não tem 'Ossos de esqueleto' suficientes... Você pode encontrá-lo quando caça Esqueletos.";
		next;
		mes .np$;
		mes "Para encontrar Skeletons, é melhor verificar Payon Dungeon 1F, que está localizado em Archer Village, ao norte de Payon.";
		break;
	case 2:
		callsub L_Quest,8275;
		if ((countitem(948) > 9) && (countitem(919) > 4)) {
			callsub L_Reward;
			getitem 569,100; //Novice_Potion
			delitem 948,10; //Bear's_Foot
			delitem 919,5; //Animal's_Skin
			getexp 1000,100;
			erasequest 8275;
			close;
		}
		mes .np$;
		mes "Eu posso ver que você tem uma missão de coleta.";
		mes "Mas, onde estão as Peles de Urso e Peles de Animais?";
		next;
		mes .np$;
		mes "Você ainda não fez a missão. Por que você voltou aqui?";
		next;
		mes .np$;
		mes "Você tem que trazer 10 Pele de Urso e 5 Pele de Animal!";
		mes "Você pode encontrar Pele de Urso da Pé Grande e Pele de Animal da Animals.";
		next;
		mes .np$;
		mes "Pé Grande fica ao norte do campo de Geffen ou a sudeste do campo de Payon.";
		break;
	case 3:
		callsub L_Quest,8276;
		if ((countitem(955) > 19) && (countitem(910) > 19)) {
			callsub L_Reward;
			delitem 955,20; //Worm_Peelings
			delitem 910,20; //Garlet
			getitem 569,100; //Novice_Potion
			getexp 1000,100;
			erasequest 8276;
			close;
		}
		mes .np$;
		mes "Eu posso ver que você tem uma missão de coleta.";
		mes "Mas, onde estão os Peelings e Garlets de Minhoca?";
		next;
		mes .np$;
		mes "Você tem que trazer '20 Peelings de minhocas' e '20 Garlets' para completar a missão.";
		next;
		mes .np$;
		mes "Você pode obter esses itens de 'Pierre', 'Andre', 'De Niro', 'Vitara', 'Thief Bug', 'Female Thief Bug' e 'Male Thief Bug''.";
		next;
		mes .np$;
		mes "'Piere', 'Andre', 'De Niro' e 'Vitata' estão no Ant Hell, localizado perto da Fortaleza de São Damião, a sudoeste de Morocc.";
		next;
		mes .np$;
		mes "'Thief Bug', 'Female Thief Bug' e 'Male Thief Bug' estão no Prontera Culvert. Para chegar lá, você precisa ser voluntário da força punitiva Prontera Culvert.";
		break;
	case 4:
		callsub L_Quest,8277;
		if ((countitem(942) > 9) && (countitem(1026) > 9) && (countitem(945) > 9)) {
			callsub L_Reward;
			delitem 942,10; //Yoyo_Tail
			delitem 1026,10; //Acorn
			delitem 945,10; //Raccoon_Leaf
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8277;
			close;
		}
		mes .np$;
		mes "Eu posso ver que você tem uma missão de coleta.";
		mes "Mas, onde estão os Yoyo Tails, Acorns e Raccoon Leafs?";
		next;
		mes .np$;
		mes "Você ainda não fez a missão. Por que você voltou aqui?";
		mes "Você tem que trazer '10 Yoyo Tails', '10 Acorns' e '10 Raccoon Leafs' para completar a missão.";
		next;
		mes .np$;
		mes "Você pode obter Yoyo Tails de 'Yoyo's', Acorn de 'Coco's' e Raccoon Leaf de 'Smokies'.";
		next;
		mes .np$;
		mes "Você pode encontrá-los facilmente ao redor dos campos, mas o campo a leste de Ayothaya é o melhor local para caçar todos os 3 monstros.";
		next;
		mes .np$;
		mes "Para chegar a Ayothaya, é melhor você ir para 'Aibakthing' em Alberta e pagar sua passagem.";
		break;
	case 5:
		callsub L_Quest,8278;
		if ((countitem(538) > 4) && (countitem(539) > 0)) {
			callsub L_Reward;
			delitem 538,5; //Well_Baked_Cookie
			delitem 539,1; //Piece_Of_Cake
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8278;
			close;
		}
		mes .np$;
		mes "Eu posso ver que você tem uma missão de coleta.";
		mes "Mas, onde estão os Biscoitos Bem Cozidos e o Pedaço de Bolo?";
		next;
		mes .np$;
		mes "Você ainda não fez a missão. Por que você voltou aqui?";
		mes "Você tem que trazer '5 biscoitos bem cozidos' e '1 pedaço de bolo' para completar a missão.";
		next;
		mes .np$;
		mes "Você pode obter Biscoito Bem Assado de 'Cookies' que usa camisas vermelhas ou verdes.";
		mes "Para obter Piece of Cake, você deve procurar por 'Mystcases'.";
		next;
		mes .np$;
		mes "Ambos os monstros podem ser encontrados no Armazém da Fábrica de Brinquedos ou na Sala de Monitoramento de Brinquedos. Para chegar lá, pergunte ao Sr. Claus na parte nordeste de Al De Baran.";
		break;
	case 6:
		callsub L_Quest,8279;
		if ((countitem(1017) > 4) && (countitem(1018) > 2)) {
			callsub L_Reward;
			delitem 1017,5; //Moustache_Of_Mole
			delitem 1018,3; //Nail_Of_Mole
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8279;
			close;
		}
		mes .np$;
		mes "Eu posso ver que você tem uma missão de coleta.";
		mes "Mas, onde estão os bigodes de toupeira e garras de toupeira??";
		next;
		mes .np$;
		mes "Você ainda não fez a missão. Por que você voltou aqui?";
		mes "Você tem que trazer '5 Mole Whiskers' e '3 Mole Claws' para completar a missão.";
		next;
		mes .np$;
		mes "Você pode obter esses itens de 'Martins'.";
		mes "Martins pode ser encontrado no Mjolnir Dead Pit, mas o segundo andar é perigoso, então tome cuidado!";
		next;
		mes .np$;
		mes "Para chegar lá, pergunte a um funcionário da Kafra em Geffen, eles podem te teleportar até lá.";
		break;
	}
	next;
	mes .np$;
	mes "De qualquer forma, estarei aqui esperando por você. Por favor, volte quando terminar a missão!";
	close;
L_Quest:
	if (checkquest(getarg(0)) != 1) {
		mes .np$;
		mes "Huh? Tem certeza? Algo está errado...";
		next;
		mes .np$;
		mes "Isso é estranho.";
		mes "Por favor, verifique sua janela de missão pressionando 'alt + u'.";
		close;
	}
	return;
L_Reward:
	mes .np$;
	mes "Uau~ você fez um excelente trabalho!";
	next;
	mes .np$;
	mes "Muito obrigado. Deus o abençoe!";
	emotion ET_SMILE;
	next;
	mes .np$;
	mes "Ha~! Ha~!";
	mes "Eu lhe darei uma recompensa como prometido.";
	return;
}
