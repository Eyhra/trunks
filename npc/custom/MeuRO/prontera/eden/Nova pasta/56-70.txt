//===== rAthena Script =======================================
//= Eden Group Quests - Mission [56 - 70]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 56 - 70.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Added checkquest confirmation for -1. [Euphy]
//= 1.3 Fixed Puppet Master's Agony Quest (12217 -> 3259). [Joseph]
//= 1.4 Fixed invalid check (Magic Wand Quest). [Joseph]
//= 1.5 Optimized, based on code by Zopokx. [Euphy]
//= 1.6 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.7 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,42,38,3	script	Placa de Missão[56-70]	857,{
	set .np$,"^00B2EE[ Placa de Missão ]^000000";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes .np$;
		mes "Para obter essas missões, primeiro preciso ingressar no Grupo Eden. Devo encontrar o secretário Lime Evenor e me tornar um membro.";
		close;
	}
	mes .np$;
	mes "Aqui está a lista de várias aventuras para aventureiros de nível 56~70.";
	next;
	mes .np$;
	mes "Muitas missões, como Monster Hunting, Delivery Goods, Finding People.";
	next;
	switch(select("^3CB371[»]^000000 Missões 1~5:^3CB371[»]^000000 Missões 6~10:^3CB371[»]^000000 Missões 11~15")) {
	case 1:
		switch(select("^3CB371[»]^000000 O que é essa bandagem?:^3CB371[»]^000000 Jacarés Perigosos:^3CB371[»]^000000 Isso não era uma sereia...:^3CB371[»]^000000 Meu país está me chamando!")) {
			case 1: callsub L_HuntingQuest,3250,15000,5000,930,30;
			case 2: callsub L_HuntingQuest,3251,16000,6000;
			case 3: callsub L_HuntingQuest,3252,17000,8000;
			case 4: callsub L_Quest,3254,18000,8000,7043,10,1056,30;
		}
	case 2:
		switch(select("^3CB371[»]^000000 Caçar Javali:^3CB371[»]^000000 Preparando o verão:^3CB371[»]^000000 O rancor de uma mulher:^3CB371[»]^000000 Ingredientes especiais:^3CB371[»]^000000 A agonia do mestre de marionetes")) {
			case 1: callsub L_HuntingQuest,3255,16000,7000;
			case 2: callsub L_Quest,3256,15000,5000,918,30;
			case 3: callsub L_HuntingQuest,3257,17000,8000;
			case 4: callsub L_Quest,3258,16000,7000,7198,40;
			case 5: callsub L_HuntingQuest,3259,17000,7000,1060,30;
		}
	case 3:
		switch(select("^3CB371[»]^000000 Moscas Cansativas:^3CB371[»]^000000 Munak Perigoso:^3CB371[»]^000000 Faça o mundo verde:^3CB371[»]^000000 Varinha mágica")) {
			case 1: callsub L_HuntingQuest,3260,16000,6000;
			case 2: callsub L_HuntingQuest,3261,16000,7000;
			case 3: callsub L_Quest,3262,17000,7000,1032,40;
			case 4: callsub L_Quest,3263,18000,8000,1021,30,7150,30;
		}
	}
	end;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes .np$;
		mes "Você gostaria de aceitar esta missão?";
		next;
		if(select("^3CB371[»]^000000 Aceitar a Missão:^ff0000[»]^000000 Cancelar") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes .np$;
				mes "Essas missões não são adequadas para o meu nível. Eu deveria procurar outras missões.";
				close;
			}
			setquest .@quest_id;
			mes .np$;
			mes "Aceitei com sucesso a missão. Eu tenho que voltar e confirmar meu trabalho depois de completar a missão.";
			next;
			mes .np$;
			mes "Esta missão não tem limite de tempo.";
		}
		close;
	}
	if (getargcount() > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id) < 2 && (countitem(getarg(3)) < getarg(4) || .@items == 1)) {
		mes .np$;
		mes "Você tem uma missão em andamento. Deseja verificar os detalhes?";
		next;
		if(select("^3CB371[»]^000000 Checar detalhes:^ff0000[»]^000000 Cancelar") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else {
		mes .np$;
		mes "Eu tenho feito muito bem para a missão. Devo denunciar agora?";
		next;
		if(select("^3CB371[»]^000000 Entregar a Missão:^ff0000[»]^000000 Cancelar") == 1) {
			delitem getarg(3),getarg(4);
			if (.@items)
				delitem getarg(5),getarg(6);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes .np$;
			mes "Você completou a missão. Obter recompensas.";
		}
		close;
	}
	end;

L_HuntingQuest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes .np$;
		mes "Você gostaria de aceitar esta missão?";
		next;
		if(select("^3CB371[»]^000000 Aceitar a Missão:^ff0000[»]^000000 Cancelar") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes .np$;
				mes "Essas missões não são adequadas para o meu nível. Eu deveria procurar outras missões.";
				close;
			}
			setquest .@quest_id;
			mes .np$;
			mes "Aceitei com sucesso a missão. Eu tenho que voltar e confirmar meu trabalho depois de completar a missão.";
			next;
			mes .np$;
			mes "Esta missão não tem limite de tempo.";
		}
		close;
	}
	if (getargcount() > 3) {
		if (countitem(getarg(3)) < getarg(4))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id,HUNTING) < 2 || .@items == 1) {
		mes .np$;
		mes "Você tem uma missão em andamento. Deseja verificar os detalhes?";
		next;
		if(select("^3CB371[»]^000000 Checar detalhes:^ff0000[»]^000000 Cancelar") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else if (checkquest(.@quest_id,HUNTING) == 2) {
		mes .np$;
		mes "Eu tenho feito muito bem para a missão. Devo denunciar agora?";
		next;
		if(select("^3CB371[»]^000000 Entregar a Missão:^ff0000[»]^000000 Cancelar") == 1) {
			if (.@items)
				delitem getarg(3),getarg(4);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes .np$;
			mes "Você completou a missão. Obter recompensas.";
		}
		close;
	}
	end;

L_Details:
	switch(getarg(0)) {
	case 3250:
		mes .np$;
		mes "Esta missão é atribuída pela corporação Rekenber de Lighthalzen. Abaixo estão os detalhes.";
		next;
		mes .np$;
		mes "A corporação Rekenber está prestes a lançar um novo projeto chamado 'Green Medical Center'. Para o primeiro produto do projeto, estamos preparando Bandagens de bem-estar.";
		next;
		mes .np$;
		mes "Em relação ao projeto, precisamos pesquisar as reações dos monstros quando são embrulhados com Bandagens Podres.";
		next;
		mes .np$;
		mes "Se você estiver interessado, por favor, cace 30 múmias e traga 30 bandagens podres. Você pode encontrá-los na Pirâmide Oeste de Morocc.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "- Diretora-chefe da corporação Rekenber 'Julie E Delph' -";
		break;
	case 3251:
		mes .np$;
		mes "Esta missão é atribuída pela sociedade cooperativa Comodo. Abaixo estão os detalhes.";
		next;
		mes .np$;
		mes "Os jacarés enlouqueceram esses dias, eles atacam mulheres e bêbados que ficam perto da praia.";
		next;
		mes .np$;
		mes "Esta é uma situação muito vergonhosa para nós, o local de férias mais maravilhoso em Rune-Midgarts.";
		next;
		mes .np$;
		mes "Então, seria muito útil se você caçasse 30 jacarés para limpar essa situação.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		break;
	case 3252:
		mes .np$;
		mes "Esta missão é atribuída por um cliente desconhecido de Izlude.";
		next;
		mes .np$;
		mes "Admiro a praia de Izlude, desde que nasci.";
		next;
		mes .np$;
		mes "Quando eu era jovem, encontrei uma criatura misteriosa na masmorra de Izlude, nunca tinha visto algo assim antes...";
		next;
		mes .np$;
		mes "Imediatamente pensei que fosse uma sereia. Fiquei tão surpreso, estudei sobre sereias em toda a minha vida.";
		next;
		mes .np$;
		mes "Mas, ao estudar mais, descobri que a criatura não era uma sereia... sim, não era nada bonita como as outras sereias...";
		next;
		mes .np$;
		mes "Eu finalmente rastreei o fato de que era um tritão, não uma sereia!";
		next;
		mes .np$;
		mes "Merman... desperdicei minha vida estudando esse monstro! Então, por favor, cace 30 tritões para me fazer sentir melhor.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Izlude 'A sereia dos sonhos perdidos' --";
		break;
	case 3254:
		mes .np$;
		mes "Esta missão é atribuída pelo oficial do governo do Reino Rune-Midgarts.";
		next;
		mes .np$;
		mes "Para evitar uma inundação em Prontera, decidimos construir uma enorme barragem para regular o abastecimento de água.";
		next;
		mes .np$;
		mes "Mas, estamos com falta de suprimentos e precisamos coletar areia fina e areia.";
		next;
		mes .np$;
		mes "Então, gostaríamos de pedir ajuda. Pessoal, por favor, tragam 10 areia fina e 30 grão cada para que possamos construir a barragem.";
		next;
		mes .np$;
		mes "Você pode obter esses materiais de Sand Man, desejo-lhe boa sorte!";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Oficial do governo do Reino Rune-Midgarts, Dufre Kent --";
		break;
	case 3255:
		mes .np$;
		mes "Esta missão é atribuída pela prefeitura de Payon.";
		next;
		mes .np$;
		mes "Os animais selvagens são sempre aborrecimentos para os agricultores.";
		next;
		mes .np$;
		mes "Eles atacam nossa fazenda e arruínam plantações inteiras!! Não aguentamos mais!";
		next;
		mes .np$;
		mes "Então, gostaríamos de pedir a ajuda de bravos aventureiros.";
		next;
		mes .np$;
		mes "Por favor, cace 30 Selvagens para salvar nossas colheitas!";
		next;
		mes .np$;
		mes "Você pode facilmente encontrar esses monstros em torno de nossa cidade e campo.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Membros da Câmara Municipal de Payon --";
		break;
	case 3256:
		mes .np$;
		mes "Esta missão é atribuída pelo ferreiro marroquino Aragham.";
		next;
		mes .np$;
		mes "Olá~~~~, Aventureiros! Este é Aragham, o ferreiro mais quente de Morocc!";
		next;
		mes .np$;
		mes "Enfim, eu tenho um filho chamado Aragam Junior, o bonitinho. Haha.";
		next;
		mes .np$;
		mes "Ele estará participando do acampamento de verão neste verão, mas ele não sabe nadar.";
		next;
		mes .np$;
		mes "Eu quero ensiná-lo, mas como você sabe eu sou um ferreiro, muito longe de nadar! he he.";
		next;
		mes .np$;
		mes "Mas, imaginei que se eu fizesse nadadeiras, seria ótimo para a confiança dele~!";
		next;
		mes .np$;
		mes "Para fazer isso, eu preciso de 30 Sticky Webfoots de um Roda Frog. Você pode trazê-los para mim?";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "PS. Não me compare com Hollgrehenn ou Antonio!!!!";
		next;
		mes .np$;
		mes "-- O ferreiro mais quente, ferreiro Aragham de Morroc --";
		break;
	case 3257:
		mes .np$;
		mes "Esta missão é atribuída por um inventor Dorian de Izlude.";
		next;
		mes .np$;
		mes "Você já ouviu a história de que se as mulheres têm rancor em sua mente, isso trará desastres naturais.";
		next;
		mes .np$;
		mes "Uau, então os caras devem tomar cuidado com essas mulheres sensíveis! Certifique-se de que eles não têm nenhum rancor de você.";
		next;
		mes .np$;
		mes "As mulheres podem trazer fenômenos estranhos com elas!";
		next;
		mes .np$;
		mes "Estou tão intrigado com essa história que estou tentando provar que pode ser verdade.";
		next;
		mes .np$;
		mes "Então eu preciso deixar as mulheres chateadas! Haha, eu sei que ideia ruim, certo?";
		next;
		mes .np$;
		mes "Mas isso é sério apenas para estudar ... Então, por favor, cace 30 Sohee's que parecem ser revividos de almas vitimizadas.";
		next;
		mes .np$;
		mes "Para que eu possa continuar observando o caso.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Elegância inventor Dorian --";
		break;
	case 3258:
		mes .np$;
		mes "Esta missão é atribuída por Wallah de Payon.";
		next;
		mes .np$;
		mes "Olá, aventureiro. Como você está? Como está sua saúde?";
		next;
		mes .np$;
		mes "Eu sou um farmacêutico e estou tendo dificuldade em obter ingredientes especiais ultimamente.";
		next;
		mes .np$;
		mes "Porque eu sou uma menina sensível, como posso conseguir essas coisas sozinha.";
		next;
		mes .np$;
		mes "Então, por favor me ajude. Apenas traga 40 Folhas Enormes para mim, isso será suficiente para completar meu remédio especial!";
		next;
		mes .np$;
		mes "Por favor, traga-o o mais rápido possível, as pessoas precisam do meu remédio.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Wallah --";
		break;
	case 3259:
		mes .np$;
		mes "Esta missão é atribuída pelo mestre de marionetes Woonute da Geffen.";
		next;
		mes .np$;
		mes "Você sabe que ser um mestre de marionetes é um trabalho difícil para satisfazer as pessoas.";
		next;
		mes .np$;
		mes "As pessoas podem pesquisar e ver tantas coisas diferentes por conta própria hoje em dia.";
		next;
		mes .np$;
		mes "Suas expectativas estão ficando altas, não posso mais satisfazê-las.";
		next;
		mes .np$;
		mes "Mas se eu puder criar novos conceitos de brinquedos, como brinquedos vivos, será inovador.";
		next;
		mes .np$;
		mes "Ouvi dizer que existem bonecas vivas chamadas Marionettes. Você pode caçar 30 Marionetes para mim? E traga também 30 Cabelos Dourados, que serão ótimos materiais para os novos brinquedos.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Mestre de Marionetes Woonute --";
		break;
	case 3260:
		mes .np$;
		mes "Esta missão é atribuída por um exterminador de Prontera.";
		next;
		mes .np$;
		mes "À medida que o clima esquenta, os insetos se multiplicam cada vez mais. Já está fora de controle.";
		next;
		mes .np$;
		mes "As pessoas em Prontera não conseguem dormir por causa de todos os insetos em sua casa.";
		next;
		mes .np$;
		mes "Então, por favor, ajude-nos a caçar 30 moscas caçadoras. Então as moscas podem ser reduzidas lentamente.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- O exterminador de Prontera --";
		break;
	case 3261:
		mes .np$;
		mes "Esta missão é atribuída por Cheese Dongja de Payon.";
		next;
		mes .np$;
		mes "Você já ouviu falar sobre Munak??";
		next;
		mes .np$;
		mes "O monstro feio Munak está ameaçando as pessoas em Payon.";
		next;
		mes .np$;
		mes "Então, precisamos de bravos aventureiros como você!";
		next;
		mes .np$;
		mes "Please hunt 30 Munaks then Payon will be in peace.";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "- Dongja de queijo Payon -";
		break;
	case 3262:
		mes .np$;
		mes "Esta missão é atribuída por um negociante de pedras preciosas, Ibraham de Morocc.";
		next;
		mes .np$;
		mes "Uma pequena e linda flor no deserto árido... Que cena incrível seria!";
		next;
		mes .np$;
		mes "Plantar flores pode tornar o mundo verde e será tão bonito em todos os lugares.";
		next;
		mes .np$;
		mes "Pretendo plantar flores fortes no deserto de Marrocos para que o solo melhore.";
		next;
		mes .np$;
		mes "Eu preciso de 40 Maneater Blossom da Flora, eu sei que Maneater Blossom é uma flor feia, mas ainda assim é uma planta.";
		next;
		mes .np$;
		mes "Espero que você possa caçar Flora e me trazer 40 Maneater Blossoms. Você está tornando o mundo verde! Seja orgulhoso!";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- Morocc Ibraham --";
		break;
	case 3263:
		mes .np$;
		mes "Esta missão é atribuída pelo ferreiro de Payon Antonio.";
		next;
		mes .np$;
		mes "Varinha mágica! Você já ouviu falar sobre isso? É uma varinha incrível!";
		next;
		mes .np$;
		mes "Se você disser as palavras mágicas e balançar a varinha, seus desejos se tornarão realidade.";
		next;
		mes .np$;
		mes "Então, resolvi fazer sozinha. Então eu serei rico e poderoso~! Haha!!";
		next;
		mes .np$;
		mes "Para fazer a varinha, preciso de alguns materiais especiais.";
		next;
		mes .np$;
		mes "Por favor, traga-me 30 Chifres de Dokebi e 30 Cortes de Bambu. Se você fizer isso, eu vou te emprestar minha varinha mágica uma vez. Haha, bom negócio?";
		next;
		mes .np$;
		mes "Por favor, confirme para nós depois de completar a missão, então nós lhe daremos as recompensas adequadas.";
		next;
		mes .np$;
		mes "-- O ferreiro de Payon Antonio --";
		break;
	}
	return;
}
