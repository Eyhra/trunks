//===== rAthena Script =======================================
//= Eden Group Quests - Missão [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,36,38,3	script	Placa de Missão[11-25]#Tuto	4_BOARD3,{
	set .np$,"^00B2EE[ Placa de Missão ]^000000";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Você não é um membro qualificado. Você não pode usar o quadro de avisos.";
		close;
	}
	mes .np$;
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes " - Você deve coletar sua - ";
			mes " - recompensa antes de começar - ";
			mes " - uma nova missão. - ";
			mes " ";
			mes " - Gerente: Spike - ";
			close;
		}
		else if (.@j >= 0) {
			mes " - Você só pode solicitar - ";
			mes " - uma missão de cada vez. - ";
			mes " ";
			mes " - Gerente: Spike - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - Não há tarefas - ";
		mes " - aberto para você agora. - ";
		close;
	}
	if (BaseLevel > 25) {
		mes "Seu nível não é qualificado. Você não pode usar o quadro de avisos.";
		close;
	}
	mes " - Quadro de avisos da missão -";
	mes " - para iniciantes. -";
	mes " ";
	mes " - Gerente: Spike -";
	next;
	switch(select("^3CB371[»]^000000 Caça ao Zangão:^3CB371[»]^000000 Caça ao Condor:^3CB371[»]^000000 Perna de Gafanhoto:^3CB371[»]^000000 Caça ao Talo de Verme:^3CB371[»]^000000 Caça ao Esporo:^3CB371[»]^000000 Controle de Pragas:^3CB371[»]^000000 Caça Muka:^3CB371[»]^000000 Caça Familiar:^3CB371[»]^000000 Coletar Penas:^3CB371[»]^000000 Coletar Esporos de Veneno:^ff0000[»]^000000 Cancelar")) {
		case 1: callsub L_Quest,11114,"Caça ao Zangão"," - Cace 10 ^4d4dff'Zangão'^000000 - "," - noroeste de Prontera. - ";
		case 2: callsub L_Quest,11115,"Caça ao Condor"," - Cace 20 ^4d4dff'Condors'^000000 - "," - Sul do Morocc. - ";
		case 3: callsub L_Quest,11116,"Perna de Gafanhoto"," - Cace 10 ^4d4dff'Rockers'^000000 - "," - sudoeste de Prontera e - "," - traga 10 ^4d4dffPernas de Gafanhoto^000000. - ";
		case 4: callsub L_Quest,11117,"Caça ao Talo de Verme"," - Cace 20 ^4d4dff'Talo de Verme'^000000 - "," - sudeste de Payon.- ";
		case 5: callsub L_Quest,11118,"Caça ao Esporo"," - Cace 30 ^4d4dff'Esporos'^000000 - "," - em torno de Payon. - ";
		case 6: callsub L_Quest,11119,"Controle de Pragas"," - Cace 20 ^4d4dffOvos de Besouro-Ladrão^000000 - "," - em Prontera Culvert - "," - traga 10 ^4d4dffCrisálidas^000000. - ";
		case 7: callsub L_Quest,11120,"Caça Muka"," - Cace 20 ^4d4dff'Muka'^000000 - "," - sul de Prontera. - ";
		case 8: callsub L_Quest,11121,"Caça Familiar"," - Cace 20 ^4d4dffFamiliar^000000 - "," - nos esgotos do bueiro - "," - oeste de Prontera. - ";
		case 9: callsub L_Quest,11122,"Coletar Penas"," - Cace 30 ^4d4dff'Peco Pecos'^000000 - "," - sul de Prontera. - ";
		case 10: callsub L_Quest,11123,"Coletar Esporos de Veneno"," - Cace 30 ^4d4dffEsporos Venenosos^000000 - "," - dentro da Torre Geffen e - "," - traga 5 ^4d4dffEsporos Venenosos^000000. - ";
		case 11:
			mes " - Pare de ler o quadro de avisos. - ";
			close;
	}

L_Quest:
	.@quest_status = checkquest(getarg(0)+10,PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes " - Missão ^4d4dff"+ getarg(1) +"^000000 - ";
		mes " - ja esta finalizado por hoje. - ";
		mes " ";
		mes " - Gerente: Spike - ";
		close;
	}
	mes " - Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	mes " ";
	mes " - Gerente: Spike - ";
	next;
	if (select("^3CB371[»]^000000 Eu vou fazer essa Missão:^ff0000[»]^000000 Procure outras Missões") == 2) {
		mes " - Vamos procurar -";
		mes " - outras missões. - ";
		close;
	}
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	set .np$,"^00B2EE[ Spike ]^000000";
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Caça ao Zangão",900;
				case 11115: callsub L_Quest,.@i,"Caça ao Condor",1800;
				case 11116: callsub L_Quest,.@i,"Perna de Gafanhoto",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Caça ao Talo de Verme",2600;
				case 11118: callsub L_Quest,.@i,"Caça ao Esporo",3900;
				case 11119: callsub L_Quest,.@i,"Controle de Pragas",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Caça Muka",3200;
				case 11121: callsub L_Quest,.@i,"Caça Familiar",3200;
				case 11122: callsub L_Quest,.@i,"Coletar Penas",5400;
				case 11123: callsub L_Quest,.@i,"Coletar Esporos de Veneno",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes .np$;
			mes "Você ainda não terminou sua Missão.";
			mes "Mantenha o bom trabalho. Você está quase pronto.";
			close;
		}
	}
	mes .np$;
	mes "Você está procurando emprego?";
	mes "Sempre temos muito trabalho a fazer.";
	next;
	mes .np$;
	mes "Por que você não olha ao redor e encontra algumas tarefas interessantes?";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes .np$;
				mes "Você não tem o suficiente "+ getitemname(.@item_req) +".";
				mes "Você precisa trazer "+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes .np$;
		mes "Hum... deixe-me ver~";
		mes "Você completou a missão '"+ getarg(1) +"'.";
		next;
		mes .np$;
		mes "Bom trabalho~!!";
		mes "Nosso cliente está muito feliz com seu trabalho. Ele vai apreciar o que você fez.";
		mes "Por favor, ajude-nos novamente. Obrigado!";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		close;
	}
}
