//===== rAthena Script ======================================= 
//= Grupo Éden Quests - Common NPCs
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Grupo Éden Headquarter NPC's.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Grupo Éden Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 Added support to Para_Team_Mark_ in Grupo Éden. [AtlantisRO/Ragno]
//= 2.0 Added use of F_HasEdenGroupMark function. [Ragno]
//= 2.1 Traduzido para o português. [hannicaldummy]
//============================================================

// Common NPCs :: lv_tre
//============================================================
moc_para01,27,35,5	script	Secretário Lime Evenor	4_F_EDEN_OFFICER,{
	set .np$,"^00B2EE[ Lime Evenor ]^000000";
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Você possui muitos itens. Por favor, abra espaço no inventório primeiro. -";
		close;
	}
	if (countitem(6219) > 0) {
		mes .np$;
		mes "Graças a muitos aventureiros que visitam nosso Grupo Éden, preparamos recentemente uma nova versão do Grupo Éden Mark para nossos visitantes.";
		mes "E também é possível trocar a versão antiga do Grupo Éden Mark por uma nova.";
		next;
		mes .np$;
		mes "Embora tenha um pequeno atraso, esta nova versão do Grupo Éden Mark o enviará de volta ao local salvo atual.";
		mes "Quer trocar o seu?";
		next;
		if (select("^3CB371[»]^000000 Certo:^ff0000[»]^000000 Nada") == 2){
			mes .np$;
			mes "Que possível razão você teria... para não trocar?";
			close;
		}
		delitem 6219,1;
		getitem 22508,1;
		mes .np$;
		mes "Ai está. Volte novamente~";
		close;
	}
	mes .np$;
	mes "As pessoas que seguem seus sonhos e romances me ouvem. Somos representantes do paraíso chamado 'O jardim do Éden' chamado grupo Éden. Não há lugar como este em qualquer lugar.";
	mes "Olá. Posso ajudar?";
	next;
	while (1) {
		switch (select("^3CB371[»]^000000 O que é grupo Éden?:^3CB371[»]^000000 Faça parte do Grupo Éden:^3CB371[»]^000000 Registrar nova missão:^3CB371[»]^000000 Procure por missões:^ff0000[»]^000000 Cancelar")) {
		case 1:
			mes .np$;
			mes "Grupo Éden é um grupo que ajuda pessoas que sofrem de angústias em Midgard.";
			next;
			mes .np$;
			mes "Se as pessoas precisarem de ajuda, colocamos o caso no quadro de avisos e o apresentamos aos nossos membros que estão ansiosos para ajudar os necessitados.";
			next;
			mes .np$;
			mes "Os membros do Éden revisarão as missões e ajudarão aqueles que as postarem.";
			mes "Os membros fazem essas missões por recompensas.";
			next;
			mes .np$;
			mes "Se você não está muito familiarizado com Midgard, esta é uma ótima maneira de explorar o mundo.";
			mes "Ou, se você for maduro o suficiente, poderá viajar pelo mundo e fazer novos amigos enquanto faz missões. Seu nome honroso será espalhado entre as terras.";
			next;
			break;
		case 2:
			if (!callfunc("F_HasEdenGroupMark")) {
				mes .np$;
				mes "Você pode ser um membro do Eden simplesmente registrando-se comigo.";
				mes "Você gostaria de se juntar ao Grupo Éden?";
				next;
				switch (select("^3CB371[»]^000000 Sim, quero participar:^ff0000[»]^000000 Não, eu não quero participar")) {
				case 1:
					mes .np$;
					mes "Corretamente. Excelente! Por favor, escreva seu nome aqui.";
					input .@input$;
					next;
					mes .np$;
					mes "Você terminou?";
					mes "Deixe-me ver.";
					next;
					mes .np$;
					mes "Seu nome é ^3131FFBo"+.@input$+"Ba^000000...? Isso está certo?";
					mes "Huh? Não é??";
					next;
					emotion ET_SWEAT;
					mes .np$;
					mes "Hmm, isso não é o que você escreveu?";
					mes "Ok, hmm, parece um pouco difícil de ler.";
					next;
					mes .np$;
					mes "Você poderia, por favor, escrever seu nome novamente? Por favor, escreva-o ^3131FFClaramente^000000.";
					input .@input$;
					next;
					mes .np$;
					mes "Ah~ ha. Você é ^3131FF"+strcharinfo(0)+"^000000.";
					mes "Acertei dessa vez.";
					next;
					emotion ET_BEST;
					mes .np$;
					mes "Já colocamos seu nome na lista.";
					mes "Bem-vindo ao novo membro do grupo Eden!";
					next;
					mes .np$;
					mes "Espero que você possa fazer um ótimo trabalho como membro do Éden.";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes .np$;
					mes "Ainda tem dúvidas sobre o Grupo Éden?";
					next;
					break;
				}
			} else {
				mes .np$;
				mes "Você já é membro do Grupo Éden.";
				next;
			}
			break;
		case 3:
			mes .np$;
			mes "Quer registrar algumas missões que deseja que façamos?";
			next;
			if(select("^3CB371[»]^000000 Sim, quero me cadastrar:^ff0000[»]^000000 Não, eu não") == 1) {
				mes .np$;
				mes "Tudo bem. Por favor, escreva seu nome nele.";
				input .@input$;
				next;
				mes .np$;
				mes "^3131FF"+strcharinfo(0)+"^000000.. Esse é o seu nome?";
				mes "Hmm, sua caligrafia não está clara. Eu não posso ler isto";
				next;
				mes .np$;
				mes "^3131FF"+strcharinfo(0)+"^000000, me diga o que você quer se registrar.";
				mes "Para qual mapa devemos ir?";
				input .@input$;
				next;
				mes .np$;
				mes "^3131FFMapa de Missão: "+.@inputstr$+"^000000";
				mes "hum, vou anotar isso.";
				next;
				mes .np$;
				mes "Por favor, deixe-me saber que tipo de missões você está procurando.";
				mes "Deveria ser brevemente como 'Caçar 10 Porings.'";
				input .@input$;
				next;
				mes .np$;
				mes "Você terminou?";
				mes "Deixe-me ver.";
				next;
				mes .np$;
				mes "Huh...........";
				next;
				mes .np$;
				mes "Hum.. huh??..................";
				next;
				emotion ET_THINK;
				mes .np$;
				mes "Eu acho que você deveria completar uma de nossas missões antes de assumir uma nova missão.";
				next;
				emotion ET_HNG;
				mes .np$;
				mes "^3131FFCliente: Lime Evenor^000000";
				mes "^3131FFMissão: Pratique sua caligrafia por um mês.^000000";
				mes "Você tem uma caligrafia muito ruim!";
				next;
				mes .np$;
				mes "^3131FFMissão:"+.@input$+"^000000";
				mes "De qualquer forma, você terminou de se registrar para uma nova missão.";
				next;
				mes .np$;
				mes "As missões estão totalmente reservadas. Você tem que esperar por um local para limpar.";
				mes "Por favor, aguarde até a sua vez.";
				next;
			}
			break;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				mes .np$;
				mes "Gostaria de tentar algumas missões como membro do Éden?";
				next;
				mes .np$;
				mes "Verifique o lado direito do quadro de avisos, existem vários tipos de missões esperando por você.";
				mes "Por favor, leia atentamente a lista de missões e as qualificações e escolha uma delas.";
				next;
			}
			else {
				mes .np$;
				mes "Você precisa se juntar ao Grupo Éden primeiro se quiser fazer algumas missões.";
				close;
			}
			break;
		case 5:
			mes .np$;
			mes "Se tiver alguma dúvida volte novamente.";
			close;
		}
	}
	close;
}

-	script	Oficial de Teletransporte do Éden#0::eto	-1,{
	set .np$,"^00B2EE[ Oficial de Teletransporte do Éden ]^000000";
	mes .np$;
	mes "Aqueles que estão vagando por este mundo estranho com olhos sem rumo escutam e vêm a mim!";
	mes "O Grupo Éden está aqui para te ajudar e vai te mostrar o grande futuro.";
	next;
	mes .np$;
	mes "Você não tem nada a fazer além de perder seu tempo?";
	mes "Você está ansioso para fazer algo bom, mas ninguém lhe oferece trabalho?";
	mes "Você gostaria de ser um solucionador de problemas?";
	mes "O Grupo Éden está aqui para resolver o seu problema.";
	next;
	switch (select("^3CB371[»]^000000 Ir para o Grupo Éden:^ff0000[»]^000000 Não quero mais falar.")) {
	case 1:
		mes .np$;
		mes "Vamos para nossa base secreta!";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes .np$;
		mes "Se você não sabe o que fazer para o seu futuro, não hesite em me dizer. É só passar e falar comigo! OK?";
		close;
	}
}
prontera,124,76,3	duplicate(eto)	Oficial de Teletransporte do Éden#1	729
moc_ruins,68,164,3	duplicate(eto)	Oficial de Teletransporte do Éden#2	729
geffen,132,66,3	duplicate(eto)	Oficial de Teletransporte do Éden#3	729
alberta,124,67,3	duplicate(eto)	Oficial de Teletransporte do Éden#4	729
aldebaran,133,119,5	duplicate(eto)	Oficial de Teletransporte do Éden#5	729
izlude_in,68,162,1	duplicate(eto)	Oficial de Teletransporte do Éden#6	729
prt_church,103,78,3	duplicate(eto)	Oficial de Teletransporte do Éden#7	729
geffen_in,160,104,5	duplicate(eto)	Oficial de Teletransporte do Éden#8	729
moc_prydb1,53,126,3	duplicate(eto)	Oficial de Teletransporte do Éden#9	729
alberta_in,75,39,3	duplicate(eto)	Oficial de Teletransporte do Éden#10	729
payon_in02,58,58,1	duplicate(eto)	Oficial de Teletransporte do Éden#11	729
payon,177,111,3	duplicate(eto)	Oficial de Teletransporte do Éden#12	729
que_ng,33,73,3	duplicate(eto)	Oficial de Teletransporte do Éden#13	729
que_ng,144,161,5	duplicate(eto)	Oficial de Teletransporte do Éden#14	729
yuno,144,189,5	duplicate(eto)	Oficial de Teletransporte do Éden#15	729
rachel,125,144,3	duplicate(eto)	Oficial de Teletransporte do Éden#16	729
comodo,202,151,4	duplicate(eto)	Oficial de Teletransporte do Éden#17	729
hugel,93,153,4	duplicate(eto)	Oficial de Teletransporte do Éden#18	729
veins,220,109,4	duplicate(eto)	Oficial de Teletransporte do Éden#19	729
einbroch,250,211,4	duplicate(eto)	Oficial de Teletransporte do Éden#20	729
lighthalzen,164,86,4	duplicate(eto)	Oficial de Teletransporte do Éden#21	729
amatsu,100,145,5	duplicate(eto)	Oficial de Teletransporte do Éden#22	729
ayothaya,221,191,3	duplicate(eto)	Oficial de Teletransporte do Éden#23	729
louyang,224,107,3	duplicate(eto)	Oficial de Teletransporte do Éden#24	729
gonryun,162,122,5	duplicate(eto)	Oficial de Teletransporte do Éden#25	729
moscovia,209,197,5	duplicate(eto)	Oficial de Teletransporte do Éden#26	729
brasilis,191,224,3	duplicate(eto)	Oficial de Teletransporte do Éden#27	729
dewata,192,193,5	duplicate(eto)	Oficial de Teletransporte do Éden#28	729
morocc,161,97,5	duplicate(eto)	Oficial de Teletransporte do Éden#29	729
izlude,131,148,4	duplicate(eto)	Oficial de Teletransporte do Éden#30	729
izlude_a,131,148,4	duplicate(eto)	Oficial de Teletransporte do Éden#30::eto_iz_a	729
izlude_b,131,148,4	duplicate(eto)	Oficial de Teletransporte do Éden#30::eto_iz_b	729
izlude_c,131,148,4	duplicate(eto)	Oficial de Teletransporte do Éden#30::eto_iz_c	729
izlude_d,131,148,4	duplicate(eto)	Oficial de Teletransporte do Éden#30::eto_iz_d	729
umbala,105,158,3	duplicate(eto)	Oficial de Teletransporte do Éden#31	729
malaya,225,218,1	duplicate(eto)	Oficial de Teletransporte do Éden#32	729
verus04,116,243,0	duplicate(eto)	Oficial de Teletransporte do Éden#33	729

moc_para01,30,10,0	script	#eden_out	45,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;	// Old coordinates: (127,142)
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		case 33: warp "verus04",115,243; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	111,{
	set .np$,"^00B2EE[ Lime Evenor ]^000000";
	if (callfunc("F_HasEdenGroupMark")) {
		mes .np$;
		mes "Ah, é um lugar exclusivo só para membros do Éden.";
		mes "Se você é um membro, pode vir quando quiser!";
		next;
		switch (select("^3CB371[»]^000000 Entrar:^ff0000[»]^000000 Nada")) {
		case 1:
			mes "Esta porta está lindamente decorada, mas parece um pouco pesada demais.";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes .np$;
			mes "Bem, se você não está interessado.";
			close;
		}
	}
	mes .np$;
	mes "Oh, este é um lugar exclusivo apenas para membros do Éden.";
	mes "Se você quiser entrar, você tem que se juntar ao Grupo Éden.";
	close;
}

moc_para01,16,22,7	script	Velho Aventureiro#eden	900,{
	set .np$,"^00B2EE[ Velho Aventureiro ]^000000";
	mes .np$;
	mes "Duro, sou forte o suficiente, mesmo que pareça velho! Qual é o nome dela? Lima Evenor? Ela apenas me ignora como se eu fosse algum tipo de velho bastardo senil.";
	next;
	mes .np$;
	mes "Eu vim aqui para fazer parte do Grupo Éden! eu não mereço isso!";
	mes "Você também acha que eu pareço senil?";
	close;
}

moc_para01,182,48,3	script	Chefe do Éden#eden	886,{
	set .np$,"^00B2EE[ Chefe do Éden ]^000000";
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes .np$;
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion ET_CRY;
	mes .np$;
	mes "Ah, tiro! Quantos ovos eu fiz?";
	mes "Uau!!! Eu esqueci totalmente! tenho que contar novamente!";
	next;
	emotion ET_CRY;
	mes .np$;
	mes "Por que você me pediu para fazer isso? Que missão é essa?";
	next;
	emotion ET_CRY;
	mes .np$;
	mes "O que você vai fazer com todos esses ovos fritos? Tenho certeza que você não pode comer todos eles. Você só quer me incomodar, certo?";
	close;
}

moc_para01,172,28,5	script	Estagiário do Éden#eden	883,{
	set .np$,"^00B2EE[ Estagiário do Éden ]^000000";
	mes .np$;
	mes "Na verdade, percebi que...";
	next;
	mes .np$;
	mes "Temos que trabalhar muito, a menos que você queira falhar.";
	next;
	mes .np$;
	mes "Mas, eu nunca aprendi sobre produtos farmacêuticos ainda.";
	next;
	mes .np$;
	mes "Eu vou falhar. eu não posso fazer isso.";
	close;
}

moc_para01,20,35,5	script	Assistente Neede	814,{
	set .np$,"^00B2EE[ Neede ]^000000";
	set .np1$,"^00B2EE[ Lime Evenor ]^000000";
	mes .np$;
	mes "Eu me tornei membro do Eden há vários anos, mas parece que foi ontem.";
	mes "Eu tenho feito muito trabalho, então nem percebi quantos anos passei aqui.";
	next;
	mes .np$;
	mes "Isso me lembra que era como uma guerra todos os dias.";
	next;
	mes .np$;
	mes "O chefe do ^3131FF restaurante do lado direito^000000 usa muito tempero toda vez que cozinha, o cheiro está por todo o saguão. Temos que abrir a janela regularmente porque não suportamos o cheiro!";
	next;
	mes .np$;
	mes "E você vê o ^3131FFginásio^000000 na cruz do ^3131FFo lado norte do saguão^000000? Eles sempre gritam quando se exercitam, eu posso até ouvi-los daqui!";
	next;
	emotion ET_ANGER;
	mes .np$;
	mes "Além disso, a garota esquisita do ^3131FFsegundo andar^000000 está chorando o tempo todo. Seus gritos me deixam louco.";
	next;
	mes .np$;
	mes "Ah, aquela garota esquisita é na verdade nossa chefe...";
	next;
	mes .np$;
	mes "Eu não sei o que ela tem feito. Mas ela chora e grita o tempo todo. Ah, às vezes eu ouço 'Bang!', sons altos e estrondosos... não sei de onde veio.";
	next;
	emotion ET_HUK;
	mes .np$;
	mes "Espere!";
	mes "Shh! É um segredo, você não pode contar a ninguém!";
	next;
	mes .np$;
	emotion ET_OHNO;
	mes "Eu não confio em Evenor. Ela nunca mostra seus sentimentos... ela vai me delatar para meu chefe.";
	next;
	emotion ET_HNG, getnpcid(0, "Secretário Lime Evenor");
	mes .np1$;
	mes "Eu não sou esse tipo de garota.";
	next;
	emotion ET_KEK;
	mes .np$;
	mes "Huh? Você ouviu isso? Poxa~!";
	close;
}

function	script	F_HasEdenGroupMark	{
	return (countitem(6219) > 0 || countitem(22508) > 0);
}
