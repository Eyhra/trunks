//===== rAthena Script =======================================
//= Eden Group Quests - Mission [41 - 55]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 41 - 55.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.4 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,40,38,3	script	Placa de Missão[41-55]	857,{
	set .np$,"^00B2EE[ Placa de Missão ]^000000";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Você não é um membro do grupo Éden. Você não está qualificado para acessar o quadro de avisos.";
		close;
	}
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Espere um segundo!!";
		mes "Você não pode receber mais itens porque está carregando muitos itens. Por favor, tente novamente depois de aliviar.^000000";
		close;
	}
	if (checkquest(12088) == -1) {
		if (BaseLevel < 41 || BaseLevel > 55) {
			mes "Quadro de avisos de missão para aventureiros Lv41~55.";
			close;
		}
		mes .np$;
		mes "Quadro de avisos da missão.";
		next;
		if(select("^3CB371[»]^000000 Confira as Missões:^ff0000[»]^000000 Cancelar") == 2) {
			mes "^000077OK. Vamos verificar as missões da próxima vez.^000000.";
			close;
		}
		switch(select("Caça ao Sapo Thara:Caça ao Soldadinho:Caça aoKukre:Caça ao Bebê Orc:Caça ao Orc Guerreiro:Caça ao Vadon:Caça ao Megalodon:Caça ao Marse:Caça ao Cornutus:Caça ao Myst:Caça ao Shellfish:Caça ao Marine Sphere")) {
			case 1: callsub L_Quest,12072,"Você já viu um sapo vermelho? Chama-se Thara Frog e é muito raro. Estamos tentando verificar onde ela habita. Então, por favor, cace 20 Sapos Thara.";
			case 2: callsub L_Quest,12073,"Ouvi falar de alguns brinquedos atacando pessoas. É ridículo! Vamos mostrar a eles nosso poder! Temos que caçar 15 Soldadinhos!";
			case 3: callsub L_Quest,12074,"Alguém foi assaltado perto da praia! O possível suspeito é um monstro criminoso chamado Kukre. Por favor, cace 30 Kukre.";
			case 4: callsub L_Quest,12075,"Os bebês orcs estão ficando mais violentos hoje em dia. Por favor, cace 15 bebês Orcs.";
			case 5: callsub L_Quest,12076,"Orc Warriors estão aumentando sua agressividade. Por favor, cace 20 Orcs Guerreiros.";
			case 6: callsub L_Quest,12077,"Você já viu caranguejos com casca grossa? Eles são Vadons. Por favor, cace 15 Vadons.";
			case 7: callsub L_Quest,12078,"Peixes mortos estão por toda a praia e está piorando. Por favor, cace 30 Megalodon.";
			case 8: callsub L_Quest,12079,"Você pode caçar Marse? Eu preciso de 15 Marse para o meu estudo. Por favor me ajude.";
			case 9: callsub L_Quest,12080,"Um idiota Cornutus mordeu o pé do prefeito. O prefeito está chateado, então precisamos caçar 20 Cornutus para deixá-lo feliz!";
			case 10: callsub L_Quest,12081,"As pessoas estão pirando por causa de Myst. Parece um fantasma, então assusta as pessoas. Por favor, cace 15 Myst.";
			case 11: callsub L_Quest,12082,"Não pergunte por que, mas por favor, cace 30 mariscos. É uma missão secreta!";
			case 12: callsub L_Quest,12083,"Esfera Marinha? Você sabe o que é uma Esfera Marinha? Eu tenho uma missão para você caçar 15 Esferas Marinhas, então, por favor, faça isso.";
			case 13: callsub L_Quest,12084,"O prefeito quer comer um delicioso prato de peixe. Phen será o ingrediente perfeito para isso. Por favor, cace 20 Phen!";
			case 14: callsub L_Quest,12085,"Monstros Orcs mortos voltaram para nossa cidade e eles reviveram como Orcs Esqueletos. Estamos no caos! Por favor, cace 15 Orc Skeleton.";
			case 15: callsub L_Quest,12086,"Alguns monstros estranhos chamados Zenorc estão nos atacando. Por favor, cace 30 Zenorc.";
			case 16: callsub L_Quest,12087,"Esteja preparado! É uma missão cruel! Caça 15 Múmias!! Tentei uma vez, mas não consegui. É muito assustador!";
		}
		end;
	}
	setarray .@Exp[0],9600,7200,15750,7875,11100,8550,17100,9000,12300,9900,20250,10350,13800,11025,22500,11250;
	for(set .@i,0; .@i<16; set .@i,.@i+1)
		if (checkquest(12072+.@i,HUNTING) == 2) {
			mes .np$;
			mes "Você completou a missão.";
			mes "Aqui está sua recompensa.";
			erasequest 12088;
			erasequest 12072+.@i;
			specialeffect2 EF_STEAL;
			getexp .@Exp[.@i],0;
			close;
		}
	if ((checkquest(12072,HUNTING) == 0) && (checkquest(12073,HUNTING) == 0) && (checkquest(12074,HUNTING) == 0) && (checkquest(12075,HUNTING) == 0) && (checkquest(12076,HUNTING) == 0) && (checkquest(12077,HUNTING) == 0) && (checkquest(12078,HUNTING) == 0) && (checkquest(12079,HUNTING) == 0) && (checkquest(12080,HUNTING) == 0) && (checkquest(12081,HUNTING) == 0) && (checkquest(12082,HUNTING) == 0) && (checkquest(12083,HUNTING) == 0) && (checkquest(12084,HUNTING) == 0) && (checkquest(12085,HUNTING) == 0) && (checkquest(12086,HUNTING) == 0) && (checkquest(12087,HUNTING) == 0)) {
		mes "Agora você pode ver o quadro de avisos.";
		erasequest 12088;
		close;
	}
	mes "Não há nada de especial para fazer.";
	close;
L_Quest:
	mes .np$;
	mes getarg(1);
	next;
	if(select("^3CB371[»]^000000 Aceitar a Missão:^ff0000[»]^000000 Cancelar") == 1) {
		mes .np$;
		mes "Você aceitou a missão. Para mais detalhes, pressione as teclas 'alt + u', você pode obter informações na janela de missões.";
		setquest 12088;
		setquest getarg(0);
	}
	close;
}
