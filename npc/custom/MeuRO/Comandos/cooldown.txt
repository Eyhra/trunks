
function	script	Get_Time2Str	{
	.@Time_Left = getarg(0) - gettimetick(2);	
	.@Month = .@Time_Left / 2592000;
	.@Time_Left = .@Time_Left - (.@Month * 2592000);
	.@Days = .@Time_Left / 86400;
	.@Time_Left = .@Time_Left - (.@Days * 86400);
	.@Hours = .@Time_Left / 3600;
	.@Time_Left = .@Time_Left - (.@Hours * 3600);
	.@Minutes = .@Time_Left / 60;
	.@Time_Left = .@Time_Left - (.@Minutes * 60);
	
	.@Time$ = "";
	if( .@Month > 1 )
		.@Time$ = .@Time$ + .@Month + " Meses, ";
	else if( .@Month > 0 )
		.@Time$ = .@Time$ + .@Month + " Mes, ";
	else if( .@Month < 1 )
		.@Time$ = .@Time$ + "";
	
	if( .@Days > 1 )
		.@Time$ = .@Time$ + .@Days + " Dias, ";
	else if( .@Days > 0 )
		.@Time$ = .@Time$ + .@Days + " Dia, ";
	else if( .@Days < 1 )
		.@Time$ = .@Time$ + "";

	if( .@Hours > 1 )
		.@Time$ = .@Time$ + .@Hours + " Horas, ";
	else if( .@Hours > 0 )
		.@Time$ = .@Time$ + .@Hours + " Hora, ";

	if( .@Minutes > 1 )
		.@Time$ = .@Time$ + .@Minutes + " Minutos, ";
	else if( .@Minutes > 0 )
		.@Time$ = .@Time$ + .@Minutes + " Minuto, ";

	if( .@Time_Left > 1 || .@Time_Left == 0 )
		.@Time$ = .@Time$ + .@Time_Left + " Segundos";
	else if( .@Time_Left == 1 )
		.@Time$ = .@Time$ + .@Time_Left + " Segundo";
	
	return .@Time$;
}

-	script	instance_setting	-1,{
	end;
	
OnInit:
	setarray .instances$,
	//= ID, "Name", Quest Cooldown ID or "Cooldown Variable", Minimum Level
	1,"Airship Assault",15050,99,
	2,"Endless Tower",60200,50,
	3,"Ghost Palace",1261,99,
	4,"Old Glastheim",12317,99,
	5,"Old Glastheim H",12334,99,
	6,"Orc's Memory",12059,75,
	7,"Sarah's Memory",15002,99,
	8,"Sealed Shrine",3040,75;
	.size = getarraysize(.instances$);
	bindatcmd "cd",strnpcinfo(3)+"::OnInstanceView";
	bindatcmd "cooldown",strnpcinfo(3)+"::OnInstanceView";
	bindatcmd "instaconfig",strnpcinfo(3)+"::OnSetting";
end;
	
OnInstanceView:
	.@char_id = getcharid(0);
	explode(.@T$,cd_data$,",");
	.@size = getarraysize(.@T$);
	if(!.@size){
		mes "[ ^0000CD Cconfigurações ^000000 ]";
		mes "Por favor, use ^0000CD@instaconfig^000000 para a configuração inicial do comando ^0000CD@cooldown/@cd^000000.";
		end;
	}
	dispbottom "|==================== Tempo de espera de instâncias ====================|",0xFFA500;
	for(.@i = 0; .@i < .@size; .@i++){
		explode(.@TT$,.@T$[.@i],":");
		.@id$ = .@TT$[0];
		.@display = atoi(.@TT$[1]);
		if(!.@display){
			.@index = inarray(.instances$,.@id$);
			if(.@index == -1){
				dispbottom "|==================== Fim ====================|",0xFFA500;
				end;
			}
			.@name$ = .instances$[.@index+1];
			.@min_level = atoi(.instances$[.@index+3]);
			.@cd_id = atoi(.instances$[.@index+2]);
			if(BaseLevel < .@min_level){
				.@display$ = sprintf("| %s : Indisponível - Nível mínimo : %d",.@name$,.@min_level);
				dispbottom .@display$,0xFF3300;
				continue;
			}
			if(.@cd_id == 0 || .@cd_id > 999999) // Unix Check
				.@cd_time = getd(.instances$[.@index+2]);
			else {
				switch(checkquest(.@cd_id,PLAYTIME)){
					case -1:
					case 2:
						.@display$ = sprintf("| %s : Disponível",.@name$);
						dispbottom .@display$,0x00FF55;
						continue;
						
					case 0:
					case 1:
						query_sql("SELECT `time` FROM `quest` WHERE `char_id` = '"+.@char_id+"' AND `quest_id` = '"+.@cd_id+"'",.@cd_time);
						break;
				}
			}
			if(.@cd_time > gettimetick(2)){
				.@display$ = sprintf("| %s : Cooldown : " + Get_Time2Str(.@cd_time),.@name$);
				dispbottom .@display$,0xFF6347;
				continue;
			}
		}
	}
	dispbottom "|===========================================================|",0xFFA500;
end;

OnSetting:
	function instance_cd_setting;
	instance_cd_setting();
end;


function	instance_cd_setting	{
	explode(.@T$,cd_data$,",");
	.@menu$ = "^ff0000[»]^000000 Sair:";
	for(.@i = 0; .@i < .size; .@i += 4){
		explode(.@TT$,.@T$[.@index],":");
		.@id = atoi(.@TT$[0]);
		.@display = atoi(.@TT$[1]);
		if(!.@display){
			.@menu$ += "^3CB371[»]^000000 ^00CC00" +.instances$[.@i+1] + "^000000:";
			.@menu[.@index] = 0;
		} else {
			.@menu$ += "^3CB371[»]^000000 ^CC0000" +.instances$[.@i+1] + "^000000:";
			.@menu[.@index] = 1;
		}
		.@name$[.@index] = .instances$[.@i+1];
		.@TT$[0] = atoi(.instances$[.@i]);
		.@TT$[1] = .@menu[.@index];
		.@T$[.@index] = implode(.@TT$,":");
		cd_data$ = implode(.@T$,",");
		.@index++;
	}
	mes "[ ^0000CD Cconfigurações ^000000 ]";
	mes "Selecione uma instância para modificar.";
	mes "^00CC00Verde^000000 nomes coloridos estão sendo exibidos no command ^0000CD@cooldown^000000.";
	next;
	.@s = select(.@menu$) - 1;
	switch(.@s){
		case 0:
			end;
			
		default:
			.@s -= 1;
			mes "[ ^0000CD Cconfigurações ^000000 ]";
			mes "Nome : ^0000CD" + .@name$[.@s] + "^000000";
			mes "Status : " + ((.@menu[.@s] > 0)?"^CC0000Escondido^000000":"^00CC00Visível^000000");
			next;
			if(select(((.@menu[.@s] > 0)?"^3CB371[»]^000000 Adicionar instância":"^DAA520[»]^000000 Remover instância"),"^ff0000[»]^000000 Voltar à lista") == 2)
				instance_cd_setting();
			mes "[ ^0000CD Cconfigurações ^000000 ]";
			mes "A instância ^0000CD" + .@name$[.@s] + "^000000 foi " + ((.@menu[.@s] > 0)?"adicionada":"removida") + " da lista de cooldown.";
			if(.@menu[.@s] == 0)
				.@menu[.@s] = true;
			else
				.@menu[.@s] = false;
			explode(.@TT$,.@T$[.@s],":");
			.@TT$[1] = .@menu[.@s];
			.@T$[.@s] = implode(.@TT$,":");
			cd_data$ = implode(.@T$,",");
			next;
			instance_cd_setting();
	}
	end;
}

}
