// Criador de Peco-Pecos (para Cavaleiros)
//============================================================
prontera,55,350,5	script	Criador de Peco-Pecos#knt	105,{
	if(Upper==0) set .@price,2500; //Peco Normal - Preço Padrão
	if(Upper==1) set .@price,2500; //Peco Blindado
	if(Upper==2) set .@price,2500; //Peco Bebê

	mes "[Criador de Peco-Pecos]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "Seja Bem-vind" + ((Sex) ? "o":"a") + ".";
		mes "Honrad" + ((Sex) ? "o":"a") + " Cavaleir" + ((Sex) ? "o":"a") + ", Você gostaria de alugar um Peco Peco?";
		mes "A taxa do aluguel é de "+.@price+" zenys.";
		next;
		switch(select("Alugar Peco Peco:Cancelar")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Criador de Peco-Pecos]";
				mes "Você não possui zenys suficientes.";
				mes "Você está...";
				mes "quebrado por acaso?";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Criador de Peco-Pecos]";
				mes "Sinto muito, mas você não pode utilizar esse serviço no momento.";
				mes "Por favor, aprenda a habilidade Montaria primeiro.";
				close;
			}
			else if(checkriding()) {
				mes "[Criador de Peco-Pecos]";
				mes "Você já está montado em um Peco Peco.";
				close;
			}
			else if(ismounting()) {
				mes "[Criador de Peco-Pecos]";
				mes "Por favor, remova a sua montaria atual primeiro.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Criador de Peco-Pecos]";
			mes "Entendo.";
			mes "Bem então, tenha um bom dia.";
			close;
		}
	} else {
		mes "Sinto muito, mas esses Peco Pecos estão somente Disponíveis para cavaleiros e Lordes.";
		close;
	}
}

// Grande Criador de Peco-Pecos (para Templários)
//============================================================
prontera,232,318,3	script	Criador de Peco-Pecos#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - Preço Padrão
	if(Upper==1) set .@price,3500; //Blindado Peco
	if(Upper==2) set .@price,3500; //Bebê Peco

	mes "[Criador de Peco-Pecos]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "Bem-vind" + ((Sex) ? "o":"a") + ", Templári" + ((Sex) ? "o":"a") + ".";
		else mes "Bem-vind" + ((Sex) ? "o":"a") + ", Paladin" + ((Sex) ? "o":"a") + ".";
		mes "Eu tenho um Peco Peco especial preparado para você.";
		mes "Alugue um aqui por "+.@price+" zenys.";
		next;
		switch(select("Alugar Peco Peco:Sair")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Criador de Peco-Pecos]";
				mes "Você não possui zenys suficientes.";
				mes "Caso queira alugar um Peco Peco por favor tenha em mãos "+.@price+" zenys...";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Criador de Peco-Pecos]";
				mes "Primeiro você precisa aprender a habilidade Montaria caso você queira alugar um de meus pecos.";
				close;
			}
			else if(checkriding()) {
				mes "[Criador de Peco-Pecos]";
				mes "Você já está montado em um Peco Peco.";
				close;
			}
			else if(ismounting()) {
				mes "[Criador de Peco-Pecos]";
				mes "Por favor, remova a sua montaria atual antes de alugar outra.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Criador de Peco-Pecos]";
			mes "Te vejo por ai.";
			close;
		}
	} else {
		mes "O que posso fazer por você?";
		mes "Tenha em mente que Este serviço de aluguel de peco pecos é restrito a Templários e Paladinos.";
		close;
	}
}

//Criador de Falcões
//============================================================
hu_in01,381,304,5	script	Criador de Falcões#hnt	105,{
	if(Upper==0) set .@price,2500; //Falcão Normal - Preço Padrão
	if(Upper==1) set .@price,2500; //Falcão de Cachicol
	if(Upper==2) set .@price,2500; //Falcão Bebê

	mes "[Criador de Falcões]";
	if (BaseJob == Job_Hunter) {
		if (checkwug()) {
			mes "Hm...";
			mes "Você não pode alugar um falcão até que você remova primeiro o seu Lobo!";
			close;
		}
		mes "Você precisa de um Falcão?";
		mes "Eu posso te alugar o seu próprio confiável Pássaro de Rapina por apenas "+.@price+" zenys~";
		next;
		switch(select("Alugar Falcão:Cancelar")) {
		case 1:
			if (checkwug()) {
				mes "[Criador de Falcões]";
				mes "Hm...";
				mes "Você não pode alugar um falcão até que você remova primeiro o seu Lobo!";
				close;
			}
			if (Zeny < .@price) {
				mes "[Criador de Falcões]";
				mes "O que é isso?";
				mes "Você não tem zenys suficientes?!";
				mes "É melhor você começar a caçar dinheiro em vez de monstros~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Criador de Falcões]";
				mes "Poxa~";
				mes "Eu não posso te alugar um falcão no momento pois você não possui a Habilidade Adestrar Falcão aprendida o momento.";
				mes "Lide com isso.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Criador de Falcões]";
				mes "Hm...";
				mes "Você já possui um Falcão.";
				mes "Tem certeza de que não está ficando cego?";
				close;
			}
			set Zeny, Zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Criador de Falcões]";
			mes "Espere, onde pensa que você vai? Estes falcões são do mais alto nível, Eu garanto! Vamos lá alugue um.";
			close;
		}
	} else {
		mes "Jovem tolo!";
		mes "Falcões só podem serem usados por Caçadores e Atiradores de Elite, capish?";
		mes "...Heh heh, está com inveja não é?";
		close;
	}
}